package QBitcoin::Block::Validate;
use warnings;
use strict;
use feature 'state';

# Check block chain
# Check block time
# Validate all transactions
# Total amount of all fees (except coinbase) should be equal to the (minus) reward for the block validation

use Time::HiRes;
use List::Util qw(sum0);
use QBitcoin::Const;
use QBitcoin::Config;
use QBitcoin::BlockchainParams;
use QBitcoin::ProtocolState qw(skip_scripts);
use QBitcoin::CheckPoints qw(checkpoint_hash);
use QBitcoin::ValueUpgraded qw(level_by_total downgrade_value);
use QBitcoin::Log;
use QBitcoin::Transaction;
use QBitcoin::Slashing;
use QBitcoin::MinFee qw(min_fee);
use Role::Tiny;

sub validate {
    my $block = shift;

    my $now = Time::HiRes::time();
    $now >= $block->time
        or return "Block time " . $block->time . " is too early for now";
    # Block time must be exactly on a timeslot boundary: the stake signature commits to
    # timeslot($block->time) (see Block::sign_data), so a sub-slot time would make the
    # signed slot ambiguous and break slashing's "same timeslot" attribution.
    $block->time == timeslot($block->time)
        or return "Block time " . $block->time . " is not aligned to a timeslot";
    my $merkle_root = $block->calculate_merkle_root;
    $block->merkle_root eq $merkle_root
        or return "Incorrect merkle root " . unpack("H*", $block->merkle_root) . " expected " . unpack("H*", $merkle_root);
    if (my $cp_hash = checkpoint_hash($block->height)) {
        $block->hash eq $cp_hash
            or return "Block hash at checkpoint height " . $block->height . " does not match checkpoint";
    }
    if (!$block->prev_hash || $block->prev_hash eq ZERO_HASH) {
        if (!$config->{regtest}) {
            $block->upgraded    = 0; # Genesis block has no upgrades
            $block->downgraded  = 0; # Genesis block has no downgrades
            $block->downgrade_pinned = 0;
            $block->reward_fund = 0;
            $block->size = sum0(map { $_->size } @{$block->transactions});
            $block->min_fee = 0;
            if (GENESIS_HASH) {
                $block->hash eq GENESIS_HASH
                    or return "Incorrect genesis block hash " . unpack("H*", $block->hash) . ", must be " . unpack("H*", GENESIS_HASH);
                return ""; # Not needed to validate genesis block with correct hash
            }
        }
    }
    if ($block->prev_block) {
        my $timeslot = int(($block->time - GENESIS_TIME) / BLOCK_INTERVAL);
        my $prev_timeslot = int(($block->prev_block->time - GENESIS_TIME) / BLOCK_INTERVAL);
        $timeslot > $prev_timeslot
            or return "Block time " . $block->time . " is not after previous block time " . $block->prev_block->time;
        int($prev_timeslot / FORCE_BLOCKS) == int(($timeslot - 1) / FORCE_BLOCKS)
            or return "Forced block missed";
    }
    if (!@{$block->transactions} && (timeslot($block->time) - GENESIS_TIME) / BLOCK_INTERVAL % FORCE_BLOCKS) {
        return "Empty block";
    }
    if (@{$block->transactions} > MAX_TX_IN_BLOCK) {
        return "Too many transactions in block: " . @{$block->transactions} . " (max " . MAX_TX_IN_BLOCK . ")";
    }
    my $block_size = sum0(map { $_->size } @{$block->transactions});
    if ($block_size > MAX_BLOCK_SIZE) {
        return "Block size is too big: $block_size (max " . MAX_BLOCK_SIZE . ")";
    }
    my $fee = 0;
    my $stake_reward = 0;
    my %tx_in_block;
    my $empty_tx = 0;
    my $low_fee_tx = 0;
    my $min_fee = min_fee($block->prev_block, $block_size);
    my $upgraded   = $block->prev_block ? $block->prev_block->upgraded   // 0 : 0;
    my $downgraded = $block->prev_block ? $block->prev_block->downgraded // 0 : 0;
    my $downgrade_pinned = $block->prev_block ? $block->prev_block->downgrade_pinned // 0 : 0;
    my $min_block_fee;
    my $was_standard;
    my $was_slashing;
    my $can_consume = 1; # Can validator consume transaction fee? No if stake transaction has no inputs
    my $was_burn;
    my $was_downgrade;
    for (my $num = 0; $num < @{$block->transactions}; $num++) {
        my $transaction = $block->transactions->[$num];
        if ($tx_in_block{$transaction->hash}++) {
            return "Transaction " . $transaction->hash_str . " included in the block twice";
        }
        if ($transaction->valid_for_block($block) != 0) {
            return "Transaction " . $transaction->hash_str . " can't be included in block " . $block->height;
        }
        if (UPGRADE_POW && $transaction->coins_created) {
            if ($upgraded >= UPGRADE_MAX_VALUE) {
                return "Coinbase transaction " . $transaction->hash_str . " rejected: upgrade threshold reached";
            }
            if ($transaction->upgrade_level != level_by_total($upgraded += $transaction->up->value_btc)) {
                return "Incorrect upgrade level for transaction " . $transaction->hash_str;
            }
        }
        # NB: we do not check that the $txin is unspent in this branch;
        # we will check this on include this block into the best branch
        if ($transaction->is_coinbase) {
            $fee += $transaction->fee;
            if ($was_standard && !$config->{regtest}) {
                return "Coinbase transaction " . $transaction->hash_str . " must not be after standard transaction $was_standard";
            }
            if ($was_burn && !$config->{regtest}) {
                return "Coinbase transaction " . $transaction->hash_str . " must not be after burn transaction $was_burn";
            }
            if ($was_downgrade && !$config->{regtest}) {
                return "Coinbase transaction " . $transaction->hash_str . " must not be after downgrade transaction $was_downgrade";
            }
            if ($was_slashing && !$config->{regtest}) {
                return "Coinbase transaction " . $transaction->hash_str . " must not be after slashing transaction $was_slashing";
            }
        }
        elsif ($transaction->is_burn) {
            if ($was_standard && !$config->{regtest}) {
                return "Burn transaction " . $transaction->hash_str . " must not be after standard transaction $was_standard";
            }
            if ($was_slashing && !$config->{regtest}) {
                return "Burn transaction " . $transaction->hash_str . " must not be after slashing transaction $was_slashing";
            }
            # Burn finalizes a downgrade: the freeze value (fee) is destroyed and the
            # corresponding BTC is released from the peg. upgraded uses the full
            # (fee-free) downgrade_value; the service fee only reduces what the user
            # actually receives, checked separately against the SPV output value.
            my $btc_value = downgrade_value($transaction->fee, $upgraded);
            $upgraded   -= $btc_value;
            $downgraded += $btc_value;
            $was_burn = $transaction->hash_str;
        }
        elsif ($transaction->is_downgrade) {
            # The downgrade only pins the branch (heavy weight) and reserves the
            # conversion; the peg total changes when the matching burn is included
            # (or is left unchanged if the user later reclaims).
            if ($was_standard && !$config->{regtest}) {
                return "Downgrade transaction " . $transaction->hash_str . " must not be after standard transaction $was_standard";
            }
            if ($was_slashing && !$config->{regtest}) {
                return "Downgrade transaction " . $transaction->hash_str . " must not be after slashing transaction $was_slashing";
            }
            $downgrade_pinned += sum0(map { $_->value } @{$transaction->out});
            $was_downgrade = $transaction->hash_str;
        }
        elsif ($transaction->is_slashing) {
            $fee += $transaction->fee;
            if ($was_standard && !$config->{regtest}) {
                return "Slashing transaction " . $transaction->hash_str . " must not be after standard transaction $was_standard";
            }
            $was_slashing = $transaction->hash_str;
        }
        elsif ($transaction->is_standard || $transaction->is_tokens) {
            $fee += $transaction->fee;
            my $tx_fee_per_kb = int($transaction->fee * 1024 / $transaction->size);
            if ($tx_fee_per_kb < $min_fee || $transaction->fee == 0) {
                if (++$low_fee_tx > MAX_EMPTY_TX_IN_BLOCK) {
                    return "Too many low-fee transactions";
                }
                ++$empty_tx if $transaction->fee == 0;
            }
            else {
                $min_block_fee = $tx_fee_per_kb if !defined($min_block_fee) || $tx_fee_per_kb < $min_block_fee;
            }
            $was_standard = $transaction->hash_str;
        }
        elsif ($transaction->is_stake) {
            if ($num > 0) {
                return "Stake transaction " . $transaction->hash_str . " must be the first transaction in the block";
            }
            # Equivocated stake: we hold a slashing tx proving this UTXO signed another
            # block in this timeslot. Such a block is invalid no matter how heavy its
            # branch is, so we never select it (and drop it if already best).
            if (!skip_scripts() && QBitcoin::Slashing->is_banned_stake($transaction, timeslot($block->time))) {
                return "Stake transaction " . $transaction->hash_str . " is equivocated (slashed); block invalid";
            }
            $stake_reward = -$transaction->fee; # fee is negative for stake transactions
        }
        else {
            return "Transaction " . $transaction->hash_str . " is not a coinbase, downgrade, burn, stake or standard transaction";
        }
        if (!@{$transaction->in} && !$transaction->coins_created) {
            if ($num > 0) {
                return "Transaction " . $transaction->hash_str . " has no inputs";
            }
            # Stake transaction without inputs allowed only if the block has no (non-coinbase) transactions with positive fee
            $can_consume = 0;
        }
        elsif (!$can_consume && $transaction->fee > 0 && !$transaction->coins_created) {
            return "Transaction " . $transaction->hash_str . " has fee but block validator can't consume it";
        }
    }
    # After UPGRADE_FINISHED we can have no btc blocks and do not know when the upgrade was stopped,
    # so trust the stake reward in this case (until checkpoint)
    my $block_reward = skip_scripts() ? $stake_reward : (ref $block)->reward($block->prev_block, $fee, $block->time);
    # There are no block rewards for empty blocks
    if ($empty_tx >= @{$block->transactions} - 1 && (timeslot($block->time) - GENESIS_TIME) / BLOCK_INTERVAL % FORCE_BLOCKS) {
        $block_reward = 0;
    }
    $stake_reward == $block_reward
        or return "Incorrect stake reward for block " . $block->height . ": $stake_reward, expected $block_reward";
    $block->upgraded   = $upgraded;
    $block->downgraded = $downgraded;
    $block->downgrade_pinned = $downgrade_pinned;
    my $static_reward = $block_reward ? (ref $block)->static_reward($block->prev_block, $block->time) : 0;
    $block->reward_fund = $block->prev_block ? $block->prev_block->reward_fund + $fee + $static_reward - $block_reward : 0;
    $block->size = $block_size;
    $block->min_fee = $block_size > MAX_BLOCK_SIZE / 2 ? $min_block_fee : $min_fee;
    return "";
}

sub validate_chain {
    my $block = shift;

    my $fail_tx;
    for (my $num = 0; $num < @{$block->transactions}; $num++) {
        my $tx = $block->transactions->[$num];
        if (defined($tx->block_height) && $tx->block_height != $block->height) {
            Warningf("Transaction %s included in blocks %u and %u", $tx->hash_str, $tx->block_height, $block->height);
            $fail_tx = $tx->hash;
            last;
        }
        if (my $coinbase = $tx->up) {
            if ($coinbase->tx_out && $coinbase->tx_out ne $tx->hash) {
                Warningf("Coinbase transaction %s has already been spent in %s", $tx->hash_str, $coinbase->tx_out_str);
                $fail_tx = $tx->hash;
                last;
            }
            # Strict coinbase ordering: previous coinbase in BTC order must be already included in this branch
            if (!skip_scripts() && (my $prev = $coinbase->prev())) {
                if (!$prev->{confirmed}) {
                    Warningf("Coinbase %u:%u:%u has predecessor %u:%u:%u but it's not included in this branch",
                        $coinbase->btc_block_height, $coinbase->btc_tx_num, $coinbase->btc_out_num,
                        $prev->{btc_block_height}, $prev->{btc_tx_num}, $prev->{btc_out_num});
                    $fail_tx = $tx->hash;
                    last;
                }
            }
        }
        if (!skip_scripts()) {
            foreach my $in (@{$tx->in}) {
                my $txo = $in->{txo};
                # It's possible that $txo->tx_out already set for rebuild blockchain loaded from local database
                if ($txo->tx_out && $txo->tx_out ne $tx->hash) {
                    # double-spend; drop this branch, return to old best branch and decrease reputation for peer $block->received_from
                    Warningf("Double spend for transaction output %s:%u: first in transaction %s, second in %s, block from %s",
                        $txo->tx_in_str, $txo->num, $txo->tx_out_str, $tx->hash_str,
                        $block->received_from ? $block->received_from->peer->id : "me");
                    $fail_tx = $tx->hash;
                    last;
                }
                elsif (my $tx_in = QBitcoin::Transaction->get($txo->tx_in)) {
                    # Transaction with this output must be already confirmed (in the same best branch)
                    # Stored (not cached) transactions are always confirmed, not needed to load them
                    if (!defined($tx_in->block_height)) {
                        Warningf("Unconfirmed input %s:%u for transaction %s, block from %s",
                            $txo->tx_in_str, $txo->num, $tx->hash_str,
                            $block->received_from ? $block->received_from->peer->id : "me");
                        $fail_tx = $tx->hash;
                        last;
                    }
                }
            }
        }
        last if $fail_tx;
        $tx->confirm($block, $num) if $tx->is_cached;
    }

    if (!$fail_tx && !skip_scripts()) {
        my $self_weight = $block->self_weight;
        if (!defined($self_weight)) {
            $fail_tx = "block"; # does not match any transaction hash
        }
        elsif ($self_weight + ( $block->prev_block ? $block->prev_block->weight : 0 ) != $block->weight) {
            Warningf("Incorrect weight for block %s: %Lu != %Lu", $block->hash_str,
                $block->weight, $self_weight + ( $block->prev_block ? $block->prev_block->weight : 0 ));
            $fail_tx = "block";
        }
    }
    if ($fail_tx) {
        # It's not possible to include a tx twice in the same block, it's checked on block validation
        foreach my $tx (@{$block->transactions}) { # TODO: Do we need reverse order for unconfirm here?
            last if $fail_tx eq $tx->hash;
            $tx->unconfirm($block) if $tx->is_cached;
        }
    }
    return $fail_tx;
}

1;
