package QBitcoin::REST;
use warnings;
use strict;

# Esplora RESTful HTTP API
# https://github.com/blockstream/esplora/blob/master/API.md

# Cpanel::JSON::XS (unlike JSON::XS) encodes an integer that was later used in
# string context as a number, so stale string flags on live objects do not turn
# numeric fields into JSON strings
use Cpanel::JSON::XS;
use Time::HiRes;
use List::Util qw(sum0);
use MIME::Base64 qw(decode_base64);
use HTTP::Headers;
use HTTP::Response;
use QBitcoin::Const;
use QBitcoin::Config;
use QBitcoin::BlockchainParams;
use QBitcoin::Log;
use QBitcoin::IP qw(ip_port_str);
use QBitcoin::Accessors qw(mk_accessors);
use QBitcoin::ORM qw(dbh);
use QBitcoin::Address qw(address_by_hash address_by_pubkey wallet_import_format wif_to_pk wif_decode delegation_import_format pubkeyhash_str pubkeyhash_by_str pubkeyhash_by_pubkey);
use QBitcoin::Script::Delegation qw(delegation_address);
use QBitcoin::MyAddress;
use QBitcoin::StakingKey;
use QBitcoin::Delegation;
use QBitcoin::Password;
use QBitcoin::Wallet;
use QBitcoin::Transaction;
use QBitcoin::Block;
use QBitcoin::TXO;
use QBitcoin::Utils qw(get_address_txs get_address_utxo utxo_tag get_address_reclaim_utxo address_stats all_tokens_balance get_tokens_txs get_tokens_info create_txo estimate_fees check_tx_tokens_balance);
use QBitcoin::Crypto qw(pk_import pk_alg generate_keypair hash256);
use QBitcoin::Generate;
use QBitcoin::Generate::Control;
use QBitcoin::ProtocolState qw(blockchain_synced btc_synced);
use QBitcoin::Coins;
use QBitcoin::ConnectionList;
use Bitcoin::Block;
use Bitcoin::Serialized;
use parent qw(QBitcoin::HTTP);

use constant {
    FALSE => Cpanel::JSON::XS::false,
    TRUE  => Cpanel::JSON::XS::true,
};

use constant DEBUG_REST => 0;

mk_accessors(qw(cors));

my $JSON = Cpanel::JSON::XS->new;

sub type_id() { PROTOCOL_REST }

sub timeout {
    my $self = shift;
    my $time = shift // time();
    my $timeout = REST_TIMEOUT + $self->update_time - $time;
    if ($timeout < 0) {
        Infof("REST client timeout");
        $self->connection->disconnect;
        $timeout = 0;
    }
    return $timeout;
}

# All GET endpoints only read state; POST (tx send, wallet operations) modifies it
sub request_is_read_only {
    my $self = shift;
    my ($http_request) = @_;
    return $http_request->method eq "GET";
}

sub process_request {
    my $self = shift;
    my ($http_request) = @_;

    my @path = $http_request->uri->path_segments
        or return $self->http_response(404, "Unknown request");
    shift @path if @path && $path[0] eq "";
    return $self->http_response(404, "Unknown request") unless @path;
    DEBUG_REST && Debugf("REST request: /%s", join("/", @path));
    if ($path[0] eq "api") {
        $self->cors(1);
        shift @path; # remove "api"
        return $self->http_response(404, "Unknown request") unless @path;
        if ($path[0] eq "tx") {
            if ($http_request->method eq "POST") {
                @path == 1 or return $self->http_response(404, "Unknown request");
                return $self->tx_send($http_request);
            }
            ($path[1] && $path[1] =~ qr/^[0-9a-f]{64}\z/)
                or return $self->http_response(404, "Unknown request");
            my $tx = QBitcoin::Transaction->get_by_hash(pack("H*", $path[1]))
                or return $self->http_response(404, "Transaction not found");
            if (@path == 2) {
                return $self->http_ok(tx_obj($tx));
            }
            if (@path == 3) {
                if ($path[2] eq "status") {
                    return $self->http_ok(tx_status($tx));
                }
                elsif ($path[2] eq "hex") {
                    return $self->http_ok(unpack("H*", $tx->serialize));
                }
                elsif ($path[2] eq "raw") {
                    return $self->http_ok($tx->serialize);
                }
                elsif ($path[2] eq "outspends") {
                    my @out;
                    foreach my $out (@{$tx->out}) {
                        push @out, {
                            spent => $out->tx_out ? TRUE : FALSE,
                            $out->tx_out ? (
                                txid => unpack("H*", $out->tx_out),
                            ) : (),
                        };
                    }
                    return $self->http_ok(\@out);
                }
                elsif ($path[2] eq "merkleblock-proof") {
                    return $self->http_response(500, "Unimplemented");
                }
                elsif ($path[2] eq "merkle-proof") {
                    $tx->block_height
                        or return $self->http_response(404, "Transaction unconfirmed");
                    my $res = merkle_proof($tx);
                    return $res ? $self->http_ok($res) : $self->http_response(500, "Something went wrong");
                }
                else {
                    return $self->http_response(404, "Unknown request");
                }
            }
            elsif (@path == 4) {
                if ($path[2] eq "outspend" && $path[3] =~ /^(?:0|[1-9][0-9]*)\z/) {
                    return $self->http_ok({
                        spent => $tx->out->[$path[3]]->tx_out ? TRUE : FALSE,
                        $tx->out->[$path[3]]->tx_out ? (
                            txid => unpack("H*", $tx->out->[$path[3]]->tx_out),
                        ) : (),
                    });
                }
                else {
                    return $self->http_response(404, "Unknown request");
                }
            }
            else {
                return $self->http_response(404, "Unknown request");
            }
        }
        elsif ($path[0] eq "address") {
            validate_address($path[1])
                or return $self->http_response(404, "Unknown request");
            if (@path == 2) {
                return $self->get_address_stats($path[1]);
            }
            elsif ($path[2] eq "txs") {
                return $self->list_address_txs($path[1], ($path[3] // "") eq "mempool" ? 0 : 25, ($path[3] // "") eq "chain" ? 0 : 50, $path[4]);
            }
            elsif ($path[2] eq "transfers") {
                validate_txid($path[3])
                    or return $self->http_response(404, "Unknown request");
                return $self->list_token_txs($path[1], $path[3], ($path[4] // "") eq "mempool" ? 0 : 25, ($path[4] // "") eq "chain" ? 0 : 50, $path[5]);
            }
            elsif ($path[2] eq "utxo") {
                @path == 3
                    or return $self->http_response(404, "Unknown request");
                return $self->get_address_unspent($path[1]);
            }
        }
        elsif ($path[0] eq "address-prefix") {
            return $self->http_response(500, "Unimplemented");
        }
        elsif ($path[0] eq "block") {
            ($path[1] && $path[1] =~ qr/^[0-9a-f]{64}\z/)
                or return $self->http_response(404, "Unknown request");
            my $block = $self->get_block_by_hash(pack("H*", $path[1]))
                or return $self->http_response(404, "Block not found");
            if (@path == 2) {
                return $self->http_ok(block_obj($block));
            }
            if ($path[2] eq "txids") {
                @path == 3
                    or return $self->http_response(404, "Unknown request");
                return $self->http_ok([ map { unpack("H*", $_) } @{$block->tx_hashes} ]);
            }
            if ($path[2] eq "txs") {
                my $start_ndx = $path[3] || 0;
                my @ret;
                if ($start_ndx < @{$block->transactions} && $start_ndx >= 0) {
                    my $end_ndx = $start_ndx + 24;
                    $end_ndx = @{$block->transactions}-1 if $end_ndx >= @{$block->transactions};
                    @ret = map { tx_obj($_) } @{$block->transactions}[$start_ndx .. $end_ndx];
                }
                return $self->http_ok(\@ret);
            }
            if ($path[2] eq "txid") {
                if ($path[3] >= @{$block->transactions} || $path[3] < 0) {
                    return $self->http_response(404, "Transaction not found");
                }
                return $self->http_ok(tx_obj($block->transactions->[$path[3]]));
            }
            if ($path[2] eq "raw") {
                return $self->http_ok($block->serialize);
            }
            if ($path[2] eq "header") {
                return $self->http_ok(unpack("H*", $block->serialize));
            }
            if ($path[2] eq "status") {
                my $best_block = block_by_height($block->height);
                my $is_best = $best_block && $best_block->hash eq $block->hash;
                my $next_best;
                if ($is_best && $block->height < QBitcoin::Block->blockchain_height) {
                    $next_best = block_by_height($block->height + 1);
                }
                return $self->http_ok({
                    in_best_chain => $is_best ? TRUE : FALSE,
                    height        => $block->height,
                    $next_best ? ( next_best => unpack("H*", $next_best->hash) ) : (),
                });
            }
            return $self->http_response(404, "Unknown request");
        }
        elsif ($path[0] eq "blocks") {
            if (@path == 1 || @path == 2) {
                return $self->get_blocks($path[1]);
            }
            (@path == 3 && $path[1] eq "tip")
                or return $self->http_response(404, "Unknown request");
            my $best_height = QBitcoin::Block->blockchain_height;
            my $block = QBitcoin::Block->best_block($best_height)
                or return $self->http_response(500, "No blocks loaded");
            if ($path[2] eq "height") {
                return $self->http_ok($block->height);
            }
            elsif ($path[2] eq "hash") {
                return $self->http_ok(unpack("H*", $block->hash));
            }
            else {
                return $self->http_response(404, "Unknown request");
            }
        }
        elsif ($path[0] eq "block-height") {
            (@path == 2 && $path[1] =~ /^(?:0|[1-9][0-9]*)\z/)
                or return $self->http_response(404, "Unknown request");
            my $block = block_by_height($path[1])
                or return $self->http_response(404, "Block not found");
            return $self->http_ok(unpack("H*", $block->hash));
        }
        elsif ($path[0] eq "mempool") {
            my @mempool = QBitcoin::Transaction->mempool_list();
            if (@path == 1) {
                return $self->http_ok({
                    count     => scalar(@mempool),
                    vsize     => sum0(map { $_->size } @mempool),
                    total_fee => sum0(map { $_->fee } @mempool),
                    # fee_histogram => ???, # TODO
                });
            }
            @path == 2
                or return $self->http_response(404, "Unknown request");
            if ($path[1] eq "txids") {
                return $self->http_ok([ map { unpack("H*", $_->hash) } @mempool ]);
            }
            elsif ($path[1] eq "recent") {
                my @mempool = sort { ($b->received_time // 0) <=> ($a->received_time // 0) } @mempool;
                return $self->http_ok([ map { tx_obj($_) } grep { defined } @mempool[0..9] ]);
            }
            else {
                return $self->http_response(404, "Unknown request");
            }
        }
        elsif ($path[0] eq "tokens-info") {
            (@path == 2 && $path[1] && $path[1] =~ qr/^[0-9a-f]{64}\z/)
                or return $self->http_response(404, "Unknown request");
            my $token_info = get_tokens_info(pack("H*", $path[1]))
                or return $self->http_response(404, "Token not found");
            return $self->http_ok($token_info);
        }
        elsif ($path[0] eq "fee-estimates") {
            my @targets = (1 .. 25, 144, 504, 1008);
            my ($result, $error) = estimate_fees(@targets);
            return $self->http_response(503, $error) if $error;
            return $self->http_ok($result);
        }
        elsif ($path[0] eq "status") {
            return $self->http_ok(node_status());
        }
        elsif ($path[0] eq "asset") {
            return $self->http_response(500, "Unimplemented");
        }
        elsif ($path[0] eq "assets") {
            return $self->http_response(500, "Unimplemented");
        }
        return $self->http_response(404, "Unknown request");
    }
    elsif ($path[0] eq "admin") {
        if (defined(my $deny = $self->check_access($http_request))) {
            return $deny;
        }
        shift @path; # remove "admin"
        return $self->http_response(404, "Unknown request") unless @path;
        if ($path[0] eq "status") {
            return $self->http_ok({ %{node_status()}, wallet => wallet_status() });
        }
        elsif ($path[0] eq "peers") {
            return $self->http_ok(peer_info());
        }
        elsif ($path[0] eq "password") {
            @path == 1
                or return $self->http_response(404, "Unknown request");
            if ($http_request->method eq "POST") {
                return $self->set_wallet_password($http_request);
            }
            return $self->http_ok({ password_set => QBitcoin::Password->is_set ? TRUE : FALSE });
        }
        elsif ($path[0] eq "generate") {
            @path == 1 && $http_request->method eq "POST"
                or return $self->http_response(404, "Unknown request");
            return $self->set_generate($http_request);
        }
        else {
            return $self->http_response(404, "Unknown request");
        }
    }
    elsif ($path[0] eq "wallet") {
        if (defined(my $deny = $self->check_access($http_request))) {
            return $deny;
        }
        shift @path; # remove "wallet"
        return $self->http_response(404, "Unknown request") unless @path;
        if ($path[0] eq "my_addresses") {
            my $genesis = QBitcoin::Coins->genesis_scripthashes // {};
            return $self->http_ok([
                map {
                    my $addr = $_;
                    my $delegated = $addr->is_delegation
                        ? (QBitcoin::Delegation->get_by_hash(scalar $addr->scripthash) ? "both" : "owner")
                        : undef;
                    +{
                        address => $addr->address,
                        staked  => ($addr->staked || ($delegated && $delegated eq "both")) ? TRUE : FALSE,
                        algo    => [ map { CRYPT_ALGO_NAMES->{$_} } $addr->algo ],
                        $delegated ? (delegation => $delegated) : (),
                        # Genesis-reward addresses are stake-only by consensus: not counted in the balance
                        grep({ $genesis->{$_} } $addr->scripthash) ? (stakeonly => TRUE) : (),
                        # last_used => ... # TODO
                    }
                } QBitcoin::MyAddress->my_address()
            ]);
        }
        elsif ($path[0] eq "my_address") {
            $http_request->method eq "POST"
                or return $self->http_response(404, "Unknown request");
            if (@path == 2) {
                if ($path[1] eq "new") {
                    my $content = length($http_request->decoded_content // "")
                        ? eval { $JSON->decode($http_request->decoded_content) } : {};
                    ref($content) eq "HASH"
                        or return $self->http_response(400, "Invalid request body");
                    my $algo = CRYPT_ALGO_ECDSA; # TODO: support multiple algorithms
                    my $keypair = generate_keypair($algo);
                    if (defined(my $delegate_str = $content->{delegate_pubkeyhash})) {
                        # A delegated-staking address: the new key spends it, the
                        # delegate's staking key can only stake it (see QBitcoin::Script::Delegation)
                        my $delegate_pubkeyhash = eval { pubkeyhash_by_str($delegate_str) }
                            or return $self->http_response(400, "Invalid delegate pubkeyhash");
                        my $pubkeyhash = pubkeyhash_by_pubkey($keypair->pubkey_by_privkey, $algo);
                        return $self->http_ok({
                            address     => delegation_address($pubkeyhash, $delegate_pubkeyhash),
                            private_key => delegation_import_format($keypair->pk_serialize, $delegate_pubkeyhash),
                            pubkeyhash  => pubkeyhash_str($pubkeyhash),
                        });
                    }
                    my $address = address_by_pubkey($keypair->pubkey_by_privkey, $algo);
                    return $self->http_ok({ address => $address, private_key => wallet_import_format($keypair->pk_serialize) });
                }
                elsif ($path[1] eq "add") {
                    my $content = eval { $JSON->decode($http_request->decoded_content) };
                    ref($content) eq "HASH" && $content->{address} && $content->{private_key}
                        or return $self->http_response(400, "Invalid request body");
                    validate_address($content->{address})
                        or return $self->http_response(400, "Invalid address");
                    if (grep { $content->{address} eq $_->address } QBitcoin::MyAddress->my_address()) {
                        return $self->http_ok({ address => $content->{address}, reason => "Address already imported" });
                    }
                    my ($private_key, $delegate_pubkeyhash) = eval { wif_decode($content->{private_key}) };
                    $private_key
                        or return $self->http_response(400, "Invalid private key");
                    my @algos = pk_alg($private_key);
                    @algos or return $self->http_response(400, "Unsupported private key algorithm");
                    my ($pk_alg, $pubkey);
                    foreach my $algo (@algos) {
                        my $privkey = pk_import($private_key, $algo) or next;
                        my $pub = $privkey->pubkey_by_privkey or next;
                        my $addr = $delegate_pubkeyhash
                            ? delegation_address(pubkeyhash_by_pubkey($pub, $algo), $delegate_pubkeyhash)
                            : address_by_pubkey($pub, $algo);
                        if ($content->{address} eq $addr) {
                            $pk_alg = $algo;
                            $pubkey = $pub;
                            last;
                        }
                    }
                    $pk_alg or return $self->http_response(400, "Private key does not match the address");
                    my $store = $delegate_pubkeyhash
                        ? delegation_import_format($private_key, $delegate_pubkeyhash)
                        : wallet_import_format($private_key);
                    my $warning;
                    if (QBitcoin::Wallet->is_encrypted) {
                        my $master; # in-memory master key when unlocked
                        if (!QBitcoin::Wallet->unlocked) {
                            # The Basic-auth password of this request is the wallet
                            # password, so a locked wallet can still encrypt the key
                            $master = defined($self->{auth_password})
                                ? QBitcoin::Wallet->master_key_with_password($self->{auth_password}) : undef
                                or return $self->http_response(409, "The wallet is locked and the master key cannot be unwrapped with the request password");
                        }
                        $store = QBitcoin::Wallet->encrypt_pk($store, $content->{address}, $master);
                    }
                    elsif (!QBitcoin::Password->is_set) {
                        $warning = "the key is stored unencrypted; set a wallet password to encrypt the wallet keys";
                    }
                    elsif ($config->{encrypted_private_keys} // 1) {
                        $warning = "the key is stored unencrypted; change a wallet password to encrypt the wallet keys";
                    }
                    else {
                        $warning = "the key is stored unencrypted ('encrypted_private_keys' is disabled)";
                    }
                    my $my_address = QBitcoin::MyAddress->create({
                        private_key => $store,
                        pubkey      => $pubkey,
                        address     => $content->{address},
                        algo        => $pk_alg,
                        $delegate_pubkeyhash ? (deleg_pubkeyhash => $delegate_pubkeyhash) : (),
                    });
                    QBitcoin::Generate->load_address_utxo($my_address);
                    return $self->http_ok({ address => $my_address->address, $warning ? (warning => $warning) : () });
                }
                else {
                    return $self->http_response(404, "Unknown request");
                }
            }
            @path == 3
                or return $self->http_response(404, "Unknown request");
            validate_address($path[1])
                or return $self->http_response(404, "Unknown request");
            if ($path[2] eq "edit") {
                my $content = eval { $JSON->decode($http_request->decoded_content) };
                ref($content) eq "HASH" && defined($content->{staked})
                    or return $self->http_response(400, "Invalid request body");
                my ($my_address) = grep { $_->address eq $path[1] } QBitcoin::MyAddress->my_address()
                    or return $self->http_response(404, "Address not found");
                if (($my_address->staked && !$content->{staked}) || (!$my_address->staked && $content->{staked})) {
                    $my_address->private_key || !$content->{staked}
                        or return $self->http_response(400, "Cannot set watch-only address as staked");
                    $my_address->set_stake($content->{staked} ? 1 : 0)
                        or return $self->http_response(400, $my_address->is_delegation
                            ? "The address is delegated for staking; staking it here as well would equivocate"
                            : "Cannot change the staked flag");
                }
                return $self->http_ok({});
            }
            return $self->http_response(404, "Unknown request");
        }
        elsif ($path[0] eq "staking_keys") {
            my %delegations;
            $delegations{$_->staking_key_id}++ foreach QBitcoin::Delegation->list;
            return $self->http_ok([
                map +{
                    pubkeyhash  => $_->pubkeyhash_string,
                    algo        => CRYPT_ALGO_NAMES->{$_->algo},
                    delegations => $delegations{$_->id} // 0,
                }, QBitcoin::StakingKey->list
            ]);
        }
        elsif ($path[0] eq "staking_key") {
            $http_request->method eq "POST" && @path == 2 && $path[1] eq "new"
                or return $self->http_response(404, "Unknown request");
            return $self->new_staking_key();
        }
        elsif ($path[0] eq "delegations") {
            return $self->http_ok([
                map +{
                    address            => $_->address,
                    owner_pubkeyhash   => pubkeyhash_str($_->owner_pubkeyhash),
                    staking_pubkeyhash => $_->staking_key->pubkeyhash_string,
                }, QBitcoin::Delegation->list
            ]);
        }
        elsif ($path[0] eq "delegation") {
            $http_request->method eq "POST"
                or return $self->http_response(404, "Unknown request");
            if (@path == 2 && $path[1] eq "add") {
                return $self->delegation_add($http_request);
            }
            if (@path == 3 && $path[2] eq "remove") {
                validate_address($path[1])
                    or return $self->http_response(404, "Unknown request");
                my ($delegation) = grep { $_->address eq $path[1] } QBitcoin::Delegation->list
                    or return $self->http_response(404, "Delegation not found");
                $delegation->remove;
                return $self->http_ok({});
            }
            return $self->http_response(404, "Unknown request");
        }
        elsif ($path[0] eq "transaction") {
            $http_request->method eq "POST"
                or return $self->http_response(404, "Unknown request");
            @path >= 2 or return $self->http_response(404, "Unknown request");
            if ($path[1] eq "create") {
                return $self->wallet_tx_create($http_request);
            }
            elsif ($path[1] eq "send") {
                return $self->tx_send($http_request);
            }
            return $self->http_response(404, "Unknown request");
        }
        return $self->http_response(404, "Unknown request");
    }
    return $self->http_response(404, "Unknown request");
}

sub wallet_tx_create {
    my $self = shift;
    my ($http_request) = @_;

    my $content = eval { $JSON->decode($http_request->decoded_content) };
    ref($content) eq "HASH" && ref($content->{inputs}) eq "ARRAY" && ref($content->{outputs}) eq "ARRAY"
        or return $self->http_response(400, "Invalid request body");
    @{$content->{inputs}}  or return $self->http_response(400, "No inputs specified");
    @{$content->{inputs}} <= MAX_INPUTS_PER_TX
        or return $self->http_response(400, "Too many inputs specified");
    @{$content->{outputs}} or return $self->http_response(400, "No outputs specified");
    @{$content->{outputs}} <= MAX_OUTPUTS_PER_TX
        or return $self->http_response(400, "Too many outputs specified");

    # Construct inputs
    my @in;
    foreach my $input (@{$content->{inputs}}) {
        ref($input) eq "HASH" && validate_txid($input->{txid}) && defined($input->{vout})
            or return $self->http_response(400, "Invalid input format");
        push @in, { tx_out => pack("H*", $input->{txid}), num => $input->{vout} + 0 };
    }

    # Construct outputs
    my @out;
    my $token_hash;
    foreach my $out (@{$content->{outputs}}) {
        ref($out) eq "HASH" or return $self->http_response(400, "Invalid output format");
        my ($txo, $out_token_hash) = create_txo($out);
        $txo or return $self->http_response(400, "Invalid output");
        push @out, @$txo;
        if (defined($out_token_hash)) {
            if (defined($token_hash) && $token_hash ne $out_token_hash) {
                return $self->http_response(400, "Different token_id in outputs");
            }
            $token_hash //= $out_token_hash;
        }
    }
    @out or return $self->http_response(400, "No valid outputs");

    my $tx = QBitcoin::Transaction->new(
        in_raw  => \@in,
        out     => \@out,
        tx_type => defined($token_hash) ? TX_TYPE_TOKENS : TX_TYPE_STANDARD,
        defined($token_hash) ? (token_hash => $token_hash) : (),
    );

    # Load inputs from DB
    if (!$tx->load_inputs(1)) {
        return $self->http_response(400, "Failed to load transaction inputs");
    }

    if ($tx->input_pending || $tx->input_detached) {
        return $self->http_response(400, "Some inputs unknown");
    }

    my ($token_err, $token_warning) = check_tx_tokens_balance($tx);
    if ($token_err) {
        return $self->http_response(400, "Tokens balance check failed: $token_err");
    }

    # Fill the reclaim_id sentinel of any downgrade freeze output with the first
    # input's pubkey hash256, so the freeze stays reclaimable if the downgrade
    # never completes (mirrors signrawtransactionwithkey). Must run before signing:
    # it changes the outputs, which the input signatures commit to.
    {
        my @freeze_outs = grep {
            ($_->scripthash // "") eq QBT_FREEZE_SCRIPTHASH
                && length($_->data // "") >= 32 && substr($_->data, 0, 32) eq ("\x00" x 32)
        } @{$tx->out};
        if (@freeze_outs) {
            my $first_txo = $tx->in->[0]{txo};
            my $address = $first_txo && QBitcoin::MyAddress->get_by_hash($first_txo->scripthash, 0)
                or return $self->http_response(400, "No private key to fill downgrade reclaim_id");
            my $reclaim_id = hash256($address->pubkey);
            $_->{data} = $reclaim_id . substr($_->data, 32) for @freeze_outs;
        }
    }

    QBitcoin::Wallet->signing_available
        or return $self->http_response(409, "The wallet is locked; unlock it with walletunlock or enable staking in the web admin interface");

    # Verify all inputs are ours and sign
    my $input_amount = 0;
    foreach my $num (0 .. $#{$tx->in}) {
        my $in = $tx->in->[$num];
        my $txo = $in->{txo};
        if ($txo->tx_out) {
            return $self->http_response(400, sprintf("Input %s:%u already confirmed spent", $txo->tx_in_str, $txo->num));
        }
        if (!$txo->unspent) {
            return $self->http_response(400, sprintf("Input %s:%u already spent", $txo->tx_in_str, $txo->num));
        }
        $input_amount += $txo->value;
        # Freeze / downgrade-output user reclaim (ELSE branch): the wallet key is
        # identified by the reclaim_id in the leading bytes of the output data, not
        # by the output scripthash, which is shared by all users (mirrors
        # Transaction::Signature::sign_transaction).
        if (my $info = QBT_RECLAIM_SCRIPTS->{$txo->scripthash}) {
            my ($redeem_script, $len) = @$info;
            my $reclaim_id = substr($txo->data // "", 0, $len);
            my $address = QBitcoin::MyAddress->get_by_pubkeyhash($reclaim_id)
                or return $self->http_response(400, "Input " . $txo->tx_in_str . ":" . $txo->num . " reclaim_id does not match a known address");
            $tx->make_sign_reclaim($in, $address, $num, $redeem_script);
            next;
        }
        my $address = QBitcoin::MyAddress->get_by_hash($txo->scripthash, 0)
            or return $self->http_response(400, sprintf("Input %s:%u does not belong to a known address", $txo->tx_in_str, $txo->num));
        $address->private_key
            or return $self->http_response(400, "No private key for address " . $address->address);
        $tx->make_sign($in, $address, $num);
    }

    $tx->calculate_hash;

    my $tx_data = $tx->serialize;
    if (length($tx_data) > MAX_TX_SIZE) {
        return $self->http_response(400, "Transaction size too large: " . length($tx_data) . " > " . MAX_TX_SIZE);
    }

    my $output_amount = 0;
    $output_amount += $_->value foreach @{$tx->out};
    if ($input_amount < $output_amount) {
        return $self->http_response(400, "Insufficient funds: inputs $input_amount < outputs $output_amount");
    }

    return $self->http_ok({
        hex  => unpack("H*", $tx_data),
        hash => unpack("H*", $tx->hash),
        fee  => $input_amount - $output_amount,
        defined($token_warning) ? (warning => $token_warning) : (),
    });
}

sub tx_send {
    my $self = shift;
    my ($http_request) = @_;

    my $content = eval { $JSON->decode($http_request->decoded_content) };
    ref($content) eq "HASH" && $content->{hex}
        or return $self->http_response(400, "Invalid request body");
    $content->{hex} =~ /^[0-9a-f]+\z/
        or return $self->http_response(400, "Invalid hex string");

    my $data = Bitcoin::Serialized->new(pack("H*", $content->{hex}));
    my $tx = QBitcoin::Transaction->deserialize($data);
    if (!$tx || $data->length) {
        return $self->http_response(400, "Transaction decode failed");
    }
    $tx->received_from = $self;

    if (QBitcoin::Transaction->has_pending($tx->hash)) {
        return $self->http_response(400, "Transaction already published");
    }
    if (QBitcoin::Transaction->check_by_hash($tx->hash)) {
        return $self->http_response(400, "Transaction already published");
    }
    if (!$tx->load_txo()) {
        return $self->http_response(400, "Incorrect transaction data");
    }

    # Reject downgrade transactions when upgrade threshold reached or upgrade stopped
    if (my $best_block = QBitcoin::Block->best_block) {
        if (($best_block->upgraded // 0) >= UPGRADE_MAX_VALUE || $best_block->upgrade_stopped) {
            if (grep { ($_->scripthash // "") eq QBT_FREEZE_SCRIPTHASH } @{$tx->out}) {
                return $self->http_response(400, "Conversion threshold reached, downgrade not accepted");
            }
        }
    }

    if ($tx->is_pending) {
        return $self->http_response(400, "Some inputs unknown");
    }
    my $rc = $self->process_tx($tx);
    if (!defined($rc)) {
        return $self->http_response(400, "Transaction fee is too low");
    }
    if ($rc != 0) {
        return $self->http_response(400, "Transaction failed");
    }
    return $self->http_ok({ txid => unpack("H*", $tx->hash) });
}

sub http_ok {
    my $self = shift;
    my ($response) = @_;
    my $body;
    my $cont_type;
    if (ref($response)) {
        $body = $JSON->encode($response);
        $cont_type = "application/json";
    }
    else {
        $body = $response;
        $cont_type = $body =~ /^[[:print:]]*$/ ? "text/plain" : "application/octet-stream";
    }
    my $headers = HTTP::Headers->new(
        Content_Type   => $cont_type,
        Content_Length => length($body),
    );
    $headers->header(Access_Control_Allow_Origin => "*") if $self->cors;
    my $http_response = HTTP::Response->new(200, "OK", $headers, $body);
    $http_response->protocol("HTTP/1.1");
    DEBUG_REST && Debugf("REST response: %s", $cont_type eq "application/octet-stream" ? "X'" . unpack("H*", $body) : $body);
    return $self->send($http_response->as_string("\r\n"));
}

sub http_response {
    my $self = shift;
    my ($code, $message, $body) = @_;
    $body //= "";
    my $headers = HTTP::Headers->new(
        Content_Type   => "text/plain",
        Content_Length => length($body),
    );
    $headers->header(Access_Control_Allow_Origin => "*") if $self->cors;
    my $response = HTTP::Response->new($code, $message, $headers, $body);
    $response->protocol("HTTP/1.1");
    return $self->send($response->as_string("\r\n"));
}

sub response_error {
    my $self = shift;
    my ($message, $code, $result) = @_;
    return $self->http_response(500, $message, $result);
}

sub validate_address {
    $_[0] =~ ADDRESS_RE;
}

sub validate_txid {
    $_[0] =~ /^[0-9a-f]{64}\z/;
}

sub tx_status {
    my ($tx) = @_;
    if (defined $tx->block_height) {
        return {
            confirmed    => TRUE,
            block_height => $tx->block_height,
            # block_hash   => unpack("H*", $block->hash),
        };
    }
    else {
        return { confirmed => FALSE };
    }
}

sub block_by_height {
    my ($height) = @_;
    return QBitcoin::Block->best_block($height) // QBitcoin::Block->find(height => $height);
}

sub vin_obj {
    my ($tx, $vin) = @_;
    my $res = {
        txid          => unpack("H*", $vin->{txo}->tx_in),
        vout          => $vin->{txo}->num,
        # Slashing inputs spend without revealing the redeem script, so it may be unknown
        redeem_script => unpack("H*", $vin->{txo}->redeem_script // ""),
        siglist       => [ map { unpack("H*", $_) } @{$vin->{siglist}} ],
        prevout       => {
            value              => $vin->{txo}->value,
            scripthash         => unpack("H*", $vin->{txo}->scripthash),
            scripthash_address => address_by_hash($vin->{txo}->scripthash),
        },
    };
    if ($tx->is_tokens) {
        my $token_hash = $tx->token_hash || $tx->hash;
        if (($vin->{txo}->token_hash // "") eq $token_hash && $vin->{txo}->is_token_transfer) {
            $res->{prevout}->{token_id} = unpack("H*", $token_hash);
            $res->{prevout}->{token_amount} = unpack("Q<", substr($vin->{txo}->data // "", 1, 8));
            my $decimals;
            if (my $token_info = $tx->token_info) {
                $decimals = $token_info->{decimals};
            }
            $res->{prevout}->{token_decimals} = $decimals // TOKEN_DEFAULT_DECIMALS;
        }
    }
    return $res;
}

sub vout_obj {
    my ($tx, $out) = @_;
    my $res = {
        value              => $out->value,
        scripthash         => unpack("H*", $out->scripthash),
        scripthash_address => address_by_hash($out->scripthash),
    };
    # Trustless-downgrade outputs (freeze / downgrade-tx): show the Bitcoin
    # destination address and the reclaim status, matching the RPC display.
    if (my ($btc_addr, $downgrade) = QBitcoin::Transaction::output_downgrade_info($out)) {
        $downgrade->{btc_address} = $btc_addr if defined $btc_addr;
        $res->{downgrade} = $downgrade;
    }
    if ($tx->is_tokens) {
        $res->{token_id} = unpack("H*", $tx->token_hash || $tx->hash);
        if (length($out->data // "")) {
            if ($out->is_token_transfer) {
                $res->{token_amount} = unpack("Q<", substr($out->data, 1, 8));
                my $decimals;
                if (my $token_info = $tx->token_info) {
                    $decimals = $token_info->{decimals};
                }
                $res->{token_decimals} = $decimals // TOKEN_DEFAULT_DECIMALS;
            }
            elsif (my $token_info = $tx->unpack_token_info($out->data)) {
                if ($token_info->{permissions}) {
                    $res->{token_permissions} = sprintf("0x%02x", $token_info->{permissions});
                }
                foreach my $key (qw(decimals symbol name)) {
                    $res->{"token_$key"} = $token_info->{$key} if defined $token_info->{$key};
                }
            }
        }
    }
    return $res;
}

sub tx_obj {
    my ($tx) = @_;
    my $block = defined($tx->block_height) ? block_by_height($tx->block_height) : undef;
    return {
        txid          => unpack("H*", $tx->hash),
        fee           => $tx->fee,
        size          => $tx->size,
        value         => sum0(map { $_->value } @{$tx->out}) + $tx->fee,
        is_coinbase   => $tx->is_coinbase ? TRUE : FALSE,
        received_time => $tx->received_time // undef,
        tx_type       => $tx->type_as_text,
        status        => {
            defined($tx->block_height) ? (
                block_height => $tx->block_height,
                block_pos    => $tx->block_pos,
                confirmed    => TRUE,
            ) : (
                confirmed => FALSE,
            ),
            defined($block) ? (
                block_time   => $block->time,
                block_hash   => unpack("H*", $block->hash),
            ) : (),
        },
        vin  => [ map { vin_obj($tx, $_)  } @{$tx->in}  ],
        vout => [ map { vout_obj($tx, $_) } @{$tx->out} ],
        UPGRADE_POW && $tx->is_coinbase ? (
            coinbase_info => {
                block_height => $tx->up->btc_block_height,
                tx_hash      => unpack("H*", $tx->up->btc_tx_hash),
                out_num      => $tx->up->btc_out_num,
                value        => $tx->up->value,
            },
        ) : (),
        $tx->is_downgrade && $tx->down ? (
            downgrade_info => {
                freeze_txid      => unpack("H*", $tx->down->freeze_txid),
                freeze_vout      => $tx->down->freeze_vout + 0,
                btc_txid         => unpack("H*", scalar reverse $tx->down->btc_txid),
                btc_vout         => $tx->down->btc_vout + 0,
                btc_value        => $tx->down->btc_value + 0,
                btc_scriptpubkey => unpack("H*", $tx->down->scriptpubkey),
            },
        ) : (),
        $tx->is_burn && $tx->down ? (
            downgrade_info => {
                btc_block_hash => unpack("H*", scalar reverse $tx->down->btc_block_hash),
                btc_txid       => unpack("H*", scalar reverse hash256($tx->down->btc_tx_data)),
            },
        ) : (),
        $tx->is_tokens ? ( token_id => unpack("H*", $tx->token_hash // "") ) : (),
    };
}

sub block_obj {
    my $block = shift;
    return {
        id                => unpack("H*", $block->hash),
        height            => $block->height,
        weight            => $block->weight,
        block_weight      => $block->self_weight,
        previousblockhash => $block->prev_hash ? unpack("H*", $block->prev_hash) : undef,
        merkle_root       => unpack("H*", $block->merkle_root),
        timestamp         => $block->time,
        tx_count          => scalar(@{$block->tx_hashes}),
        size              => length($block->serialize),
    };
}

sub get_address_stats {
    my $self = shift;
    my ($address) = @_;
    my $stats = address_stats($address)
        or return $self->http_response(404, "Incorrect address");
    $stats->{tokens} = {};
    my $tokens = all_tokens_balance($address);
    if ($tokens && %$tokens) {
        foreach my $token (keys %$tokens) {
            $stats->{tokens}->{unpack("H*", $token)} = $tokens->{$token};
        }
    }
    return $self->http_ok($stats);
}

sub list_address_txs {
    my $self = shift;
    my ($address, $chain_cnt, $mempool_cnt, $last_seen) = @_;
    my $last_seen_bin;
    if ($last_seen && $last_seen =~ /^[0-9a-f]{64}\z/) {
        $last_seen_bin = pack("H*", $last_seen);
    }
    my ($txo_chain, $txo_mempool) = get_address_txs($address, $last_seen_bin, $chain_cnt, $mempool_cnt);
    $txo_chain
        or return $self->http_response(404, "Incorrect address");
    my @tx;
    if ($mempool_cnt) {
        foreach my $tx_data (@$txo_mempool) {
            my $tx = QBitcoin::Transaction->get($tx_data->[0])
                or next;
            push @tx, tx_obj($tx);
        }
    }
    if ($chain_cnt) {
        foreach my $tx_data (@$txo_chain) {
            my $tx = QBitcoin::Transaction->get_by_hash($tx_data->[0])
                or next;
            push @tx, tx_obj($tx);
        }
    }
    return $self->http_ok(\@tx);
}

sub list_token_txs {
    my $self = shift;
    my ($address, $token_hash, $chain_cnt, $mempool_cnt, $last_seen) = @_;
    my $last_seen_bin;
    if ($last_seen && $last_seen =~ /^[0-9a-f]{64}\z/) {
        $last_seen_bin = pack("H*", $last_seen);
    }
    my ($txs_chain, $txs_mempool) = get_tokens_txs($address, pack("H*", $token_hash), $last_seen_bin, $chain_cnt, $mempool_cnt);
    $txs_chain
        or return $self->http_response(404, "Incorrect address");
    return $self->http_ok([ map { [ unpack("H*", $_->[0]), $_->[1], $_->[2] ] } @$txs_mempool, @$txs_chain ]);
}

sub get_address_unspent {
    my $self = shift;
    my ($address) = @_;

    my ($txo_chain, $txo_mempool) = get_address_utxo($address);
    $txo_chain
        or return $self->http_response(404, "Incorrect address");
    # Surface our matured trustless-downgrade reclaim outputs under this address too.
    my $reclaim_utxo = get_address_reclaim_utxo($address);
    foreach my $txid (keys %$reclaim_utxo) {
        for my $vout (0 .. $#{$reclaim_utxo->{$txid}}) {
            my $u = $reclaim_utxo->{$txid}->[$vout] // next;
            $txo_chain->{$txid}->[$vout] = $u;
        }
    }
    my @utxo;
    foreach my $txid (keys %$txo_chain) {
        for (my $vout = 0; $vout < @{$txo_chain->{$txid}}; $vout++) {
            my $utxo = $txo_chain->{$txid}->[$vout]
                or next;
            push @utxo, {
                txid      => unpack("H*", $txid),
                vout      => $vout,
                value     => $utxo->{value},
                height    => $utxo->{block_height},
                block_pos => $utxo->{block_pos},
                status    => "confirmed",
                defined($utxo->{token_id})     ? ( token_id          => unpack("H*", $utxo->{token_id}) ) : (),
                defined($utxo->{token_amount}) ? ( token_amount      => $utxo->{token_amount}      ) : (),
                $utxo->{token_permissions}     ? ( token_permissions => $utxo->{token_permissions} ) : (),
                utxo_tag($utxo),
                $utxo->{reclaim} ? ( reclaim => TRUE ) : (),
            }
        }
    }
    @utxo = sort { $a->{height} <=> $b->{height} || $a->{block_pos} <=> $b->{block_pos} } @utxo;
    foreach my $txid (sort { $a cmp $b } keys %$txo_mempool) { # TODO: sort by received_time
        for (my $vout = 0; $vout < @{$txo_mempool->{$txid}}; $vout++) {
            my $utxo = $txo_mempool->{$txid}->[$vout]
                or next;
            push @utxo, {
                txid   => unpack("H*", $txid),
                vout   => $vout,
                value  => $utxo->{value},
                status => "unconfirmed",
                defined($utxo->{token_id})     ? ( token_id          => unpack("H*", $utxo->{token_id}) ) : (),
                defined($utxo->{token_amount}) ? ( token_amount      => $utxo->{token_amount} ) : (),
                $utxo->{token_permissions}     ? ( token_permissions => $utxo->{token_permissions} ) : (),
                utxo_tag($utxo),
            };
        }
    }
    return $self->http_ok(\@utxo);
}

sub merkle_proof {
    my ($tx) = @_;
    my $block = block_by_height($tx->block_height)
        or return undef;
    my $num = $tx->block_pos;
    if ($block->tx_hashes->[$num] ne $tx->hash) {
        Errf("block %s %u tx hash %s != %s", $block->hash_str, $num, $tx->hash_str($block->tx_hashes->[$num]), $tx->hash_str);
        return undef;
    }
    my $merkle_path = $block->merkle_path($num);
    my $hashlen = length($tx->hash);
    my $merkle_len = length($merkle_path) / $hashlen;
    my @merkle_path = map { unpack("H*", substr($merkle_path, $_*$hashlen, $hashlen)) } 1 .. $merkle_len;
    return {
        block_height => $block->height,
        pos          => $num,
        merkle       => \@merkle_path,
    };
}

sub get_blocks {
    my $self = shift;
    my ($height) = @_;
    my $best_height = QBitcoin::Block->blockchain_height;
    if (defined($height) && $height ne "" && $height ne "recent") {
        $height =~ /^(?:0|[1-9][0-9]*)\z/
            or return $self->http_response(404, "Incorrect request");
        $height <= $best_height
            or return $self->http_response(404, "Block not found");
    }
    else {
        $height = $best_height;
    }
    my @blocks;
    for (; $height >= 0 && @blocks < 10; $height--) {
        my $block = QBitcoin::Block->best_block($height)
            or last;
        push @blocks, block_obj($block);
    }
    if ($height >= 0 && @blocks < 10) {
        push @blocks, map { block_obj($_) }
            QBitcoin::Block->find(height => { '<=' => $height }, -sortby => "height DESC", -limit => 10-@blocks);
    }
    return $self->http_ok(\@blocks);
}

sub node_status {
    my $best_block;
    if (defined(my $height = QBitcoin::Block->blockchain_height)) {
        $best_block = QBitcoin::Block->best_block($height);
    }
    my @mempool = QBitcoin::Transaction->mempool_list();
    my $response = {
        chain                => $config->{regtest} ? "regtest" : $config->{testnet} ? "testnet" : "main",
        blocks               => defined($best_block) ? $best_block->height+0 : -1,
        bestblockhash        => $best_block ? unpack("H*", $best_block->hash) : undef,
        weight               => $best_block ? $best_block->weight+0   : -1,
        bestblocktime        => $best_block ? $best_block->time       : -1,
        reward               => $best_block ? QBitcoin::Block->reward($best_block, 0, $best_block->time + BLOCK_INTERVAL) : 0,
        initialblockdownload => blockchain_synced() ? FALSE : TRUE,
        mempool_size         => @mempool + 0,
        mempool_bytes        => sum0(map { $_->size } @mempool) + 0,
        total_coins          => QBitcoin::Coins->total,
    };
    if ($config->{regtest}) {
        if (my $genesis_block = QBitcoin::Block->best_block(0)) {
            $response->{genesistime} = $genesis_block->time;
        }
    }
    else {
        $response->{genesistime} = GENESIS_TIME;
    }
    if (UPGRADE_POW) {
        my ($btc_block) = Bitcoin::Block->find(-sortby => 'height DESC', -limit => 1);
        my $btc_scanned;
        if ($btc_block) {
            if ($btc_block->scanned) {
                $btc_scanned = $btc_block;
            }
            else {
                ($btc_scanned) = Bitcoin::Block->find(scanned => 1, -sortby => 'height DESC', -limit => 1);
            }
        }
        $response->{btc_synced}  = btc_synced() ? TRUE : FALSE,
        $response->{btc_headers} = $btc_block   ? $btc_block->height+0   : 0,
        $response->{btc_scanned} = $btc_scanned ? $btc_scanned->height+0 : 0,
    }
    return $response;
}

sub peer_info {
    my @peers;
    foreach my $connection (QBitcoin::ConnectionList->connected(PROTOCOL_QBITCOIN, PROTOCOL_BITCOIN)) {
        my $peer = $connection->peer;
        push @peers, {
            addr        => ip_port_str($connection->addr, $connection->port),
            addrlocal   => ip_port_str($connection->my_addr, $connection->my_port),
            inbound     => $connection->direction == DIR_IN ? TRUE : FALSE,
            protocol    => $connection->type,
            software    => $peer->software // "",
            network     => $peer->ipv4 ? "ipv4" : "ipv6",
            createtime  => $connection->state_time,
            bytessent   => $connection->bytes_sent,
            bytesrecv   => $connection->bytes_recv,
            objsent     => $connection->obj_sent,
            objrecv     => $connection->obj_recv,
            reputation  => $peer->reputation,
        };
    }
    return \@peers;
}

sub is_local {
    my $self = shift;
    return $self->connection->my_ip eq "127.0.0.1";
}

# Guard for /admin/* and /wallet/*.
# Returns undef if the request is allowed, or an (already sent) error response otherwise.
# - If a wallet password is configured, require HTTP Basic auth (the username is
#   ignored, only the password is checked) and allow the request from any address.
# - If no password is configured, keep the historical localhost-only restriction.
sub check_access {
    my $self = shift;
    my ($http_request) = @_;
    if (QBitcoin::Password->is_set) {
        my $auth = $http_request->header('Authorization');
        if (defined($auth) && $auth =~ /^\s*Basic\s+(\S+)/i) {
            my $decoded = eval { decode_base64($1) };
            if (defined($decoded)) {
                my (undef, $password) = split(/:/, $decoded, 2);
                if (defined($password) && QBitcoin::Password->check_password($password)) {
                    # Keep the verified plaintext password for handlers that need
                    # it as the wallet password (unlock via the staking toggle,
                    # key encryption on import, password change)
                    $self->{auth_password} = $password;
                    return undef;
                }
            }
        }
        return $self->http_auth_required;
    }
    return $self->is_local ? undef : $self->http_response(403, "Forbidden");
}

sub http_auth_required {
    my $self = shift;
    my $body = "Authentication required";
    my $headers = HTTP::Headers->new(
        Content_Type     => "text/plain",
        Content_Length   => length($body),
        WWW_Authenticate => 'Basic realm="qbitcoin wallet"',
    );
    my $response = HTTP::Response->new(401, "Unauthorized", $headers, $body);
    $response->protocol("HTTP/1.1");
    return $self->send($response->as_string("\r\n"));
}

sub set_wallet_password {
    my $self = shift;
    my ($http_request) = @_;
    my $content = eval { $JSON->decode($http_request->decoded_content) };
    ref($content) eq "HASH" && defined($content->{password}) && !ref($content->{password})
        or return $self->http_response(400, "Invalid request body");
    my $password = $content->{password};
    length($password) > 0
        or return $self->http_response(400, "Password must not be empty");
    length($password) <= QBitcoin::Password::MAX_LEN()
        or return $self->http_response(400, "Password too long");
    # The old password (when one is set) has already been verified by check_access
    # via HTTP Basic auth; there is no forgotten-password reset over REST
    if (defined(my $err = QBitcoin::Wallet->change_password($self->{auth_password}, $password))) {
        return $self->http_response(400, $err);
    }
    return $self->http_ok({});
}

# POST /wallet/staking_key/new: create a staking key for delegated staking and
# store it in the wallet; the returned pubkeyhash is what the delegate publishes
sub new_staking_key {
    my $self = shift;
    my $algo = CRYPT_ALGO_ECDSA; # TODO: support multiple algorithms
    my $keypair = generate_keypair($algo);
    my $pubkey = $keypair->pubkey_by_privkey
        or return $self->http_response(500, "Cannot generate a staking key");
    my $pubkeyhash_str = pubkeyhash_str(pubkeyhash_by_pubkey($pubkey, $algo));
    my $store = wallet_import_format($keypair->pk_serialize);
    my $warning;
    if (QBitcoin::Wallet->is_encrypted) {
        my $master; # in-memory master key when unlocked
        if (!QBitcoin::Wallet->unlocked) {
            # The Basic-auth password of this request is the wallet password
            $master = defined($self->{auth_password})
                ? QBitcoin::Wallet->master_key_with_password($self->{auth_password}) : undef
                or return $self->http_response(409, "The wallet is locked and the master key cannot be unwrapped with the request password");
        }
        $store = QBitcoin::Wallet->encrypt_pk($store, $pubkeyhash_str, $master);
    }
    elsif (!QBitcoin::Password->is_set) {
        $warning = "the key is stored unencrypted; set a wallet password to encrypt the wallet keys";
    }
    else {
        $warning = "the key is stored unencrypted ('encrypted_private_keys' is disabled)";
    }
    QBitcoin::StakingKey->create({
        private_key => $store,
        pubkey      => $pubkey,
        algo        => $algo,
    })
        or return $self->http_response(500, "Cannot store the staking key");
    return $self->http_ok({ pubkeyhash => $pubkeyhash_str, $warning ? (warning => $warning) : () });
}

# POST /wallet/delegation/add: register a delegated-staking address on this
# (delegate) node; its coins are staked by this node from now on
sub delegation_add {
    my $self = shift;
    my ($http_request) = @_;
    my $content = eval { $JSON->decode($http_request->decoded_content) };
    ref($content) eq "HASH" && $content->{owner_pubkeyhash}
        or return $self->http_response(400, "Invalid request body");
    my $owner_pubkeyhash = eval { pubkeyhash_by_str($content->{owner_pubkeyhash}) }
        or return $self->http_response(400, "Invalid owner pubkeyhash");
    my $staking_key;
    if (defined(my $staking_str = $content->{staking_pubkeyhash})) {
        my $staking_pubkeyhash = eval { pubkeyhash_by_str($staking_str) }
            or return $self->http_response(400, "Invalid staking pubkeyhash");
        $staking_key = QBitcoin::StakingKey->get_by_pubkeyhash($staking_pubkeyhash)
            or return $self->http_response(404, "No such staking key");
    }
    else {
        my @keys = QBitcoin::StakingKey->list;
        @keys == 1
            or return $self->http_response(400, @keys
                ? "More than one staking key in the wallet; specify the staking pubkeyhash"
                : "No staking key in the wallet; create one first");
        $staking_key = $keys[0];
    }
    my $delegation = QBitcoin::Delegation->create($staking_key, $owner_pubkeyhash)
        or return $self->http_response(500, "Cannot store the delegation");
    QBitcoin::Generate->load_address_utxo($delegation);
    return $self->http_ok({ address => $delegation->address });
}

sub wallet_status {
    my $encrypted = QBitcoin::Wallet->is_encrypted;
    my $generate  = QBitcoin::Generate::Control->generate_enabled;
    return {
        password_set   => QBitcoin::Password->is_set ? TRUE : FALSE,
        keys_encrypted => $encrypted ? TRUE : FALSE,
        locked         => ($encrypted && !QBitcoin::Wallet->unlocked) ? TRUE : FALSE,
        generate       => $generate ? TRUE : FALSE,
        staking_active => ($generate && QBitcoin::Wallet->signing_available) ? TRUE : FALSE,
    };
}

# POST /admin/generate: the staking toggle of the web admin interface.
# Enabling generation on a locked wallet also unlocks it: the Basic-auth password
# of the request is the wallet password.
sub set_generate {
    my $self = shift;
    my ($http_request) = @_;
    my $content = eval { $JSON->decode($http_request->decoded_content) };
    ref($content) eq "HASH" && defined($content->{generate})
        or return $self->http_response(400, "Invalid request body");
    if ($content->{generate}) {
        if (QBitcoin::Wallet->is_encrypted && !QBitcoin::Wallet->unlocked) {
            defined($self->{auth_password}) && QBitcoin::Wallet->unlock($self->{auth_password})
                or return $self->http_response(409, "Cannot unlock the wallet master key with the request password");
            Noticef("Wallet unlocked via web admin interface");
        }
        QBitcoin::Generate::Control->generate_enabled(1);
        Noticef("Block generation enabled via web admin interface");
    }
    else {
        # Only stops generation; the keys stay unlocked (locking is walletlock's job)
        QBitcoin::Generate::Control->generate_enabled(0);
        Noticef("Block generation disabled via web admin interface");
    }
    return $self->http_ok(wallet_status());
}

1;
