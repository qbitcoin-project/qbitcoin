package QBitcoin::RPC::Commands;
use warnings;
use strict;

use Role::Tiny;
use List::Util qw(sum0 sum min max);
use QBitcoin::Const;
use QBitcoin::RPC::Const;
use QBitcoin::Config;
use QBitcoin::BlockchainParams;
use QBitcoin::Log;
use QBitcoin::IP qw(ip_port_str parse_addr_port host_to_ips);
use QBitcoin::ORM qw(dbh);
use QBitcoin::Crypto qw(pk_import pk_alg generate_keypair hash160);
use QBitcoin::Block;
use QBitcoin::Coins;
use QBitcoin::Transaction;
use QBitcoin::ProtocolState qw(mempool_synced blockchain_synced btc_synced);
use QBitcoin::Transaction;
use QBitcoin::TXO;
use QBitcoin::Address qw(wif_to_pk wif_decode wif_delegation_hash scripthash_by_address address_by_pubkey wallet_import_format delegation_import_format address_by_hash pubkeyhash_str pubkeyhash_by_pubkey);
use QBitcoin::Script::Delegation qw(delegation_script delegation_address);
use QBitcoin::MyAddress;
use QBitcoin::StakingKey;
use QBitcoin::Delegation;
use QBitcoin::Password;
use QBitcoin::Password::Throttle qw(throttle_message);
use QBitcoin::Wallet;
use QBitcoin::Tag;
use QBitcoin::Generate;
use QBitcoin::Generate::Control;
use QBitcoin::Protocol;
use QBitcoin::Peer;
use QBitcoin::ConnectionList;
use QBitcoin::Utils qw(get_address_txs get_address_utxo address_received address_balance tokens_balance tokens_received get_tokens_info create_txo estimate_fees check_tx_tokens_balance);
use Bitcoin::Serialized;
use Bitcoin::Block;

my %PARAMS;
my %HELP;
my %SENSITIVE; # commands whose params must not be logged in plaintext (e.g. passwords)
my %READONLY;  # commands which do not modify in-memory or database state
my %REQUIRE_PASSWORD; # commands gated by the wallet password (when one is set), see QBitcoin::RPC::process_request

sub params            { $PARAMS{$_[1]}           }
sub help              { $HELP{$_[1]}             }
sub sensitive         { $SENSITIVE{$_[1]}        }
sub readonly          { $READONLY{$_[1]}         }
sub requires_password { $REQUIRE_PASSWORD{$_[1]} }

# Read-only commands may be processed in a forked child in parallel with the main
# process (see QBitcoin::Fork). Do not mark a command here if it modifies mempool,
# blockchain, wallet or any other in-memory state (sendrawtransaction, import*,
# getnewaddress, stake/unstake, set*), or interacts with the wallet decryption
# state (dumpprivkey) - such commands must be processed in the main process.
$READONLY{$_} = 1 foreach qw(
    ping
    help
    getblockchaininfo
    getbestblockhash
    getblockheader
    getblockcount
    getblock
    getblockhash
    getrawtransaction
    createrawtransaction
    signrawtransactionwithkey
    decoderawtransaction
    getmempoolinfo
    getrawmempool
    getmempoolentry
    validateaddress
    getnetworkinfo
    getindexinfo
    getchaintxstats
    getblockstats
    getpeerinfo
    listpeers
    getaddressinfo
    getaddressbalance
    getreceivedbyaddress
    listunspent
    listtransactions
    listmyaddresses
    getbalance
    estimatesmartfee
    createdelegationaddress
    liststakingkeys
    listdelegations
    gettokensbalance
    gettokensreceived
    gettokensinfo
);

$PARAMS{ping} = "";
$HELP{ping} = qq(
Check that the node alive and responsible.

Result:
null    (json null)

Examples:
> qbitcoin-cli ping
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "ping", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_ping {
    my $self = shift;
    $self->response_ok;
}

$PARAMS{getblockchaininfo} = "";
$HELP{getblockchaininfo} = qq(
Returns an object containing various state info regarding blockchain processing.

Result:
{                                         (json object)
  "chain" : "str",                        (string) current network name (main, test, regtest)
  "blocks" : n,                           (numeric) the height of the most-work fully-validated chain. The genesis block has height 0
  "bestblockhash" : "str",                (string) the hash of the currently best block
  "weight" : n,                           (numeric) the current weight
  "bestblocktime" : n,                    (numeric) time for the current best block
  "initialblockdownload" : true|false,    (boolean) (debug information) estimate of whether this node is in Initial Block Download mode
  "total_coins" : n,                      (numeric) total number of generated (upgraded) coins
  "btc_headers" : n,                      (numeric) number of processed btc block headers
  "btc_scanned" : n,                      (numeric) number of scanned btc blocks
  "btc_synced" : true|false,              (boolean) is btc blockchain fully synced or is in initial block download mode
}

Examples:
> qbitcoin-cli getblockchaininfo
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblockchaininfo", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getblockchaininfo {
    my $self = shift;
    my $best_block;
    if (defined(my $height = QBitcoin::Block->blockchain_height)) {
        $best_block = QBitcoin::Block->best_block($height);
    }
    my $total_coins = QBitcoin::Coins->total();
    my $response = {
        chain                => $config->{regtest} ? "regtest" : $config->{testnet} ? "testnet" : "main",
        blocks               => $best_block ? $best_block->height+0   : -1,
        bestblockhash        => $best_block ? unpack("H*", $best_block->hash) : undef,
        weight               => $best_block ? $best_block->weight+0   : -1,
        bestblocktime        => $best_block ? $best_block->time       : -1,
        initialblockdownload => blockchain_synced() ? FALSE : TRUE,
        total_coins          => $total_coins ? $total_coins / DENOMINATOR : 0,
        # size_on_disk         => # TODO
    };
    $response->{headers} = $response->{blocks}; # satisfy explorers
    if (UPGRADE_POW) {
        my ($btc_block) = Bitcoin::Block->find(-sortby => 'height DESC', -limit => 1);
        my $btc_scanned;
        if ($btc_block) {
            if ($btc_block->scanned) {
                $btc_scanned = $btc_block;
            }
            else {
                ($btc_scanned) = Bitcoin::Block->find(scanned => 1, -sortby => 'height DESC', -limit => 1);
            }
        }
        $response->{btc_synced}  = btc_synced() ? TRUE : FALSE;
        $response->{btc_headers} = $btc_block   ? $btc_block->height+0   : 0;
        $response->{btc_scanned} = $btc_scanned ? $btc_scanned->height+0 : 0;
    }
    return $self->response_ok($response);
}

$PARAMS{getbestblockhash} = "";
$HELP{getbestblockhash} = qq(
Returns the hash of the best (tip) block in the most-weight fully-validated chain.

Result:
"hex"    (string) the block hash, hex-encoded

Examples:
> qbitcoin-cli getbestblockhash
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getbestblockhash", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getbestblockhash {
    my $self = shift;
    my $best_block;
    if (defined(my $height = QBitcoin::Block->blockchain_height)) {
        $best_block = QBitcoin::Block->best_block($height);
    }
    return $self->response_ok($best_block ? unpack("H*", $best_block->hash) : undef);
}

$PARAMS{help} = "command?";
$HELP{help} = q(
List all commands, or get help for a specified command.

Arguments:
1. command    (string, optional, default=all commands) The command to get help on

Result:
"str"    (string) The help text
);
sub cmd_help {
    my $self = shift;
    if (my $cmd = $self->args->[0]) {
        if (defined $self->params($cmd)) {
            return $self->response_ok($self->brief($cmd) . "\n" . ($HELP{$cmd} // ""));
        }
        else {
            return $self->response_ok("help: unknown command: $cmd");
        }
    }
    my $help = "";
    foreach my $cmd (sort keys %PARAMS) {
        $help .= $self->brief($cmd) . "\n";
    }
    return $self->response_ok($help);
}

$PARAMS{getblockheader} = "blockhash";
$HELP{getblockheader} = qq(
Returns an Object with information about blockheader <hash>.

Arguments:
1. blockhash    (string, required) The block hash

Result:
{                                 (json object)
  "hash" : "hex",                 (string) the block hash (same as provided)
  "confirmations" : n,            (numeric) The number of confirmations, or -1 if the block is not on the main chain
  "height" : n,                   (numeric) The block height or index
  "merkleroot" : "hex",           (string) The merkle root
  "time" : xxx,                   (numeric) The block time expressed in UNIX epoch time
  "nTx" : n,                      (numeric) The number of transactions in the block
  "previousblockhash" : "hex",    (string) The hash of the previous block
  "nextblockhash" : "hex"         (string) The hash of the next block
}

Examples:
> qbitcoin-cli getblockheader "00000000c937983704a73af28acdec37b049d214adbda81d7e2a3dd146f6ed09"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblockheader", "params": ["00000000c937983704a73af28acdec37b049d214adbda81d7e2a3dd146f6ed09"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getblockheader {
    my $self = shift;
    my $hash = pack("H*", $self->args->[0]);
    my $best_height = QBitcoin::Block->blockchain_height;
    my $block = $self->get_block_by_hash($hash)
        or return $self->response_error("Block not found", ERR_INVALID_ADDRESS_OR_KEY);
    my $best_block = QBitcoin::Block->best_block($best_height);
    my $next_block = QBitcoin::Block->best_block($block->height + 1) // QBitcoin::Block->find(height => $block->height + 1);

    return $self->response_ok({
        hash              => unpack("H*", $block->hash),
        height            => $block->height,
        time              => $block->time,
        confirmations     => $best_height - $block->height + 1,
        nTx               => @{$block->tx_hashes}+0,
        previousblockhash => unpack("H*", $block->prev_hash),
        nextblockhash     => $next_block ? unpack("H*", $next_block->hash) : undef,
        merkleroot        => unpack("H*", $block->merkle_root),
        weight            => $block->weight,
        confirm_weight    => $best_block->weight - $block->weight,
    });
}

$PARAMS{getblockcount} = "";
$HELP{getblockcount} = qq(
Returns the height of the most-work fully-validated chain.
The genesis block has height 0.

Result:
n    (numeric) The current block count

Examples:
> qbitcoin-cli getblockcount
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblockcount", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getblockcount {
    my $self = shift;
    return $self->response_ok(QBitcoin::Block->blockchain_height);
}

$PARAMS{getblock} = "blockhash verbosity?";
$HELP{getblock} = qq(
If verbosity is 1, returns an Object with information about block <hash>.
If verbosity is 2, returns an Object with information about block <hash> and information about each transaction.

Arguments:
1. blockhash    (string, required) The block hash
2. verbosity    (numeric, optional, default=1) 1 for a json object, and 2 for json object with transaction data

Result (for verbosity = 1):
{                                 (json object)
  "hash" : "hex",                 (string) the block hash (same as provided)
  "confirmations" : n,            (numeric) The number of confirmations, or -1 if the block is not on the main chain
  "size" : n,                     (numeric) The block size
  "weight" : n,                   (numeric) The block weight
  "height" : n,                   (numeric) The block height or index
  "upgraded" : n,                 (numeric) Cumulative BTC satoshis converted to QBTC (net)
  "merkleroot" : "hex",           (string) The merkle root
  "tx" : [                        (json array) The transaction ids
    "hex",                        (string) The transaction id
    ...
  ],
  "time" : xxx,                   (numeric) The block time expressed in UNIX epoch time
  "nTx" : n,                      (numeric) The number of transactions in the block
  "previousblockhash" : "hex",    (string) The hash of the previous block
  "nextblockhash" : "hex"         (string) The hash of the next block
}

Result (for verbosity = 2):
{             (json object)
  ...,        Same output as verbosity = 1
  "tx" : [    (json array)
    {         (json object)
      ...     The transactions in the format of the getrawtransaction RPC. Different from verbosity = 1 "tx" result
    },
    ...
  ]
}

Examples:
> qbitcoin-cli getblock "00000000c937983704a73af28acdec37b049d214adbda81d7e2a3dd146f6ed09"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblock", "params": ["00000000c937983704a73af28acdec37b049d214adbda81d7e2a3dd146f6ed09"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getblock {
    my $self = shift;
    my $hash = pack("H*", $self->args->[0]);
    my $verbosity = $self->args->[1] // 1;

    my $best_height = QBitcoin::Block->blockchain_height;
    my $block = $self->get_block_by_hash($hash)
        or return $self->response_error("Block not found", ERR_INVALID_ADDRESS_OR_KEY);
    my $best_block = QBitcoin::Block->best_block($best_height);
    my $next_block = QBitcoin::Block->best_block($block->height + 1) // QBitcoin::Block->find(height => $block->height + 1);

    my $res = {
        hash              => unpack("H*", $block->hash),
        height            => $block->height,
        time              => $block->time,
        confirmations     => $best_height - $block->height + 1,
        previousblockhash => unpack("H*", $block->prev_hash // ZERO_HASH),
        nextblockhash     => $next_block ? unpack("H*", $next_block->hash) : undef,
        merkleroot        => unpack("H*", $block->merkle_root),
        weight            => $block->weight,
        confirm_weight    => $best_block->weight - $block->weight,
        upgraded          => $block->upgraded,
    };
    if ($verbosity == 1) {
        $res->{tx} = [ map { unpack("H*", $_) } @{$block->tx_hashes} ];
    }
    else {
        $res->{tx} = [ map { $_->as_hashref } @{$block->transactions} ];
    }

    return $self->response_ok($res);
}

$PARAMS{getblockhash} = "height";
$HELP{getblockhash} = qq(
Returns hash of block in best-block-chain at height provided.

Arguments:
1. height    (numeric, required) The height index

Result:
"hex"    (string) The block hash

Examples:
> qbitcoin-cli getblockhash 1000
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblockhash", "params": [1000]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getblockhash {
    my $self = shift;
    my $height = $self->args->[0];
    my $block = QBitcoin::Block->best_block($height) // QBitcoin::Block->find(height => $height);
    if (!$block) {
        return $self->response_error("Block not found", ERR_INVALID_ADDRESS_OR_KEY);
    }
    return $self->response_ok(unpack("H*", $block->hash));
}

$PARAMS{getrawtransaction} = "txid verbose?";
$HELP{getrawtransaction} = qq(
getrawtransaction "txid" ( verbose )

Return the raw transaction data.

If verbose is 'true', returns an Object with information about 'txid'.
If verbose is 'false' or omitted, returns a string that is serialized, hex-encoded data for 'txid'.

Arguments:
1. txid         (string, required) The transaction id
2. verbose      (boolean, optional, default=true) If false, return a string, otherwise return a json object

Result (if verbose is set false):
"str"    (string) The serialized, hex-encoded data for 'txid'

Result (if verbose is not set or is set to true):
{                                    (json object)
  "hash" : "hex",                    (string) The transaction hash (the same as the txid)
  "size" : n,                        (numeric) The serialized transaction size
  "in" : [                           (json array)
    {                                (json object)
      "txid" : "hex",                (string) The transaction id
      "num" : n,                     (numeric) The output number
      "redeem_script" : "hex",       (string, optional) The redeem script in hex
      "siglist" : [                  (json object) The list of signatures
        "hex"                        (string) hex
      },
    },
    ...
  ],
  "out" : [                          (json array)
    {                                (json object)
      "value" : n,                   (numeric) The value in QBTC
      "address" : "str",             (string) qbitcoin address
      "data" : "hex",                (string, optional) The data in hex (if a data output)
    },
    ...
  ],
  "blockhash" : "hex",               (string) the block hash
  "confirmations" : n,               (numeric) The confirmations
  "blocktime" : xxx,                 (numeric) The block time expressed in UNIX epoch time
  "fee" : n                          (numeric) The transaction fee in QBTC
}

Examples:
> qbitcoin-cli getrawtransaction "mytxid"
> qbitcoin-cli getrawtransaction "mytxid" true
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getrawtransaction", "params": ["mytxid", true]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getrawtransaction {
    my $self = shift;
    my $hash = pack("H*", $self->args->[0]);
    my $verbose = $self->args->[1] // TRUE;
    my $tx = QBitcoin::Transaction->get_by_hash($hash);
    if (!$tx) {
        return $self->response_error("No such mempool or blockchain transaction", ERR_INVALID_ADDRESS_OR_KEY);
    }
    if (!$verbose) {
        return $self->response_ok(unpack("H*", $tx->serialize));
    }

    my $res = $tx->as_hashref;
    if (defined $tx->block_height) {
        my $best_height = QBitcoin::Block->blockchain_height;
        $res->{confirmations} = $best_height - $tx->block_height + 1;
        $res->{block_height} = $tx->block_height;
        $res->{block_pos} = $tx->block_pos;
        my $block = QBitcoin::Block->best_block($tx->block_height) // QBitcoin::Block->find(height => $tx->block_height);
        if ($block) {
            my $best_block = QBitcoin::Block->best_block($best_height);
            $res->{confirm_weight} = $best_block->weight - ($block->prev_block ? $block->prev_block->weight : 0);
            $res->{blockhash} = unpack("H*", $block->hash);
            $res->{blocktime} = $block->time;
        }
    }
    else {
        $res->{confirmations} = 0;
        $res->{confirm_weight} = 0;
    }
    return $self->response_ok($res);
}

$PARAMS{createrawtransaction} = "inputs outputs";
$HELP{createrawtransaction} = qq(
createrawtransaction [{"txid":"hex","vout":n},...] [{"address":amount},...]

Create a transaction spending the given inputs and creating new outputs.
Outputs can be addresses or data.
Returns hex-encoded raw transaction.
Note that the transaction's inputs are not signed, and
it is not stored in the wallet or transmitted to the network.
token_id cannot be different in the transaction outputs.
Empty token_id means creating new tokens, id will be assigned as txid.

Arguments:
1. inputs                      (json array, required) The inputs
     [
       {                       (json object)
         "txid": "hex",        (string, required) The transaction id
         "vout": n,            (numeric, required) The output number
       },
       ...
     ]
2. outputs                     (json array, required) The outputs (key-value pairs)
     [
       {                       (json object)
         "address": amount,      (numeric or string, required) A key-value pair. The key (string) is the qbitcoin address, the value (float or string) is the amount in QBTC
         "token_id": "hex",      (numeric or string, optional) Token id, txid of the token creation transaction or empty for new token
         "token_amount": amount, (numeric or string, optional) Amount in tokens
         "token_permissions": [ "mint", ... ], (json array, optional) Token permissions
         "token_decimals": n,    (numeric, optional) Token decimals, valid only for token creation, 0 to 18
         "token_name": "str",    (string, optional) Token name, valid only for token creation
         "token_symbol": "str",  (string, optional) Token symbol, valid only for token creation
       },
       ...
     ]

Result:
"hex"    (string) hex string of the transaction

Examples:
> qbitcoin-cli createrawtransaction '[{"txid":"myid","vout":0}]' '[{"address":0.01}]'
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "createrawtransaction", "params": ['[{"txid":"myid","vout":0}]', '[{"address":0.01}]"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_createrawtransaction {
    my $self = shift;
    my $inputs  = $self->args->[0];
    my $outputs = $self->args->[1];
    @$inputs <= MAX_INPUTS_PER_TX
        or return $self->response_error("Too many inputs: " . @$inputs . " > " . MAX_INPUTS_PER_TX, ERR_INVALID_REQUEST);
    @$outputs <= MAX_OUTPUTS_PER_TX
        or return $self->response_error("Too many outputs: " . @$outputs . " > " . MAX_OUTPUTS_PER_TX, ERR_INVALID_REQUEST);
    my @in  = map {{ txo => QBitcoin::TXO->new_txo(tx_in => pack("H*", $_->{txid}), num => $_->{vout}+0) }} @$inputs;
    my @out;
    my $token_hash;
    foreach my $out (@$outputs) {
        my ($txo, $out_token_hash) = create_txo($out);
        $txo or return $self->response_error("Invalid output", ERR_INVALID_REQUEST);
        push @out, @$txo;
        if (defined($out_token_hash)) {
            if (defined($token_hash) && $token_hash ne $out_token_hash) {
                return $self->response_error("Different token_id in outputs", ERR_INVALID_REQUEST);
            }
            $token_hash //= $out_token_hash;
        }
    }
    my $tx = QBitcoin::Transaction->new(
        in      => \@in,
        out     => \@out,
        tx_type => defined($token_hash) ? TX_TYPE_TOKENS : TX_TYPE_STANDARD,
        defined($token_hash) ? (token_hash => $token_hash) : (),
    );
    my $tx_data = $tx->serialize_unsigned;
    if (length($tx_data) > MAX_TX_SIZE) {
        return $self->response_error("Transaction size too large: " . length($tx_data) . " > " . MAX_TX_SIZE, ERR_INVALID_REQUEST);
    }
    return $self->response_ok(unpack("H*", $tx_data));
}

$PARAMS{sendrawtransaction} = "hexstring";
$HELP{sendrawtransaction} = qq(
sendrawtransaction "hexstring"

Submit a raw transaction (serialized, hex-encoded) to local node and network.

Also see createrawtransaction and signrawtransactionwithkey calls.

Arguments:
1. hexstring     (string, required) The hex string of the raw transaction

Result:
"hex"    (string) The transaction hash in hex

Examples:

Create a transaction
> qbitcoin-cli createrawtransaction '[{"txid" : "mytxid","vout":0}]" "{"myaddress":0.01}'
Sign the transaction, and get back the hex
> qbitcoin-cli signrawtransactionwithkey "myhex" '["myprivatekey"]'

Send the transaction (signed hex)
> qbitcoin-cli sendrawtransaction "signedhex"

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "sendrawtransaction", "params": ["signedhex"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_sendrawtransaction {
    my $self = shift;
    my $data = Bitcoin::Serialized->new(pack("H*", $self->args->[0]));
    my $tx = QBitcoin::Transaction->deserialize($data);
    if (!$tx || $data->length) {
        return $self->response_error("TX decode failed.", ERR_DESERIALIZATION_ERROR);
    }
    $tx->received_from = $self;
    if (QBitcoin::Transaction->has_pending($tx->hash)) {
        # Transaction already known, but in pending state
        return $self->response_error("Some inputs unknown.", ERR_VERIFY_ALREADY_IN_CHAIN);
    }
    if (QBitcoin::Transaction->check_by_hash($tx->hash)) {
        # Transaction already in blockchain, return its hash for idempotency
        return $self->response_ok(unpack("H*", $tx->hash));
    }
    if (!$tx->load_txo()) {
        return $self->response_error("Incorrect transaction data.", ERR_DESERIALIZATION_ERROR);
    }
    # Reject downgrade transactions (outputs to freeze address) when upgrade threshold reached
    if (my $best_block = QBitcoin::Block->best_block) {
        if (($best_block->upgraded // 0) >= UPGRADE_MAX_VALUE) {
            my $freeze_sh = hash160(QBT_FREEZE_SCRIPT);
            if (grep { ($_->scripthash // "") eq $freeze_sh } @{$tx->out}) {
                return $self->response_error("Conversion threshold reached, downgrade not accepted.", ERR_INVALID_REQUEST);
            }
        }
    }
    if ($tx->is_pending) {
        return $self->response_error("Some inputs unknown.", ERR_VERIFY_ALREADY_IN_CHAIN);
    }
    my $rc = $self->process_tx($tx);
    if (!defined($rc)) {
        return $self->response_error("Transaction fee is too low.", ERR_INVALID_REQUEST);
    }
    if ($rc != 0) {
        return $self->response_error("Transaction failed.", ERR_VERIFY_ALREADY_IN_CHAIN);
    }
    return $self->response_ok(unpack("H*", $tx->hash));
}

$SENSITIVE{signrawtransactionwithkey} = 1;
$PARAMS{signrawtransactionwithkey} = "hexstring privatekeys replace?";
$HELP{signrawtransactionwithkey} = qq(
signrawtransactionwithkey "hexstring" ["privatekey",...] ( replace )

Sign inputs for raw transaction (serialized, hex-encoded).
The second argument is an array of base58-encoded private
keys that will be the only keys used to sign the transaction.

Arguments:
1. hexstring                        (string, required) The transaction hex string
2. privkeys                         (json array, required) The base58-encoded private keys for signing
     [
       "privatekey",                (string) private key in base58-encoding
       ...
     ]
3. replace                           (boolean, optional, default=false) Whether to allow signing with already spent inputs (only if not confirmed yet)

Result:
{                             (json object)
  "hex" : "hex",              (string) The hex-encoded raw transaction with signature(s)
  "hash" : "hex",             (string) The hex-encoded transaction hash (txid)
  "complete" : true|false,    (boolean) If the transaction has a complete set of signatures
  "errors" : [                (json array, optional) Script verification errors (if there are any)
    {                         (json object)
      "txid" : "hex",         (string) The hash of the referenced, previous transaction
      "vout" : n,             (numeric) The index of the output to spent and used as input
      "scriptSig" : "hex",    (string) The hex-encoded signature script
      "error" : "str"         (string) Verification or signing error related to the input
    },
    ...
  ]
}

If the transaction burns tokens (spends token outputs without transferring the
tokens to the outputs), it is signed anyway and the response contains a top-level
"warning" field describing the burned tokens.

Examples:
> qbitcoin-cli signrawtransactionwithkey "myhex" '["key1","key2"]'
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "signrawtransactionwithkey", "params": ["myhex", ["key1","key2"]]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_signrawtransactionwithkey {
    my $self = shift;
    my $data = Bitcoin::Serialized->new(pack("H*", $self->args->[0]));
    my $privkeys = $self->args->[1];
    my $replace = $self->args->[2] // FALSE;
    my $tx = QBitcoin::Transaction->deserialize($data);
    if (!$tx || $data->length) {
        return $self->response_error("TX decode failed.", ERR_DESERIALIZATION_ERROR);
    }
    if (!$tx->is_standard && !$tx->is_tokens) {
        return $self->response_error("Non-standard transaction.", ERR_INVALID_REQUEST);
    }
    $tx->received_from = $self;
    if (!$tx->load_inputs(1)) {
        return $self->response_error("Incorrect transaction data.", ERR_DESERIALIZATION_ERROR);
    }
    if ($tx->is_pending) {
        return $self->response_error("Some inputs unknown.", ERR_DESERIALIZATION_ERROR);
    }
    # A dumped delegation WIF carries the delegate pubkeyhash in its payload
    my @address = map { QBitcoin::MyAddress->new(private_key => $_, deleg_pubkeyhash => wif_delegation_hash($_)) } @$privkeys;

    # Fill the reclaim_id sentinel of any downgrade freeze output with the first
    # signing key's pubkey hash256, so the user can later reclaim the QBTC if the
    # downgrade never completes. A single hash256 reclaim_id serves EC and
    # post-quantum keys alike. Must happen before signing (it changes the outputs).
    {
        my $freeze_sh = hash160(QBT_FREEZE_SCRIPT);
        for my $out (@{$tx->out}) {
            next unless ($out->scripthash // "") eq $freeze_sh;
            next unless length($out->data // "") >= 32 && substr($out->data, 0, 32) eq ("\x00" x 32);
            my $key = $address[0]
                or return $self->response_error("No private key to fill downgrade reclaim_id", ERR_INVALID_REQUEST);
            $out->{data} = hash256($key->pubkey) . substr($out->data, 32);
        }
    }

    my @errors;
    my $input_amount = 0;
    foreach my $num (0 .. $#{$tx->in}) {
        my $in = $tx->in->[$num];
        my $txo = $in->{txo};
        if ($txo->tx_out) {
            # Already confirmed spent
            return $self->response_error(sprintf("Input %s:%u already confirmed spent.", $txo->tx_in_str, $txo->num), ERR_DESERIALIZATION_ERROR);
        }
        elsif (!$txo->unspent && !$replace) {
            # Unconfirmed spent
            return $self->response_error(sprintf("Input %s:%u already spent.", $txo->tx_in_str, $txo->num), ERR_DESERIALIZATION_ERROR);
        }
        $input_amount += $txo->value;
        my ($address, $script);
        foreach my $addr (@address) {
            if ($script = $addr->script_by_hash($txo->scripthash)) {
                $address = $addr;
                last;
            }
        }
        if ($address) {
            $tx->make_sign($in, $address, $num);
        }
        else {
            push @errors, {
                txid       => unpack("H*", $txo->tx_in),
                vout       => $in->{txo}->num,
                scripthash => unpack("H*", $txo->scripthash),
                error      => "Unknown scripthash",
            };
        }
    }
    if (!@errors) {
        $tx->calculate_hash;
        if (QBitcoin::Transaction->check_by_hash($tx->hash)) {
            return $self->response_error("Transaction already published.", ERR_VERIFY_ALREADY_IN_CHAIN);
        }
    }

    my $output_amount;
    foreach my $out (@{$tx->out}) {
        $output_amount += $out->value;
    }
    if ($input_amount < $output_amount) {
        return $self->response_error("Insufficient funds: $input_amount < $output_amount", ERR_INVALID_REQUEST);
    }
    my $tx_data = $tx->serialize_unsigned;
    if (length($tx_data) > MAX_TX_SIZE) {
        return $self->response_error("Transaction size too large: " . length($tx_data) . " > " . MAX_TX_SIZE, ERR_INVALID_REQUEST);
    }
    my $fee_per_kb = ($input_amount - $output_amount) * 1024 / length($tx_data);
    my $max_fee_per_kb = $self->max_fee_per_kb;
    if ($max_fee_per_kb && $fee_per_kb > $max_fee_per_kb) {
        return $self->response_error("Transaction fee too high: " . $fee_per_kb / DENOMINATOR . " > " . $max_fee_per_kb / DENOMINATOR . " QBTC/kb", ERR_INVALID_REQUEST);
    }

    my ($token_err, $token_warning) = check_tx_tokens_balance($tx);
    if ($token_err) {
        return $self->response_error("Tokens balance check failed: $token_err", ERR_INVALID_REQUEST);
    }

    return $self->response_ok({
        hex      => unpack("H*", $tx_data),
        hash     => unpack("H*", $tx->hash),
        complete => @errors ? FALSE : TRUE,
        errors   => \@errors,
    }, $token_warning);
}

sub max_fee_per_kb {
    my $self = shift;
    return $config->{max_fee_per_kb} if defined $config->{max_fee_per_kb};
    return 0 if $config->{testnet} || $config->{regtest};
    return 100000; # 0.001 QBTC
}

$PARAMS{decoderawtransaction} = "hexstring";
$HELP{decoderawtransaction} = qq(
decoderawtransaction "hexstring"

Return a JSON object representing the serialized, hex-encoded transaction.

Arguments:
1. hexstring    (string, required) The transaction hex string

Result:
{                                    (json object)
  "txid" : "hex",                    (string) The transaction id
  "hash" : "hex",                    (string) The transaction hash (the same as the txid)
  "size" : n,                        (numeric) The serialized transaction size
  "type" : "str",                    (string) The transaction type
  "in" : [                           (json array)
    {                                (json object)
      "txid" : "hex",                (string) The transaction id
      "num" : n,                     (numeric) The output number
      "redeem_script" : "hex",       (string, optional) The redeem script in hex
      "siglist" : [                  (json object) The list of signatures
        "hex"                        (string) hex
      },
    },
    ...
  ],
  "out" : [                          (json array)
    {                                (json object)
      "value" : n,                   (numeric) The value in QBTC
      "address" : "str",             (string) qbitcoin address
      "data" : "hex",                (string, optional) The data in hex (if a data output)
    },
    ...
  ]
}

Examples:
> qbitcoin-cli decoderawtransaction "hexstring"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "decoderawtransaction", "params": ["hexstring"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_decoderawtransaction {
    my $self = shift;
    my $data = Bitcoin::Serialized->new(pack("H*", $self->args->[0]));

    my $tx = QBitcoin::Transaction->deserialize($data);
    if (!$tx || $data->length) {
        return $self->response_error("TX decode failed.", ERR_DESERIALIZATION_ERROR);
    }
    return $self->response_ok($tx->as_hashref);
}

$PARAMS{getmempoolinfo} = "";
$HELP{getmempoolinfo} = qq(
getmempoolinfo

Returns details on the active state of the TX memory pool.

Result:
{                            (json object)
  "loaded" : true|false,     (boolean) True if the mempool is fully loaded
  "size" : n,                (numeric) Current tx count
  "bytes" : n,               (numeric) Sum of all transaction sizes
}

Examples:
> qbitcoin-cli getmempoolinfo
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getmempoolinfo", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getmempoolinfo {
    my $self = shift;

    my @mempool = QBitcoin::Transaction->mempool_list();
    return $self->response_ok({
        loaded => mempool_synced() ? TRUE : FALSE,
        size   => @mempool+0,
        bytes  => sum0(map { $_->size } @mempool),
    });
}

$PARAMS{getrawmempool} = "verbose?";
$HELP{getrawmempool} = qq(
getrawmempool ( verbose )

Returns all transaction ids in memory pool as a json array of string transaction ids.

Arguments:
1. verbose             (boolean, optional, default=false) True for a json object, false for array of transaction ids

Result (for verbose = false):
[           (json array)
  "hex",    (string) The transaction id
  ...
]

Result (for verbose = true):
{                                         (json object)
  "transactionid" : {                     (json object)
    "size" : n,                           (numeric) transaction size
    "fee" : n,                            (numeric) transaction fee
    "time" : xxx,                         (numeric) local time transaction entered pool in seconds since 1 Jan 1970 GMT
  },
  ...
}

Examples:
> qbitcoin-cli getrawmempool true
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getrawmempool", "params": [true]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getrawmempool {
    my $self = shift;
    my $verbose = $self->args->[0] // FALSE;

    return $self->response_ok([ map { $verbose ? $_->as_hashref : unpack("H*", $_->hash) } QBitcoin::Transaction->mempool_list() ]);
}

$PARAMS{validateaddress} = "address";
$HELP{validateaddress} = qq(
validateaddress "address"

Return information about the given qbitcoin address.

Arguments:
1. address    (string, required) The address to validate

Result:
{                               (json object)
  "isvalid" : true|false,       (boolean) If the address is valid or not. If not, this is the only property returned.
  "address" : "str",            (string) The qbitcoin address validated
  "scriptHash" : "hex",         (string) The hex-encoded scriptHash generated by the address
}

Examples:
> qbitcoin-cli validateaddress "myaddress"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "validateaddress", "params": ["myaddress"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_validateaddress {
    my $self = shift;
    my $address = $self->args->[0] // FALSE;

    my $scripthash = eval { scripthash_by_address($address) };
    if ($scripthash) {
        return $self->response_ok({ isvalid => TRUE, address => $address, scripthash => unpack("H*", $scripthash) });
    }
    else {
        return $self->response_ok({ isvalid => FALSE });
    }
}

$PARAMS{getnetworkinfo} = "";
$HELP{getnetworkinfo} = qq(
getnetworkinfo
Returns an object containing various state info regarding P2P networking.

Result:
{                                                    (json object)
  "version" : n,                                     (numeric) the server version
  "protocolversion" : n,                             (numeric) the protocol version
  "connections" : n,                                 (numeric) the total number of connections
  "connections_in" : n,                              (numeric) the number of inbound connections
  "connections_out" : n,                             (numeric) the number of outbound connections
  "networkactive" : true|false,                      (boolean) whether p2p networking is enabled
  "networks" : [                                     (json array) information per network
    {                                                (json object)
      "name" : "str",                                (string) network (ipv4, ipv6 or onion)
      "limited" : true|false,                        (boolean) is the network limited using -onlynet?
      "reachable" : true|false,                      (boolean) is the network reachable?
      "proxy" : "str",                               (string) ("host:port") the proxy that is used for this network, or empty if none
      "proxy_randomize_credentials" : true|false     (boolean) Whether randomized credentials are used
    },
    ...
  ],
}

Examples:
> qbitcoin-cli getnetworkinfo
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getnetworkinfo", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getnetworkinfo {
    my $self = shift;
    my $connect_in = 0;
    my $connect_out = 0;
    foreach my $connection (QBitcoin::ConnectionList->connected(PROTOCOL_QBITCOIN)) {
        $connection->direction == DIR_IN ? $connect_in++ : $connect_out++;
    }
    return $self->response_ok({
        version         => VERSION,
        subversion      => SOFTWARE,
        protocolversion => QBitcoin::Protocol->PROTOCOL_VERSION,
        connections_in  => $connect_in,
        connections_out => $connect_out,
        connections     => $connect_in + $connect_out,
        networkactive   => TRUE,
        networks        => [{
            name      => "ipv4",
            reachable => TRUE,
        }, {
            name      => "ipv6",
            reachable => TRUE,
        }],
    });
}

# Just to satisfy btc explorer
$PARAMS{getindexinfo} = "";
$HELP{getindexinfo} = qq(
getindexinfo

Returns the status of all available indices currently running in the node.

Result:
{                               (json object)
  "name" : {                    (json object) The name of the index
    "synced" : true|false,      (boolean) Whether the index is synced or not
    "best_block_height" : n     (numeric) The block height to which the index is synced
  }
}

Examples:
> qbitcoin-cli getindexinfo
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getindexinfo", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getindexinfo {
    my $self = shift;
    return $self->response_ok({
        txindex => {
            synced => TRUE,
        },
    });
}

$PARAMS{getchaintxstats} = "nblocks? blockhash?";
$HELP{getchaintxstats} = qq(
getchaintxstats ( nblocks "blockhash" )

Compute statistics about the total number and rate of transactions in the chain.

Arguments:
1. nblocks      (numeric, optional, default=one month) Size of the window in number of blocks
2. blockhash    (string, optional, default=chain tip) The hash of the block that ends the window.

Result:
{                                       (json object)
  "time" : xxx,                         (numeric) The timestamp for the final block in the window, expressed in UNIX epoch time
  "txcount" : n,                        (numeric) The total number of transactions in the chain up to that point
  "window_final_block_hash" : "hex",    (string) The hash of the final block in the window
  "window_final_block_height" : n,      (numeric) The height of the final block in the window.
  "window_block_count" : n,             (numeric) Size of the window in number of blocks
  "window_tx_count" : n,                (numeric) The number of transactions in the window. Only returned if "window_block_count" is > 0
  "window_interval" : n,                (numeric) The elapsed time in the window in seconds. Only returned if "window_block_count" is > 0
  "txrate" : n                          (numeric) The average rate of transactions per second in the window. Only returned if "window_interval" is > 0
}

Examples:
> qbitcoin-cli getchaintxstats
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getchaintxstats", "params": [2016]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getchaintxstats {
    my $self = shift;
    my $nblocks = $self->args->[0] // 30*24*3600/BLOCK_INTERVAL;
    my $last_block;
    if (my $blockhash = $self->args->[1]) {
        $last_block = $self->get_block_by_hash(pack("H*", $blockhash))
            or return $self->response_error("Block not found", ERR_INVALID_ADDRESS_OR_KEY);
    }
    else {
        my $best_height = QBitcoin::Block->blockchain_height;
        $last_block = QBitcoin::Block->best_block($best_height)
            or return $self->response_error("Block not found", ERR_INVALID_ADDRESS_OR_KEY);
    }
    my $start_height = $last_block->height - $nblocks + 1;
    $start_height = 0 if $start_height < 0;
    # TODO: count via QBitcoin::Transaction->fetch(..., -func => { count => 'count(*)' });
    my ($count) = dbh->selectrow_array(
        "SELECT COUNT(*) FROM `" . QBitcoin::Transaction->TABLE . "` WHERE block_height >= ? AND block_height <= ?",
        undef, $start_height, $last_block->height);

    return $self->response_ok({
        time                      => $last_block->time,
        window_final_block_hash   => unpack("H*", $last_block->hash),
        window_final_block_height => $last_block->height,
        window_block_count        => $last_block->height - $start_height + 1,
        window_tx_count           => $count,
        window_interval           => ($last_block->height - $start_height) * BLOCK_INTERVAL,
        $last_block->height > $start_height ? ( txrate => $count * BLOCK_INTERVAL / ($last_block->height - $start_height) ) : (),
    });
}

$PARAMS{getblockstats} = "hash_or_height";
$HELP{getblockstats} = qq(
getblockstats hash_or_height

Compute per block statistics for a given window. All amounts are in satoshis.

Arguments:
1. hash_or_height    (string or numeric, required) The block hash or height of the target block

Result:
{                              (json object)
  "avgfee" : n,                (numeric) Average fee in the block
  "avgfeerate" : n,            (numeric) Average feerate (in satoshis per virtual byte)
  "avgtxsize" : n,             (numeric) Average transaction size
  "blockhash" : "hex",         (string) The block hash (to check for potential reorgs)
  "feerate_percentiles" : [    (json array) Feerates at the 10th, 25th, 50th, 75th, and 90th percentile weight unit (in satoshis per virtual byte)
    n,                         (numeric) The 10th percentile feerate
    n,                         (numeric) The 25th percentile feerate
    n,                         (numeric) The 50th percentile feerate
    n,                         (numeric) The 75th percentile feerate
    n                          (numeric) The 90th percentile feerate
  ],
  "height" : n,                (numeric) The height of the block
  "ins" : n,                   (numeric) The number of inputs (excluding coinbase)
  "maxfee" : n,                (numeric) Maximum fee in the block
  "maxfeerate" : n,            (numeric) Maximum feerate (in satoshis per virtual byte)
  "maxtxsize" : n,             (numeric) Maximum transaction size
  "medianfee" : n,             (numeric) Truncated median fee in the block
  "mediantime" : n,            (numeric) The block median time past
  "mediantxsize" : n,          (numeric) Truncated median transaction size
  "minfee" : n,                (numeric) Minimum fee in the block
  "minfeerate" : n,            (numeric) Minimum feerate (in satoshis per virtual byte)
  "mintxsize" : n,             (numeric) Minimum transaction size
  "outs" : n,                  (numeric) The number of outputs
  "subsidy" : n,               (numeric) The block subsidy
  "time" : n,                  (numeric) The block time
  "total_out" : n,             (numeric) Total amount in all outputs (excluding coinbase and thus reward [ie subsidy + totalfee])
  "total_size" : n,            (numeric) Total size of all non-coinbase transactions
  "total_weight" : n,          (numeric) Total weight of all non-coinbase transactions
  "totalfee" : n,              (numeric) The fee total
  "txs" : n,                   (numeric) The number of transactions (including coinbase)
  "utxo_increase" : n,         (numeric) The increase/decrease in the number of unspent outputs
  "utxo_size_inc" : n          (numeric) The increase/decrease in size for the utxo index (not discounting op_return and similar)
}

Examples:
> qbitcoin-cli getblockstats '"00000000c937983704a73af28acdec37b049d214adbda81d7e2a3dd146f6ed09"'
> qbitcoin-cli getblockstats 1000
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblockstats", "params": ["00000000c937983704a73af28acdec37b049d214adbda81d7e2a3dd146f6ed09"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getblockstats", "params": [1000]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getblockstats {
    my $self = shift;
    my $hash_or_height = $self->args->[0];

    my $block;
    if (length($hash_or_height) == 64) {
        $block = $self->get_block_by_hash(pack("H*", $hash_or_height));
    }
    else {
        $block = QBitcoin::Block->best_block($hash_or_height) // QBitcoin::Block->find(height => $hash_or_height);
    }
    $block or return $self->response_error("Block not found", ERR_INVALID_ADDRESS_OR_KEY);
    my @tx = sort { $a->fee/$a->size <=> $b->fee/$b->size } grep { $_->fee >= 0 } @{$block->transactions};
    my $res = {
        blockhash  => unpack("H*", $block->hash),
        height     => $block->height,
        ins        => sum0(map { scalar @{$_->in} } @{$block->transactions}),
        outs       => sum0(map { scalar @{$_->out} } @{$block->transactions}),
        subsidy    => 0,
        time       => $block->time,
        total_out  => sum0(map { $_->value } map { @{$_->out} } @{$block->transactions})/DENOMINATOR,
        total_size => sum0(map { $_->size } @{$block->transactions}),
        txs        => @{$block->transactions}+0,
        totalfee   => 0,
    };
    $res->{utxo_increase} = $res->{outs} - $res->{ins};
    if (@tx) {
        $res->{avgfee}       = sum(map { $_->fee } @tx) / @tx;
        $res->{avgfeerate}   = sum(map { $_->fee/$_->size } @tx) / @tx;
        $res->{avgtxsize}    = sum(map { $_->size } @{$block->transactions}) / @{$block->transactions};
        $res->{maxfee}       = max(map { $_->fee } @tx);
        $res->{maxfeerate}   = $tx[-1]->fee/$tx[-1]->size;
        $res->{maxtxsize}    = max(map { $_->size } @{$block->transactions});
        $res->{medianfee}    = (sort { $a->fee <=> $b->fee } @tx)[@tx/2]->fee;
        $res->{mediantxsize} = (sort { $a->size <=> $b->size } @{$block->transactions})[@{$block->transactions}/2]->size;
        $res->{minfee}       = min(map { $_->fee } @tx);
        $res->{minfeerate}   = $tx[0]->fee/$tx[0]->size;
        $res->{mintxsize}    = min(map { $_->size } @{$block->transactions});
        $res->{subsidy}      = -$block->transactions->[0]->fee;
        $res->{totalfee}     = -$block->transactions->[0]->fee;
        $res->{feerate_percentiles} = [ map { $tx[@tx*$_/100]->fee/$tx[@tx*$_/100]->size } qw(10 25 50 75 90) ];
    }
    return $self->response_ok($res);
}

$PARAMS{getmempoolentry} = "txid verbose?";
$HELP{getmempoolentry} = qq(
getmempoolentry "txid"

Returns mempool data for given transaction

Arguments:
1. txid    (string, required) The transaction id (must be in mempool)

Result:
{                                       (json object)
  "size" : n,                           (numeric) transaction size in bytes
  "fee"  : n,                           (numeric) transaction fee in QBTC
  "time" : xxx,                         (numeric) local time transaction entered pool in seconds since 1 Jan 1970 GMT
}

Examples:
> qbitcoin-cli getmempoolentry "mytxid"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getmempoolentry", "params": ["mytxid"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getmempoolentry {
    my $self = shift;
    my $hash = pack("H*", $self->args->[0]);
    my $verbose = $self->args->[1] // TRUE;
    my $tx = QBitcoin::Transaction->get($hash)
        or return $self->response_error("No such mempool", ERR_INVALID_ADDRESS_OR_KEY);
    return $self->response_ok($tx->as_hashref);
}

$SENSITIVE{importprivkey} = 1;
$PARAMS{importprivkey} = "privkey address_type?";
$HELP{importprivkey} = qq(
importprivkey "privkey" ( address_type )

Adds a private key (as returned by dumpprivkey) to your wallet.

A delegation owner key (as returned by getnewaddress with delegate_pubkeyhash)
is recognized automatically and imports the delegated-staking address it
controls; no extra arguments are needed.

When the wallet private keys are encrypted the wallet must be unlocked first
(see walletunlock); the imported key is stored encrypted. Otherwise the key is
stored in plaintext and the command warns about it.

Arguments:
1. privkey        (string, required) The private key (see dumpprivkey)
2. address_type   (string, optional, default="ecdsa") The address type. Options are "ecdsa", "schnorr", "falcon".

Result:
null    (json null)

Examples:

Dump a private key
> qbitcoin-cli dumpprivkey "myaddress"

Import the private key
> qbitcoin-cli importprivkey "mykey"

Import as schnorr key
> qbitcoin-cli importprivkey "mykey" "schnorr"

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "importprivkey", "params": ["mykey"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_importprivkey {
    my $self = shift;
    if (QBitcoin::Wallet->is_encrypted && !QBitcoin::Wallet->unlocked) {
        return $self->response_error("The wallet is locked; unlock it with walletunlock first", ERR_WALLET_UNLOCK_NEEDED);
    }
    my ($private_key, $delegate_pubkeyhash) = wif_decode($self->args->[0]);
    my $pk_alg = $self->args->[1];
    if (!$pk_alg) {
        ($pk_alg) = pk_alg($private_key)
            or return $self->response_error("Incorrect private key", ERR_INVALID_ADDRESS_OR_KEY);
    }
    my $privkey = pk_import($private_key, $pk_alg)
        or return $self->response_error("Incorrect private key", ERR_INVALID_ADDRESS_OR_KEY);
    my $pubkey = $privkey->pubkey_by_privkey
        or return $self->response_error("This type of private key is not supported for my_address", ERR_INVALID_ADDRESS_OR_KEY);
    my $address = $delegate_pubkeyhash
        ? delegation_address(pubkeyhash_by_pubkey($pubkey, $pk_alg), $delegate_pubkeyhash)
        : address_by_pubkey($pubkey, $pk_alg);
    if (grep { $address eq $_->address } QBitcoin::MyAddress->my_address()) {
        return $self->response_ok("Private key for address $address already imported");
    }
    my $wif = $delegate_pubkeyhash
        ? delegation_import_format($private_key, $delegate_pubkeyhash)
        : wallet_import_format($private_key);
    my $warning = "";
    if (QBitcoin::Wallet->is_encrypted) {
        $wif = QBitcoin::Wallet->encrypt_pk($wif, $address);
    }
    elsif (!QBitcoin::Password->is_set) {
        $warning = "; WARNING: the key is stored unencrypted, set a wallet password with setwalletpassword to encrypt the wallet keys";
    }
    elsif ($config->{encrypted_private_keys} // 1) {
        $warning = "; WARNING: the key is stored unencrypted, change a wallet password to encrypt the wallet keys";
    }
    else {
        $warning = "; WARNING: the key is stored unencrypted ('encrypted_private_keys' is disabled)";
    }
    my $my_address = QBitcoin::MyAddress->create({
        private_key => $wif,
        pubkey      => $pubkey,
        address     => $address,
        algo        => $pk_alg,
        $delegate_pubkeyhash ? (deleg_pubkeyhash => $delegate_pubkeyhash) : (),
    });
    QBitcoin::Generate->load_address_utxo($my_address);

    return $self->response_ok("Private key for address $address imported$warning");
}

$PARAMS{importaddress} = "address tag?";
$HELP{importaddress} = qq(
importaddress "address" [ "tag" ]

Adds a watch-only address to the wallet.
Transactions to this address will be tracked and generate notifications
if a notification channel is configured.

Arguments:
1. address    (string, required) The qbitcoin address to watch
2. tag        (string, optional) A tag for grouping notifications

Result:
"str"    (string) Result message

Examples:
> qbitcoin-cli importaddress "bqXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
> qbitcoin-cli importaddress "bqXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX" "exchange"

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "importaddress", "params": ["address"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_importaddress {
    my $self = shift;
    my $address_str = $self->args->[0];
    my $tag_name    = $self->args->[1];
    my $scripthash = scripthash_by_address($address_str);

    # Check if already exists
    if (QBitcoin::MyAddress->get_by_hash($scripthash, 1)) {
        return $self->response_ok("Address $address_str is already in the wallet");
    }

    my %attrs = (address => $address_str);
    if (defined $tag_name && length $tag_name) {
        my $tag = QBitcoin::Tag->get_or_create($tag_name);
        $attrs{tag_id} = $tag->id;
    }

    # Create watch-only address (no private_key)
    QBitcoin::MyAddress->create(\%attrs);

    return $self->response_ok("Watch-only address $address_str imported");
}

$PARAMS{getnewstakingkey} = "address_type?";
$HELP{getnewstakingkey} = qq(
getnewstakingkey ( address_type )

Creates a new staking key for delegated staking and stores it in the wallet.
A staking key can only sign the stake branch of a delegation covenant; it
never controls money. Publish the returned pubkeyhash: an owner builds a
delegated-staking address from it (see getnewaddress) and this node registers
the address with adddelegationaddress.

When the wallet private keys are encrypted the wallet must be unlocked first
(see walletunlock); the new key is stored encrypted.

Arguments:
1. address_type    (string, optional, default="ecdsa") The key type. Options are "ecdsa", "schnorr", "falcon".

Result:
{
    "pubkeyhash",  (string) The staking pubkeyhash to publish for the owners
}

Examples:
> qbitcoin-cli getnewstakingkey
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getnewstakingkey", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getnewstakingkey {
    my $self = shift;
    if (QBitcoin::Wallet->is_encrypted && !QBitcoin::Wallet->unlocked) {
        return $self->response_error("The wallet is locked; unlock it with walletunlock first", ERR_WALLET_UNLOCK_NEEDED);
    }
    my $algo = $self->args->[0] // CRYPT_ALGO_ECDSA;
    my $keypair = generate_keypair($algo);
    my $pubkey = $keypair->pubkey_by_privkey
        or return $self->response_error("This type of key is not supported for staking", ERR_INVALID_ADDRESS_OR_KEY);
    return $self->_store_staking_key($keypair->pk_serialize, $pubkey, $algo);
}

$SENSITIVE{importstakingkey} = 1;
$PARAMS{importstakingkey} = "privkey address_type?";
$HELP{importstakingkey} = qq(
importstakingkey "privkey" ( address_type )

Adds a staking key for delegated staking (as returned by dumpstakingkey) to
the wallet. See getnewstakingkey.

WARNING: a staking key must run on exactly ONE node. If the node you exported
it from is still staking, two nodes will stake the same delegated outputs -
that is equivocation, and the slashing penalty is paid from the owners' coins
entrusted to you. Stop the old node before importing the key here.

Arguments:
1. privkey        (string, required) The staking private key (an ordinary WIF)
2. address_type   (string, optional, default from the key) The key type. Options are "ecdsa", "schnorr", "falcon".

Result:
{
    "pubkeyhash",  (string) The staking pubkeyhash to publish for the owners
}

Examples:
> qbitcoin-cli importstakingkey "mykey"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "importstakingkey", "params": ["mykey"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_importstakingkey {
    my $self = shift;
    if (QBitcoin::Wallet->is_encrypted && !QBitcoin::Wallet->unlocked) {
        return $self->response_error("The wallet is locked; unlock it with walletunlock first", ERR_WALLET_UNLOCK_NEEDED);
    }
    my ($private_key, $delegate_pubkeyhash) = wif_decode($self->args->[0]);
    if ($delegate_pubkeyhash) {
        return $self->response_error("This is a delegation owner key; use importprivkey for it", ERR_INVALID_ADDRESS_OR_KEY);
    }
    my $pk_alg = $self->args->[1];
    if (!$pk_alg) {
        ($pk_alg) = pk_alg($private_key)
            or return $self->response_error("Incorrect private key", ERR_INVALID_ADDRESS_OR_KEY);
    }
    my $privkey = pk_import($private_key, $pk_alg)
        or return $self->response_error("Incorrect private key", ERR_INVALID_ADDRESS_OR_KEY);
    my $pubkey = $privkey->pubkey_by_privkey
        or return $self->response_error("This type of private key is not supported for staking", ERR_INVALID_ADDRESS_OR_KEY);
    return $self->_store_staking_key($private_key, $pubkey, $pk_alg);
}

sub _store_staking_key {
    my $self = shift;
    my ($private_key, $pubkey, $algo) = @_;
    my $pubkeyhash = pubkeyhash_by_pubkey($pubkey, $algo);
    my $pubkeyhash_str = pubkeyhash_str($pubkeyhash);
    if (QBitcoin::StakingKey->get_by_pubkeyhash($pubkeyhash)) {
        return $self->response_ok({ pubkeyhash => $pubkeyhash_str });
    }
    my $wif = wallet_import_format($private_key);
    if (QBitcoin::Wallet->is_encrypted) {
        $wif = QBitcoin::Wallet->encrypt_pk($wif, $pubkeyhash_str);
    }
    my $staking_key = QBitcoin::StakingKey->create({
        private_key => $wif,
        pubkey      => $pubkey,
        algo        => $algo,
    })
        or return $self->response_error("Cannot store the staking key", ERR_INTERNAL_ERROR);
    return $self->response_ok({ pubkeyhash => $pubkeyhash_str });
}

$PARAMS{dumpstakingkey} = "staking_pubkeyhash/pubkeyhash";
$REQUIRE_PASSWORD{dumpstakingkey} = 1;
$HELP{dumpstakingkey} = qq(
dumpstakingkey "staking_pubkeyhash"

Reveals the staking private key for the given staking pubkeyhash.
Then the importstakingkey can be used with this output.
Enabled by 'allow_dumpprivkey' config option.

A staking key must run on exactly ONE node: when moving it, stop this node
before the new one starts staking, otherwise both will stake the same
delegated outputs (equivocation, slashed from the owners' coins).

Arguments:
1. staking_pubkeyhash    (string, required) The staking pubkeyhash (see liststakingkeys)

Result:
"key"    (string) The staking private key

Examples:
> qbitcoin-cli dumpstakingkey "6nXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "dumpstakingkey", "params": ["6nXXXX"], "password": "mysecret"}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_dumpstakingkey {
    my $self = shift;
    $config->{allow_dumpprivkey}
        or return $self->response_error("This command is disabled", ERR_INVALID_ADDRESS_OR_KEY);
    my $staking_key = QBitcoin::StakingKey->get_by_pubkeyhash($self->args->[0])
        or return $self->response_error("No such staking key", ERR_INVALID_ADDRESS_OR_KEY);
    my $stored = $staking_key->private_key;
    QBitcoin::Wallet->is_encrypted_pk($stored)
        or return $self->response_ok($stored);
    my $master; # decrypt_pk defaults to the in-memory master key when unlocked
    if (!QBitcoin::Wallet->unlocked) {
        $master = QBitcoin::Wallet->master_key_with_password($self->auth_password // "")
            or return $self->response_error("Cannot unlock the wallet master key with this password", ERR_WALLET_PASSWORD_INCORRECT);
    }
    my $wif = QBitcoin::Wallet->decrypt_pk($stored, $staking_key->pubkeyhash_string, $master)
        or return $self->response_error("Cannot decrypt the private key", ERR_INTERNAL_ERROR);
    $self->hide_response = 1;
    return $self->response_ok($wif);
}

$PARAMS{liststakingkeys} = "";
$HELP{liststakingkeys} = qq(
liststakingkeys

Returns the list of the wallet staking keys for delegated staking.

Result:
[
  {
    "pubkeyhash" : "str",   (string) The staking pubkeyhash (publish it for the owners)
    "algo" : "str",         (string) Key algorithm
    "delegations" : n,      (numeric) Number of delegated addresses on this key
  },
  ...
]

Examples:
> qbitcoin-cli liststakingkeys
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "liststakingkeys", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_liststakingkeys {
    my $self = shift;
    my %delegations;
    $delegations{$_->staking_key_id}++ foreach QBitcoin::Delegation->list;
    my @list = map {{
        pubkeyhash  => $_->pubkeyhash_string,
        algo        => CRYPT_ALGO_NAMES->{$_->algo},
        delegations => $delegations{$_->id} // 0,
    }} QBitcoin::StakingKey->list;
    return $self->response_ok(\@list);
}

$PARAMS{createdelegationaddress} = "owner_pubkeyhash/pubkeyhash staking_pubkeyhash/pubkeyhash";
$HELP{createdelegationaddress} = qq(
createdelegationaddress "owner_pubkeyhash" "staking_pubkeyhash"

Computes the delegated-staking address for the given owner and delegate
pubkeyhashes. Stateless: does not touch the wallet; use it to verify that
both sides derived the same address.

Arguments:
1. owner_pubkeyhash      (string, required) The owner pubkeyhash
2. staking_pubkeyhash    (string, required) The delegate staking pubkeyhash

Result:
{
    "address",        (string) The delegated-staking address
    "redeem_script",  (string) The hex-encoded covenant script
}

Examples:
> qbitcoin-cli createdelegationaddress "6nXXXX" "6nYYYY"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "createdelegationaddress", "params": ["6nXXXX", "6nYYYY"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_createdelegationaddress {
    my $self = shift;
    my ($owner_pubkeyhash, $staking_pubkeyhash) = @{$self->args};
    return $self->response_ok({
        address       => delegation_address($owner_pubkeyhash, $staking_pubkeyhash),
        redeem_script => unpack("H*", delegation_script($owner_pubkeyhash, $staking_pubkeyhash)),
    });
}

$PARAMS{adddelegationaddress} = "owner_pubkeyhash/pubkeyhash staking_pubkeyhash/pubkeyhash?";
$HELP{adddelegationaddress} = qq(
adddelegationaddress "owner_pubkeyhash" ( "staking_pubkeyhash" )

Registers a delegated-staking address on this (delegate) node: the address is
built from the owner pubkeyhash and a wallet staking key, and its coins are
staked by this node from now on. The staking key can only return the full
value back to the address; the block reward is distributed according to the
reward_addr config option ("reward_addr <address> <share>" keeps the share as
this node's fee and sends the remainder to the delegated address).

WARNING: a delegated address must be staked by ONE node only. If this staking
key runs on another node too (a hot spare, an old node left running after a
migration), both will stake the same outputs - that is equivocation, and the
slashing penalty is paid from the owner's coins entrusted to you.

Arguments:
1. owner_pubkeyhash      (string, required) The owner pubkeyhash received from the coins owner
2. staking_pubkeyhash    (string, optional) The wallet staking key to use; may be omitted when the wallet has exactly one

Result:
{
    "address",     (string) The delegated-staking address
}

Examples:
> qbitcoin-cli adddelegationaddress "6nXXXX"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "adddelegationaddress", "params": ["6nXXXX"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_adddelegationaddress {
    my $self = shift;
    my ($owner_pubkeyhash, $staking_pubkeyhash) = @{$self->args};
    my $staking_key;
    if ($staking_pubkeyhash) {
        $staking_key = QBitcoin::StakingKey->get_by_pubkeyhash($staking_pubkeyhash)
            or return $self->response_error("No such staking key", ERR_INVALID_ADDRESS_OR_KEY);
    }
    else {
        my @keys = QBitcoin::StakingKey->list;
        @keys == 1
            or return $self->response_error(@keys ? "More than one staking key in the wallet; specify the staking pubkeyhash" : "No staking key in the wallet; create one with getnewstakingkey", ERR_INVALID_ADDRESS_OR_KEY);
        $staking_key = $keys[0];
    }
    my $delegation = QBitcoin::Delegation->create($staking_key, $owner_pubkeyhash)
        or return $self->response_error("Cannot store the delegation", ERR_INTERNAL_ERROR);
    QBitcoin::Generate->load_address_utxo($delegation);
    return $self->response_ok({ address => $delegation->address });
}

$PARAMS{removedelegationaddress} = "address";
$HELP{removedelegationaddress} = qq(
removedelegationaddress "address"

Stops staking the given delegated-staking address on this node and removes it
from the wallet. The owner keeps full control of the coins; they just stop
being staked here.

Arguments:
1. address    (string, required) The delegated-staking address (see listdelegations)

Result:
"str"    (string) Result message

Examples:
> qbitcoin-cli removedelegationaddress "3uXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
);
sub cmd_removedelegationaddress {
    my $self = shift;
    my $address = $self->args->[0];
    my ($delegation) = grep { $_->address eq $address } QBitcoin::Delegation->list
        or return $self->response_error("No such delegation address", ERR_INVALID_ADDRESS_OR_KEY);
    $delegation->remove;
    return $self->response_ok("Delegation address $address removed");
}

$PARAMS{listdelegations} = "";
$HELP{listdelegations} = qq(
listdelegations

Returns the list of the delegated-staking addresses staked by this node.

Result:
[
  {
    "address" : "str",             (string) The delegated-staking address
    "owner_pubkeyhash" : "str",    (string) The owner pubkeyhash
    "staking_pubkeyhash" : "str",  (string) The staking key used for this address
  },
  ...
]

Examples:
> qbitcoin-cli listdelegations
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "listdelegations", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_listdelegations {
    my $self = shift;
    my @list = map {{
        address            => $_->address,
        owner_pubkeyhash   => pubkeyhash_str($_->owner_pubkeyhash),
        staking_pubkeyhash => $_->staking_key->pubkeyhash_string,
    }} QBitcoin::Delegation->list;
    return $self->response_ok(\@list);
}

$PARAMS{setaddresstag} = "address tag?";
$HELP{setaddresstag} = qq(
setaddresstag "address" [ "tag" ]

Sets or clears the tag for an address in the wallet.
If tag is empty or omitted, the tag is cleared.

Arguments:
1. address    (string, required) The qbitcoin address
2. tag        (string, optional) The tag to set (omit or "" to clear)

Result:
"str"    (string) Result message

Examples:
> qbitcoin-cli setaddresstag "bqXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX" "exchange"
> qbitcoin-cli setaddresstag "bqXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
);
sub cmd_setaddresstag {
    my $self = shift;
    my $address_str = $self->args->[0];
    my $tag_name    = $self->args->[1];
    my $scripthash  = scripthash_by_address($address_str);

    my $my_address = QBitcoin::MyAddress->get_by_hash($scripthash, 1)
        or return $self->response_error("Address not found in wallet", ERR_INVALID_ADDRESS_OR_KEY);

    if (defined $tag_name && length $tag_name) {
        my $tag = QBitcoin::Tag->get_or_create($tag_name);
        $my_address->update({ tag_id => $tag->id });
        return $self->response_ok("Tag '$tag_name' set for address $address_str");
    }
    else {
        $my_address->update({ tag_id => undef });
        return $self->response_ok("Tag cleared for address $address_str");
    }
}

$PARAMS{dumpprivkey} = "address";
$REQUIRE_PASSWORD{dumpprivkey} = 1;
$HELP{dumpprivkey} = qq(
dumpprivkey "address"

Reveals the private key corresponding to 'address'.
Then the importprivkey can be used with this output

If a wallet password is set the command requires it (qbitcoin-cli prompts for it
and retries); an encrypted key is returned decrypted, whether or not the wallet
is unlocked.

Arguments:
1. address    (string, required) The address for the private key

Result:
"str"    (string) The private key

Examples:
> qbitcoin-cli dumpprivkey "myaddress"
> qbitcoin-cli importprivkey "mykey"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "dumpprivkey", "params": ["myaddress"], "password": "mysecret"}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_dumpprivkey {
    my $self = shift;
    $config->{allow_dumpprivkey}
        or return $self->response_error("This command is disabled", ERR_INVALID_ADDRESS_OR_KEY);
    my $scripthash = scripthash_by_address($self->args->[0])
        or return $self->response_error("The address is not correct", ERR_INVALID_ADDRESS_OR_KEY);
    my $my_address = QBitcoin::MyAddress->get_by_hash($scripthash, 0)
        or return $self->response_error("Private key is unknown for this address", ERR_INVALID_ADDRESS_OR_KEY);
    $self->hide_response = 1;
    my $stored = $my_address->private_key;
    QBitcoin::Wallet->is_encrypted_pk($stored)
        or return $self->response_ok($stored);
    my $master; # decrypt_pk defaults to the in-memory master key when unlocked
    if (!QBitcoin::Wallet->unlocked) {
        $master = QBitcoin::Wallet->master_key_with_password($self->auth_password // "")
            or return $self->response_error("Cannot unlock the wallet master key with this password", ERR_WALLET_PASSWORD_INCORRECT);
    }
    my $wif = QBitcoin::Wallet->decrypt_pk($stored, $my_address->address_raw, $master)
        or return $self->response_error("Cannot decrypt the private key", ERR_INTERNAL_ERROR);
    return $self->response_ok($wif);
}

$PARAMS{getpeerinfo} = "";
$HELP{getpeerinfo} = qq(
Returns data about each connected network node as a json array of objects.

Result:
[                                     (json array)
  {                                   (json object)
    "addr" : "str",                   (string) (host:port) The IP address and port of the peer
    "addrlocal" : "str",              (string) (ip:port) Bind address of the connection to the peer
    "hostname" : "str",               (string) Human-readable host name of the peer: self-announced in the greeting,
                                      or the name the peer is configured by. Informational only, never used in any
                                      logic; an unverified name is an arbitrary claim of the peer, do not trust it
    "hostname_verified" : true|false, (boolean) The hostname resolves to the peer address (forward-confirmed DNS)
    "network" : "str",                (string) Network (ipv4, ipv6, onion, i2p, not_publicly_routable)
    "createtime" : n,                 (numeric) The connection create time in seconds since epoch
    "bytessent" : n,                  (numeric) The total bytes sent
    "bytesrecv" : n,                  (numeric) The total bytes received
    "objsent" : n,                    (numeric) The total objects sent
    "objrecv" : n,                    (numeric) The total objects received
    "pingtime" : n,                   (numeric) ping time (if available)
    "minping" : n,                    (numeric) minimum observed ping time (if any at all)
    "inbound" : true|false,           (boolean) Inbound (true) or Outbound (false)
    "protocol" : "str",               (string) Protocol (qbitcoin, bitcoin)
    "software" : "str",               (string) Name and version of the peer software (user agent)
  },
]

Examples:
> qbitcoin-cli getpeerinfo
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getpeerinfo", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getpeerinfo {
    my $self = shift;
    my @peers;
    foreach my $connection (QBitcoin::ConnectionList->connected(PROTOCOL_QBITCOIN, PROTOCOL_BITCOIN)) {
        my $peer = $connection->peer;
        push @peers, {
            addr        => ip_port_str($connection->addr, $connection->port),
            addrlocal   => ip_port_str($connection->my_addr, $connection->my_port),
            hostname    => $peer->display_hostname // "",
            hostname_verified => $peer->display_hostname_verified ? TRUE : FALSE,
            inbound     => $connection->direction == DIR_IN ? TRUE : FALSE,
            protocol    => $connection->type,
            software    => $peer->software // "",
            network     => $peer->ipv4 ? "ipv4" : "ipv6",
            createtime  => $connection->state_time,
            bytessent   => $connection->bytes_sent,
            bytesrecv   => $connection->bytes_recv,
            objsent     => $connection->obj_sent,
            objrecv     => $connection->obj_recv,
            reputation  => $peer->reputation,
            # minping     => $peer->ping_min_ms / 1000,
            # pingtime    => $peer->ping_avg_ms / 1000,
        };
    }
    return $self->response_ok(\@peers);
}

$PARAMS{listpeers} = "";
$HELP{listpeers} = qq(
Returns data about each known peer as a json array of objects.
Unlike getpeerinfo it lists all known peers (the candidates for outgoing
connections), not only the currently connected ones.

Result:
[                                     (json array)
  {                                   (json object)
    "addr" : "str",                   (string) (host:port) The IP address and port of the peer
    "hostname" : "str",               (string) Human-readable host name of the peer (see getpeerinfo); do not trust an unverified name
    "hostname_verified" : true|false, (boolean) The hostname resolves to the peer address (forward-confirmed DNS)
    "protocol" : "str",               (string) Protocol (QBitcoin, Bitcoin)
    "connected" : true|false,         (boolean) Whether the peer is currently connected
    "connect_allowed" : true|false,   (boolean) Whether an outgoing connection to the peer is allowed now (not disabled, not in failed-connects backoff and the same node is not already connected via another address)
    "reputation" : n,                 (numeric) The peer reputation
    "failed_connects" : n,            (numeric) Number of failed outgoing connects since the last success (see resetpeer)
    "last_success_time" : n,          (numeric) Time of the last successful outgoing handshake, or null if the peer was never verified reachable
    "last_fail_time" : n,             (numeric) Time of the last failed outgoing connect (if any)
    "create_time" : n,                (numeric) Time the peer was learned
    "update_time" : n,                (numeric) Time of the last peer state update
    "software" : "str",               (string) Name and version of the peer software (if known)
    "banned" : true|false,            (boolean) Incoming connections from the peer are disabled
    "nocall" : true|false,            (boolean) Outgoing connections to the peer are disabled
    "hidden" : true|false,            (boolean) The peer is configured as hidden (never announced to other peers)
    "pinned" : true|false,            (boolean) The peer is pinned (explicitly configured)
  },
]

Examples:
> qbitcoin-cli listpeers
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "listpeers", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_listpeers {
    my $self = shift;
    my @peers;
    foreach my $type_id (PROTOCOL_QBITCOIN, PROTOCOL_BITCOIN) {
        foreach my $peer (sort { $b->reputation <=> $a->reputation || $a->id cmp $b->id } QBitcoin::Peer->get_all($type_id)) {
            push @peers, {
                addr              => ip_port_str($peer->ip, $peer->port),
                hostname          => $peer->display_hostname // "",
                hostname_verified => $peer->display_hostname_verified ? TRUE : FALSE,
                protocol          => $peer->type,
                connected         => $peer->conn_state == STATE_CONNECTED ? TRUE : FALSE,
                connect_allowed   => $peer->is_connect_allowed ? TRUE : FALSE,
                reputation        => $peer->reputation + 0,
                failed_connects   => ($peer->failed_connects // 0) + 0,
                last_success_time => $peer->last_success_time,
                last_fail_time    => $peer->last_fail_time,
                create_time       => $peer->create_time,
                update_time       => $peer->update_time,
                software          => $peer->software // "",
                banned            => (($peer->status // 0) & PEER_STATUS_BANNED) ? TRUE : FALSE,
                nocall            => (($peer->status // 0) & PEER_STATUS_NOCALL) ? TRUE : FALSE,
                hidden            => $peer->hidden ? TRUE : FALSE,
                pinned            => $peer->pinned ? TRUE : FALSE,
            };
        }
    }
    return $self->response_ok(\@peers);
}

$PARAMS{resetpeer} = "node";
$HELP{resetpeer} = qq(
resetpeer "node"

Reset the failed-connects counter and backoff for the given known peer,
so a new outgoing connection to the peer may be initiated on the next
connection round if all other conditions are met (see listpeers).

Arguments:
1. node    (string, required) The peer IP address or hostname (see listpeers)

Result:
"str"    (string) Result message

Examples:
> qbitcoin-cli resetpeer "192.168.0.6"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "resetpeer", "params": ["192.168.0.6"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_resetpeer {
    my $self = shift;
    my ($host) = parse_addr_port($self->args->[0]);
    my %ip = map { $_ => 1 } host_to_ips($host)
        or return $self->response_error("Cannot resolve $host", ERR_INVALID_ADDRESS_OR_KEY);
    my @reset;
    foreach my $type_id (PROTOCOL_QBITCOIN, PROTOCOL_BITCOIN) {
        foreach my $peer (grep { $ip{$_->ip} } QBitcoin::Peer->get_all($type_id)) {
            $peer->update(failed_connects => 0, last_fail_time => undef);
            push @reset, $peer->type . " peer " . $peer->id;
        }
    }
    @reset
        or return $self->response_error("Unknown peer", ERR_INVALID_ADDRESS_OR_KEY);
    return $self->response_ok("Reset failed connects for " . join(", ", @reset));
}

$PARAMS{getaddressbalance} = "address minconf?";
$HELP{getaddressbalance} = qq{
getaddressbalance "address" ( minconf )

Returns the total amount on the given address in transactions with at least minconf confirmations.

Arguments:
1. address    (string, required) The qbitcoin address for transactions.
2. minconf    (numeric, optional, default=1, max=${\(INCORE_LEVELS+1)}) Only include transactions confirmed at least this many times.

Result:
n    (numeric) The total amount in QBTC unspent at this address.

Examples:

The amount from transactions with at least 1 confirmation
> qbitcoin-cli getaddressbalance "myaddress"

The amount including unconfirmed transactions, zero confirmations
> qbitcoin-cli getaddressbalance "myaddress" 0

The amount with at least 6 confirmations
> qbitcoin-cli getaddressbalance "myaddress" 6

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getaddressbalance", "params": ["myaddress", 6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_getaddressbalance {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my $address = $self->args->[0];
    my $minconf = $self->args->[1] // 1;
    my $value = address_balance($address, $minconf);
    defined $value
        or return $self->response_error("Too many transactions on this address", ERR_INTERNAL_ERROR);
    return $self->response_ok($value/DENOMINATOR);
}

$PARAMS{getreceivedbyaddress} = "address minconf?";
$HELP{getreceivedbyaddress} = qq{
getreceivedbyaddress "address" ( minconf )

Returns the total received amount on the given address in transactions with at least minconf confirmations.

Arguments:
1. address    (string, required) The qbitcoin address for transactions.
2. minconf    (numeric, optional, default=1, max=${\(INCORE_LEVELS+1)}) Only include transactions confirmed at least this many times.

Result:
n    (numeric) The total amount in QBTC received at this address.

Examples:

The amount from transactions with at least 1 confirmation
> qbitcoin-cli getaddressbalance "myaddress"

The amount including unconfirmed transactions, zero confirmations
> qbitcoin-cli getreceivedbyaddress "myaddress" 0

The amount with at least 6 confirmations
> qbitcoin-cli getreceivedbyaddress "myaddress" 6

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getreceivedbyaddress", "params": ["myaddress", 6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_getreceivedbyaddress {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my $address = $self->args->[0];
    my $minconf = $self->args->[1] // 1;
    my $value = address_received($address, $minconf);
    defined($value)
        or return $self->response_error("Internal error", ERR_INTERNAL_ERROR);
    return $self->response_ok($value/DENOMINATOR);
}

$PARAMS{listunspent} = "address minconf?";
$HELP{listunspent} = qq{
listunspent address ( minconf )

Returns array of unspent transaction outputs on the given address with at least minconf confirmations.

Arguments:
1. address    (string, required) The qbitcoin address for transactions.
2. minconf    (numeric, optional, default=1, max=${\(INCORE_LEVELS+1)}) Only include transactions confirmed at least this many times.

Result:
[                                (json array)
  {                              (json object)
    "txid" : "hex",              (string) the transaction id
    "vout" : n,                  (numeric) the vout value
    "address" : "str",           (string) the qbitcoin address
    "amount" : n,                (numeric) the transaction output amount in QBTC
    "confirmations" : n,         (numeric) The number of confirmations
  },
  ...
]

Examples:
> qbitcoin-cli listunspent "myaddress"
> qbitcoin-cli listunspent "myaddress" 6
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "listunspent", "params": ["myaddress",6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_listunspent {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my $address = $self->args->[0];
    my $minconf = $self->args->[1] // 1;
    my ($chain_utxo, $mempool_utxo) = get_address_utxo($address);
    $chain_utxo
        or return $self->response_error("Too many transactions on this address", ERR_INTERNAL_ERROR);
    my $best_height = QBitcoin::Block->blockchain_height
        or return $self->response_ok([]);
    my @utxo;
    foreach my $hash ($minconf ? ($chain_utxo) : ($chain_utxo, $mempool_utxo)) {
        foreach my $txid (keys %$hash) {
            for (my $vout = @{$hash->{$txid}}-1; $vout >= 0; $vout--) {
                my $utxo = $hash->{$txid}->[$vout]
                    or next;
                !$minconf || $utxo->{block_height} <= $best_height - $minconf + 1
                    or last;
                push @utxo, {
                    txid    => unpack("H*", $txid),
                    vout    => $vout,
                    address => $address,
                    amount  => $utxo->{value} / DENOMINATOR,
                    defined($utxo->{token_id})     ? ( token_id          => unpack("H*", $utxo->{token_id}) ) : (),
                    defined($utxo->{token_amount}) ? ( token_amount      => $utxo->{token_amount}      ) : (),
                    $utxo->{token_permissions}     ? ( token_permissions => $utxo->{token_permissions} ) : (),
                    !defined($utxo->{token_id}) && ord($utxo->{data} // "") == ord(TXO_DATA_TAG) ? ( tag => substr($utxo->{data}, 1) ) : (),
                    defined($utxo->{block_height}) ? (
                        confirmations => $best_height - $utxo->{block_height} + 1,
                        block_height  => $utxo->{block_height},
                        block_pos     => $utxo->{block_pos},
                    ) : (
                        confirmations => 0,
                    ),
                };
            }
        }
    }
    @utxo = sort {
        $b->{confirmations}    <=> $a->{confirmations}    ||
        ($a->{block_pos} // 0) <=> ($b->{block_pos} // 0) ||
        $a->{txid} cmp $b->{txid} ||
        $a->{vout} <=> $b->{vout}
    } @utxo;
    return $self->response_ok(\@utxo);
}

$PARAMS{listtransactions} = "address minconf?";
$HELP{listtransactions} = qq{
listtransactions address ( minconf )

Returns array of all transaction (inputs and outputs) on the given address with at least minconf confirmations.

Arguments:
1. address    (string, required) The qbitcoin address for transactions.
2. minconf    (numeric, optional, default=1, max=${\(INCORE_LEVELS+1)}) Only include transactions confirmed at least this many times.

Result:
[                                (json array)
  {                              (json object)
    "txid" : "hex",              (string) the transaction id
    "amount" : n,                (numeric) the received (positive) or sent (negative) amount in QBTC
    "height" : n,                (numeric) the block height containing the transaction (or -1 if unconfirmed)
    "confirmations" : n,         (numeric) The number of confirmations
  },
  ...
]

Examples:
> qbitcoin-cli listtransactions "myaddress"
> qbitcoin-cli listtransactions "myaddress" 6
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "listtransactions", "params": ["myaddress",6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_listtransactions {
    my $self = shift;
    my $address = $self->args->[0];
    my $minconf = $self->args->[1] // 1;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my ($txs_chain, $txs_mempool) = get_address_txs($address, undef, undef, undef);
    $txs_chain
        or return $self->response_error("Incorrect address", ERR_INTERNAL_ERROR);
    my $best_height = QBitcoin::Block->blockchain_height
        or return $self->response_ok([]);
    $txs_mempool = [] if $minconf > 0;
    @$txs_chain = grep { $_->[2] <= $best_height - $minconf + 1 } @$txs_chain if $minconf > 1;
    return $self->response_ok([
        map(+{
            txid          => unpack("H*", $_->[0]),
            amount        => $_->[1] / DENOMINATOR,
            height        => $_->[2],
            confirmations => $best_height - $_->[2] + 1,
        }, @$txs_chain),
        map(+{
            txid          => unpack("H*", $_->[0]),
            amount        => $_->[1] / DENOMINATOR,
            height        => -1,
            confirmations => 0,
        }, @$txs_mempool),
    ]);
}

$PARAMS{listmyaddresses} = "include_watchonly?";
$HELP{listmyaddresses} = qq(
listmyaddresses ( include_watchonly )

Returns the list of addresses in the wallet.

Arguments:
1. include_watchonly    (boolean, optional, default=true) Also list watch-only addresses (imported without private key)

Result:
{                              (json object) json object with addresses as keys
  "address" : {                (json object) json object with information about address
    "algo" : "str",            (string) crypto algorithm of the address key
    "staked" : true|false      (boolean) whether the address is used for staking (block validation)
    "watchonly" : true|false   (boolean) whether the address is watch-only (no private key)
    "tag" : "str"|null         (string or null) notification tag for this address
    "delegation" : "str",      (string, optional) delegated-staking role of this wallet: "owner", "delegate" or "both"
    "stakeonly" : true         (boolean, optional) only the staking key is here: the address is staked for a foreign owner and is not counted in getbalance
  },
  ...
}

Examples:
> qbitcoin-cli listmyaddresses
> qbitcoin-cli listmyaddresses false
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "listmyaddresses", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_listmyaddresses {
    my $self = shift;
    my $include_watchonly = $self->args->[0] // TRUE;
    my %list;
    foreach my $my_address (QBitcoin::MyAddress->watched_address) {
        next if $my_address->is_watchonly && !$include_watchonly;
        my $delegation;
        if (!$my_address->is_watchonly && $my_address->is_delegation) {
            $delegation = QBitcoin::Delegation->get_by_hash(scalar $my_address->scripthash) ? "both" : "owner";
        }
        $list{$my_address->address} = {
            algo      => defined($my_address->algo) ? CRYPT_ALGO_NAMES->{$my_address->algo} : undef,
            staked    => $my_address->staked ? TRUE : FALSE,
            watchonly => $my_address->is_watchonly ? TRUE : FALSE,
            tag       => $my_address->tag,
            $delegation ? (delegation => $delegation) : (),
        };
        $list{$my_address->address}{staked} = TRUE if $delegation && $delegation eq "both";
    }
    # Addresses delegated to this node whose owner key is elsewhere: staked
    # here, but not our money (not counted in getbalance)
    foreach my $delegation (QBitcoin::Delegation->list) {
        next if $list{$delegation->address};
        $list{$delegation->address} = {
            algo       => CRYPT_ALGO_NAMES->{$delegation->staking_key->algo},
            staked     => TRUE,
            stakeonly  => TRUE,
            watchonly  => FALSE,
            tag        => undef,
            delegation => "delegate",
        };
    }
    $self->response_ok(\%list);
}

$PARAMS{getaddressinfo} = "address";
$HELP{getaddressinfo} = qq(
getaddressinfo "address"

Return information about the given qbitcoin address.
Some of the information is present only if the address is in the wallet
(see listmyaddresses).

Arguments:
1. address    (string, required) The qbitcoin address for which to get information

Result:
{                               (json object)
  "address" : "str",            (string) The qbitcoin address
  "scripthash" : "hex",         (string) The hex-encoded scripthash generated by the address
  "ismine" : true|false,        (boolean) If the wallet has the private key for the address
  "iswatchonly" : true|false,   (boolean) If the address is watch-only (in the wallet without private key)
  "algo" : "str",               (string, optional) crypto algorithm of the address key (wallet addresses only)
  "staked" : true|false,        (boolean, optional) whether the address is used for staking (wallet addresses only)
  "tag" : "str"|null,           (string or null, optional) notification tag for the address (wallet addresses only)
  "pubkey" : "hex",             (string, optional) The hex value of the raw public key (if known)
  "pubkeyhash" : "str",         (string, optional) base58 hash of the public key (wallet addresses with a private key)
  "delegation" : "str",         (string, optional) delegated-staking role of this wallet: "owner", "delegate" or "both"
  "stakeonly" : true,           (boolean, optional) only the staking key is here: staked for a foreign owner, not counted in getbalance
  "delegate_pubkeyhash" : "str",(string, optional) the delegate staking pubkeyhash (delegation owner side)
  "owner_pubkeyhash" : "str",   (string, optional) the owner pubkeyhash (delegation delegate side)
  "staking_pubkeyhash" : "str"  (string, optional) the staking key used for this address (delegation delegate side)
}

Examples:
> qbitcoin-cli getaddressinfo "myaddress"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getaddressinfo", "params": ["myaddress"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getaddressinfo {
    my $self = shift;
    my $address = $self->args->[0];
    my $scripthash = eval { scripthash_by_address($address) }
        or return $self->response_error("Invalid address", ERR_INVALID_ADDRESS_OR_KEY);
    my $my_address = QBitcoin::MyAddress->get_by_hash($scripthash, 1);
    my $res = {
        address     => $address,
        scripthash  => unpack("H*", $scripthash),
        ismine      => $my_address && !$my_address->is_watchonly ? TRUE : FALSE,
        iswatchonly => $my_address && $my_address->is_watchonly  ? TRUE : FALSE,
    };
    my $delegation = QBitcoin::Delegation->get_by_hash($scripthash);
    if ($my_address) {
        $res->{algo}   = defined($my_address->algo) ? CRYPT_ALGO_NAMES->{$my_address->algo} : undef;
        $res->{staked} = $my_address->staked ? TRUE : FALSE;
        $res->{tag}    = $my_address->tag;
        # pubkey derivation dies for an encrypted key without a stored pubkey while the wallet is locked
        if (my $pubkey = eval { $my_address->pubkey }) {
            $res->{pubkey} = unpack("H*", $pubkey);
            $res->{pubkeyhash} = pubkeyhash_str(pubkeyhash_by_pubkey($pubkey, $my_address->algo // 0)) unless $my_address->is_watchonly;
        }
        if (!$my_address->is_watchonly && $my_address->is_delegation) {
            $res->{delegation} = $delegation ? "both" : "owner";
            $res->{delegate_pubkeyhash} = pubkeyhash_str($my_address->deleg_pubkeyhash);
            $res->{staked} = TRUE if $delegation;
        }
    }
    if ($delegation && !$res->{delegation}) {
        $res->{delegation} = "delegate";
        $res->{stakeonly}  = TRUE;
        $res->{staked}     = TRUE;
        $res->{owner_pubkeyhash}   = pubkeyhash_str($delegation->owner_pubkeyhash);
        $res->{staking_pubkeyhash} = $delegation->staking_key->pubkeyhash_string;
    }
    return $self->response_ok($res);
}

$PARAMS{getbalance} = "minconf?";
$HELP{getbalance} = qq(
getbalance ( minconf )

Returns total balance of the addresses in the wallet with at least minconf confirmations.

Result:
n    (numeric) The total amount in QBTC in the wallet.

Examples:
> qbitcoin-cli getbalance
> qbitcoin-cli getbalance 6
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getbalance", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getbalance {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my @my_txo = QBitcoin::TXO->my_utxo();
    my $minconf = $self->args->[0] // 1;
    my $value = 0;
    if ($minconf) {
        my $best_height = QBitcoin::Block->blockchain_height
            or return $self->response_ok(0);
        foreach my $txo (@my_txo) {
            my $block_height = QBitcoin::Transaction->check_by_hash($txo->tx_in)
                or next;
            next if $block_height < 0;
            next if $block_height > $best_height - $minconf + 1;
            $value += $txo->value;
        }
    }
    else {
        $value = sum0(map { $_->value } @my_txo);
    }
    return $self->response_ok($value/DENOMINATOR);
}

$PARAMS{getnewaddress} = "address_type? delegate_pubkeyhash/pubkeyhash?";
$HELP{getnewaddress} = qq(
getnewaddress ( address_type delegate_pubkeyhash )

Returns a new qbitcoin address and private key.
Private key is not stored in the wallet and can be imported using importprivkey.

With delegate_pubkeyhash (the staking pubkeyhash published by a delegate, see
getnewstakingkey) the returned address is a delegated-staking address: the
returned key spends it freely, the delegate's staking key can only stake it
and must return the full value back to the address. The returned private key
contains the delegate pubkeyhash, so it alone is enough to import or restore
the address; the returned "pubkeyhash" is your side of the covenant - send it
to the delegate so their node can register the address for staking (see
adddelegationaddress).
WARNING: the delegate can never spend your coins, but they are the slashing
collateral: if the delegate's node equivocates (e.g. runs its staking key on
two nodes at once), the penalty is paid from the address's coins. Choose a
delegate you trust to operate a single node - the covenant does not protect
against this, only reputation does.

Arguments:
1. address_type          (string, optional, default="ecdsa") The address type to use. Options are "ecdsa", "schnorr", "falcon".
2. delegate_pubkeyhash   (string, optional) The delegate staking pubkeyhash for a delegated-staking address

Result:
{
    "address",     (string) The new qbitcoin address
    "private_key", (string) The private key for the new address
    "pubkeyhash",  (string, optional) The owner pubkeyhash to send to the delegate (delegated-staking addresses only)
}

Examples:
> qbitcoin-cli getnewaddress
> qbitcoin-cli getnewaddress "ecdsa" "6nXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXXX"
> curl --user myusername --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getnewaddress", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getnewaddress {
    my $self = shift;
    my $algo = $self->args->[0] // CRYPT_ALGO_ECDSA;
    my $keypair = generate_keypair($algo);
    if (defined(my $delegate_pubkeyhash = $self->args->[1])) {
        my $pubkeyhash = pubkeyhash_by_pubkey($keypair->pubkey_by_privkey, $algo);
        return $self->response_ok({
            address     => delegation_address($pubkeyhash, $delegate_pubkeyhash),
            private_key => delegation_import_format($keypair->pk_serialize, $delegate_pubkeyhash),
            pubkeyhash  => pubkeyhash_str($pubkeyhash),
        });
    }
    my $address = address_by_pubkey($keypair->pubkey_by_privkey, $algo);
    $self->hide_response = 1;
    return $self->response_ok({ address => $address, private_key => wallet_import_format($keypair->pk_serialize) });
}

$PARAMS{estimatesmartfee} = "conf_target estimate_mode?";
$HELP{estimatesmartfee} = qq(
estimatesmartfee conf_target ( "estimate_mode" )

Estimates the approximate fee per kilobyte needed for a transaction to begin
confirmation within conf_target blocks if possible.

Arguments:
1. conf_target      (numeric, required) Confirmation target in blocks (1 - 99)
2. estimate_mode    (string, optional, default=CONSERVATIVE) The fee estimate mode.
                    Whether to return a more conservative estimate which also satisfies
                    a longer history. A conservative estimate potentially returns a
                    higher feerate and is more likely to be sufficient for the desired
                    target, but is not as responsive to short term drops in the
                    prevailing fee market.  Must be one of:
                    "ECONOMICAL"
                    "CONSERVATIVE"

Result:
{                   (json object)
  "feerate" : n,    (numeric, optional) estimate fee rate in QBTC/kB (only present if no errors were encountered)
  "errors" : [      (json array, optional) Errors encountered during processing (if there are any)
    "str",          (string) error
    ...
  ],
}

Examples:
> qbitcoin-cli estimatesmartfee 6
> curl --user myusername --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "estimatesmartfee", "params": [6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_estimatesmartfee {
    my $self = shift;
    my ($target, $mode) = @{$self->args};
    $mode //= "CONSERVATIVE";
    $target /= 2 if uc($mode) eq "CONSERVATIVE";
    my ($result, $error) = estimate_fees($target);
    if ($error) {
        return $self->response_error($error, ERR_INTERNAL_ERROR);
    }
    return $self->response_ok({ feerate => $result->{$target} * 1024 / DENOMINATOR });
}

$PARAMS{stakeaddress} = "address";
$HELP{stakeaddress} = qq(
stakeaddress address

Set address to be used for staking (block validation).

WARNING: an address must be staked on exactly ONE node. If the same private key
is imported on another node which stakes it too (a hot spare, an old node left
running after a migration), both nodes will stake the same outputs - that is
equivocation, and the slashing penalty is paid from these coins. Stop staking
the address on the other node before enabling it here.

Arguments:
1. address    (string, required) The qbitcoin address to be used for staking.
              Must be already in the wallet (imported using importprivkey).

null    (json null)

Examples:

> qbitcoin-cli stakeaddress "myaddress"

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "stakeaddress", "params": ["myaddress"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_stakeaddress {
    my $self = shift;
    my ($address) = @{$self->args};
    my $scripthash = scripthash_by_address($self->args->[0])
        or return $self->response_error("The address is not correct", ERR_INVALID_ADDRESS_OR_KEY);
    my $my_address = QBitcoin::MyAddress->get_by_hash($scripthash, 0)
        or return $self->response_error("The address is not in the wallet", ERR_INVALID_ADDRESS_OR_KEY);
    $my_address->private_key
        or return $self->response_error("Private key is unknown for this address", ERR_INVALID_ADDRESS_OR_KEY);
    if ($my_address->staked) {
        return $self->response_ok("Address $address is already using for staking");
    }
    if ($my_address->is_delegation) {
        return $self->response_error("Address $address is delegated for staking; staking it here as well would equivocate and lead to slashing", ERR_INVALID_ADDRESS_OR_KEY);
    }
    $my_address->set_stake(1)
        or return $self->response_error("Cannot set address $address for staking", ERR_INTERNAL_ERROR);
    return $self->response_ok("Address $address set for staking");
}

$PARAMS{unstakeaddress} = "address";
$HELP{unstakeaddress} = qq(
unstakeaddress address

Disable staking (block validation) by this address.

Arguments:
1. address    (string, required) The qbitcoin address that is using for staking.

null    (json null)

Examples:

> qbitcoin-cli unstakeaddress "myaddress"

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "unstakeaddress", "params": ["myaddress"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_unstakeaddress {
    my $self = shift;
    my ($address) = @{$self->args};
    my $scripthash = scripthash_by_address($self->args->[0])
        or return $self->response_error("The address is not correct", ERR_INVALID_ADDRESS_OR_KEY);
    my $my_address = QBitcoin::MyAddress->get_by_hash($scripthash, 0)
        or return $self->response_error("The address is not in the wallet", ERR_INVALID_ADDRESS_OR_KEY);
    if (!$my_address->staked) {
        return $self->response_ok("Address $address is not using for staking");
    }
    $my_address->set_stake(0);
    return $self->response_ok("Address $address will not be used for staking");
}

$PARAMS{gettokensbalance} = "address token_id minconf?";
$HELP{gettokensbalance} = qq{
gettokensbalance "address" "token_id" ( minconf )

Returns the total amount of tokens on the given address in transactions with at least minconf confirmations.

Arguments:
1. address    (string, required) The qbitcoin address for transactions.
2. token_id   (string, required) The token identifier (create transaction).
3. minconf    (numeric, optional, default=1, max=${\(INCORE_LEVELS+1)}) Only include transactions confirmed at least this many times.

Result:
n    (numeric) The total amount of tokens unspent at this address.

Examples:

The amount of tokens from transactions with at least 1 confirmation
> qbitcoin-cli gettokensbalance "myaddress" "token_id"

The amount including unconfirmed transactions, zero confirmations
> qbitcoin-cli gettokensbalance "myaddress" "token_id" 0

The amount with at least 6 confirmations
> qbitcoin-cli gettokensbalance "myaddress" "token_id" 6

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "gettokensbalance", "params": ["myaddress", "token_id", 6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_gettokensbalance {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my $address = $self->args->[0];
    my $token_hash = pack("H*", $self->args->[1]);
    my $minconf = $self->args->[2] // 1;
    my $value = tokens_balance($address, $token_hash, $minconf);
    defined $value
        or return $self->response_error("Too many transactions on this address", ERR_INTERNAL_ERROR);
    if ($value && (my $token_tx = QBitcoin::Transaction->get_by_hash($token_hash))) {
        my $token_info = $token_tx->token_info;
        $value /= 10 ** ($token_info->{decimals} // TOKEN_DEFAULT_DECIMALS);
    }
    return $self->response_ok($value);
}

$PARAMS{gettokensreceived} = "address token_id minconf?";
$HELP{gettokensreceived} = qq{
gettokensreceived "address" "token_id" ( minconf )

Returns the total received amount of tokens on the given address in transactions with at least minconf confirmations.

Arguments:
1. address    (string, required) The qbitcoin address for transactions.
2. token_id   (string, required) The token identifier (create transaction).
3. minconf    (numeric, optional, default=1, max=${\(INCORE_LEVELS+1)}) Only include transactions confirmed at least this many times.

Result:
n    (numeric) The total amount of tokens received at this address.

Examples:

The received amount of tokens from transactions with at least 1 confirmation
> qbitcoin-cli gettokensreceived "myaddress" "token_id"

The received amount including unconfirmed transactions, zero confirmations
> qbitcoin-cli gettokensreceived "myaddress" "token_id" 0

The received amount with at least 6 confirmations
> qbitcoin-cli gettokensreceived "myaddress" "token_id" 6

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "gettokensreceived", "params": ["myaddress", "token_id", 6]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_gettokensreceived {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my $address = $self->args->[0];
    my $token_hash = pack("H*", $self->args->[1]);
    my $minconf = $self->args->[2] // 1;
    my $value = tokens_received($address, $token_hash, $minconf);
    defined $value
        or return $self->response_error("Too many transactions on this address", ERR_INTERNAL_ERROR);
    if ($value && (my $token_tx = QBitcoin::Transaction->get_by_hash($token_hash))) {
        my $token_info = $token_tx->token_info;
        $value /= 10 ** ($token_info->{decimals} // TOKEN_DEFAULT_DECIMALS);
    }
    return $self->response_ok($value);
}

$PARAMS{gettokensinfo} = "token_id";
$HELP{gettokensinfo} = qq{
gettokensinfo "token_id"

Returns common information about the given token.

Arguments:
1. token_id    (string, required) The token identifier (create transaction).

Result:
n    (numeric) The total amount of tokens received at this address.

Examples:

The received amount of tokens from transactions with at least 1 confirmation
> qbitcoin-cli gettokensinfo "token_id"

As a JSON-RPC call
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "gettokensinfo", "params": ["token_id"]}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
};
sub cmd_gettokensinfo {
    my $self = shift;
    blockchain_synced() && mempool_synced()
        or return $self->response_error("Blockchain is not synced", ERR_INTERNAL_ERROR);
    my $token_hash  = pack("H*", $self->args->[0]);
    my $info = get_tokens_info($token_hash)
        or return $self->response_error("Token not found", ERR_INTERNAL_ERROR);
    return $self->response_ok($info);
}

# getmemoryinfo
# getrpcinfo
# stop
# uptime

# addnode
# clearbanned
# disconnectnode
# getaddednodeinfo
# getconnectioncount
# listbanned
# setban

$PARAMS{setwalletpassword} = "password";
$SENSITIVE{setwalletpassword} = 1;
$HELP{setwalletpassword} = qq(
Set or change the wallet password. The password protects the /admin/* and
/wallet/* REST API and, unless 'encrypted_private_keys' is disabled in the
configuration file, encrypts the wallet private keys stored in the database
(see walletunlock/walletlock/getwalletinfo).

When no password is set yet this command works unconditionally, so it is the
recommended way to protect the wallet right after installing the node.

The password may be passed as an argument, but for safety qbitcoin-cli also reads
it from standard input when the argument is omitted (so it does not appear in the
process list or shell history):

    qbitcoin-cli setwalletpassword            # prompts on a terminal
    echo -n "mysecret" | qbitcoin-cli setwalletpassword

Changing an already set password requires the current one (qbitcoin-cli prompts
for it and retries the request with a top-level "password" field). Running the
command with the current password also converges the key encryption state to the
'encrypted_private_keys' policy, so it may be run with the same password to
encrypt or decrypt the stored keys after changing that option.

Resetting a FORGOTTEN password is possible only with 'allow_password_reset'
enabled in the configuration file and PERMANENTLY DESTROYS all encrypted private
keys (they cannot be decrypted without the old password; qbitcoin-cli asks for an
explicit confirmation and retries with a top-level "force" field):

    allow_password_reset = 1

Arguments:
1. password    (string, required) the new wallet password

Result:
null    (json null)

Examples:
> qbitcoin-cli setwalletpassword "mysecret"
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "setwalletpassword", "params": ["newsecret"], "password": "oldsecret"}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_setwalletpassword {
    my $self = shift;
    my ($new) = @{$self->args};
    if (!QBitcoin::Password->is_set) {
        if (defined(my $err = QBitcoin::Wallet->change_password(undef, $new))) {
            return $self->response_error($err, ERR_MISC);
        }
        return $self->response_ok;
    }
    my $old = $self->auth_password;
    if (!defined($old) && !$self->force) {
        my $hint = $config->{allow_password_reset} ? "; leave the password input empty if it is forgotten" : "";
        return $self->response_error("Changing the wallet password requires the current one$hint", ERR_WALLET_PASSWORD_REQUIRED);
    }
    if (defined($old)) {
        # Checking the current password is a brute-force oracle like any other,
        # so it is subject to the same per-source limit (see QBitcoin::RPC)
        if (my $delay = $self->auth_throttle_delay) {
            return $self->response_error(throttle_message($delay), ERR_WALLET_PASSWORD_INCORRECT);
        }
        if (QBitcoin::Password->check_password($old)) {
            $self->register_auth_success;
            if (defined(my $err = QBitcoin::Wallet->change_password($old, $new))) {
                return $self->response_error($err, ERR_MISC);
            }
            return $self->response_ok;
        }
        $self->register_auth_failure;
    }
    # The current password was not provided or does not match: forgotten-password reset
    if (!$config->{allow_password_reset}) {
        return $self->response_error(
            "Incorrect wallet password; resetting a forgotten password requires 'allow_password_reset' in the configuration file",
            ERR_WALLET_PASSWORD_INCORRECT);
    }
    if (!$self->force) {
        my $count = QBitcoin::Wallet->encrypted_count;
        my $warning = $count
            ? "This will PERMANENTLY DESTROY $count encrypted private key(s): they cannot be decrypted without the old password."
            : "The old password will be overwritten.";
        return $self->response_error("Resetting the wallet password. $warning Continue?", ERR_CONFIRMATION_REQUIRED);
    }
    QBitcoin::Wallet->reset_destroy($new);
    return $self->response_ok;
}

$PARAMS{walletunlock} = "";
$REQUIRE_PASSWORD{walletunlock} = 1;
$HELP{walletunlock} = qq(
Decrypt the wallet master key into the node process memory so the encrypted
private keys can be used for signing; block generation (staking) resumes
automatically if enabled. The wallet stays unlocked until walletlock or a node
restart.

Requires the wallet password: qbitcoin-cli prompts for it and retries the
request with a top-level "password" field.

Result:
null    (json null)

Examples:
> qbitcoin-cli walletunlock
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "walletunlock", "params": [], "password": "mysecret"}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_walletunlock {
    my $self = shift;
    QBitcoin::Wallet->is_encrypted
        or return $self->response_ok("Private keys are not encrypted, nothing to unlock");
    QBitcoin::Wallet->unlocked
        and return $self->response_ok("The wallet is already unlocked");
    defined($self->auth_password)
        or return $self->response_error("This command requires the wallet password", ERR_WALLET_PASSWORD_REQUIRED);
    QBitcoin::Wallet->unlock($self->auth_password)
        or return $self->response_error("Cannot unlock the wallet master key with this password", ERR_WALLET_PASSWORD_INCORRECT);
    Noticef("Wallet unlocked via RPC");
    return $self->response_ok;
}

$PARAMS{walletlock} = "";
$HELP{walletlock} = qq(
Remove the decrypted wallet master key from the node process memory. Signing
with the encrypted private keys (including block generation) becomes impossible
until walletunlock.

Result:
null    (json null)

Examples:
> qbitcoin-cli walletlock
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "walletlock", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_walletlock {
    my $self = shift;
    QBitcoin::Wallet->is_encrypted
        or return $self->response_ok("Private keys are not encrypted, nothing to lock");
    QBitcoin::Wallet->unlocked
        or return $self->response_ok("The wallet is already locked");
    QBitcoin::Wallet->lock;
    Noticef("Wallet locked via RPC");
    return $self->response_ok;
}

$PARAMS{getwalletinfo} = "";
$HELP{getwalletinfo} = qq(
Returns an object containing wallet state info.

Result:
{                             (json object)
  "password_set" : true|false,     (boolean) a wallet password is set
  "keys_encrypted" : true|false,   (boolean) the stored private keys are encrypted
  "locked" : true|false,           (boolean) keys are encrypted and the wallet is not unlocked (signing impossible)
  "generate" : true|false,         (boolean) block generation is enabled
  "staking_active" : true|false,   (boolean) block generation is enabled and signing is possible
  "addresses" : n,                 (numeric) number of addresses with a private key
  "watchonly_addresses" : n,       (numeric) number of watch-only addresses
  "staked_addresses" : n,          (numeric) number of staked addresses
  "warning" : "str"                (string, optional) key storage state does not match the encrypted_private_keys option
}

Examples:
> qbitcoin-cli getwalletinfo
> curl --data-binary '{"jsonrpc": "1.0", "id": "curltest", "method": "getwalletinfo", "params": []}' -H 'content-type: application/json;' http://127.0.0.1:${\RPC_PORT}/
);
sub cmd_getwalletinfo {
    my $self = shift;
    my $encrypted = QBitcoin::Wallet->is_encrypted;
    my $generate  = QBitcoin::Generate::Control->generate_enabled;
    my @my_address = QBitcoin::MyAddress->my_address;
    my $info = {
        password_set        => QBitcoin::Password->is_set ? TRUE : FALSE,
        keys_encrypted      => $encrypted ? TRUE : FALSE,
        locked              => ($encrypted && !QBitcoin::Wallet->unlocked) ? TRUE : FALSE,
        generate            => $generate ? TRUE : FALSE,
        staking_active      => ($generate && QBitcoin::Wallet->signing_available) ? TRUE : FALSE,
        addresses           => scalar(@my_address) + 0, # +0: encode as json number even for an empty list
        watchonly_addresses => scalar(grep { $_->is_watchonly } QBitcoin::MyAddress->watched_address) + 0,
        staked_addresses    => scalar(() = QBitcoin::MyAddress->stake_address) + 0,
    };
    if (QBitcoin::Password->is_set) {
        my $policy = $config->{encrypted_private_keys} // 1;
        if ($policy && !$encrypted && @my_address) {
            $info->{warning} = "private keys are stored unencrypted; run setwalletpassword to encrypt them";
        }
        elsif (!$policy && $encrypted) {
            $info->{warning} = "private keys are encrypted but 'encrypted_private_keys' is disabled; run setwalletpassword to decrypt them";
        }
    }
    return $self->response_ok($info);
}

# signmessagewithprivkey
# verifymessage

# listreceivedbyaddress

1;
