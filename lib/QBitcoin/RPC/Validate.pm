package QBitcoin::RPC::Validate;
use warnings;
use strict;

use Role::Tiny;
use Cpanel::JSON::XS;
use Scalar::Util qw(looks_like_number);
use QBitcoin::Const;
use QBitcoin::RPC::Const;
use QBitcoin::Config;
use QBitcoin::BlockchainParams;
use QBitcoin::Address qw(wif_to_pk pubkeyhash_by_str);
use QBitcoin::Password;
use QBitcoin::Accessors qw(mk_accessors);
use Bitcoin::Address qw(is_btc_address);

mk_accessors(qw(validate_message));

my $JSON = Cpanel::JSON::XS->new;

my %SPEC = (
    height         => qr/^(?:0|[1-9][0-9]{0,9})\z/,
    blockhash      => qr/^[0-9a-f]{64}\z/,
    txid           => qr/^[0-9a-f]{64}\z/,
    command        => qr/^[a-z]{2,64}\z/,
    verbosity      => qr/^[12]\z/,
    hexstring      => qr/^(?:[0-9a-f][0-9a-f])+\z/,
    nblocks        => qr/^[1-9][0-9]{0,9}\z/,
    hash_or_height => qr/^(?:0|[1-9][0-9]{0,9}|[0-9a-f]{64})\z/,
    minconf        => qr/^(?:0|[1-9][0-9]{0,9})\z/,
    conf_target    => qr/^[1-9][0-9]?\z/,
    estimate_mode  => qr/^(?:economical|conservative)\z/i,
    token_id       => qr/^[0-9a-f]{64}\z/,
    verbose        => \&validate_boolean,
    replace        => \&validate_boolean,
    address        => \&validate_address,
    inputs         => \&validate_inputs,
    outputs        => \&validate_outputs,
    privatekeys    => \&validate_privkeys,
    privkey        => \&validate_privkey,
    address_type   => \&validate_address_type,
    pubkeyhash     => \&validate_pubkeyhash,
    tag            => qr/^(?:[a-zA-Z][a-zA-Z0-9_.-]{0,63})?\z/,
    split_spec     => \&validate_split_spec,
    password       => \&validate_password,
    node           => qr/^[0-9A-Za-z\[\]:.\-]{1,255}\z/,
    include_watchonly => \&validate_boolean,
);

sub validate {
    my $self = shift;
    my @spec = split(/\s+/, $_[0]);
    my $args = $self->args;

    if (@$args > @spec) {
        return $self->incorrect_params("Too many params", \@spec);
    }
    my $optional;
    for (my $i = 0; $i < @$args; $i++) {
        my $arg = $args->[$i];
        my $arg_name = $spec[$i];
        if (substr($arg_name, -1) eq '?') {
            $optional = 1;
        }
        elsif ($optional) {
            # Mandatory params cannot be after optional
            die "Incorrect params spec for " . $self->cmd;
        }
        my $spec_arg = $arg_name;
        $spec_arg =~ s/\?$//;
        $spec_arg =~ s@.*/@@;
        $arg_name =~ s@[:?/].*@@;
        if (my $rule = $SPEC{$spec_arg}) {
            if (ref($rule) eq 'Regexp') {
                defined($arg) && !ref($arg) && $arg =~ $rule
                    or return $self->incorrect_params("Incorrect parameter '$arg_name'", \@spec);
            }
            elsif (ref($rule) eq 'CODE') {
                $self->validate_message = undef;
                $rule->($args->[$i], $self) # arg may be modified by validation function
                    or return $self->incorrect_params($self->validate_message // "Incorrect parameter '$arg_name'", \@spec);
            }
            else {
                Warningf("Unknown type of validation rule for [%s]", $spec_arg);
            }
        }
        else {
            Warningf("No validation rule for [%s]", $spec_arg);
        }
    }
    for (my $i = @$args; $i < @spec; $i++) {
        next if substr($spec[$i], -1) eq '?';
        if ($optional) {
            die "Incorrect params spec for " . $self->cmd;
        }
        else {
            return $self->incorrect_params("Mandatory params missing", \@spec);
        }
    }
    return 0;
}

sub validate_boolean {
    my $value = $_[0];
    return 1 if ref($value) eq ref(FALSE);
    if (ref($value) eq "SCALAR") {
        $_[0] = $$value eq "1" ? TRUE : $$value eq "0" ? FALSE : return 0;
        return 1;
    }
    return 0 if ref($value);
    return 0 unless $value =~ /^(?:0|1|true|false)\z/;
    $value = 0 if $value eq "false";
    $_[0] = $value ? TRUE : FALSE;
    return 1;
}

sub validate_address {
    $_[0] =~ ADDRESS_RE;
}

sub validate_btc_address {
    return is_btc_address($_[0]);
}

sub is_amount {
    my $amount = shift;
    looks_like_number($amount) or return 0;
    $amount >= 0 or return 0;
    $amount * DENOMINATOR <= MAX_VALUE or return 0;
    return 1;
}

sub validate_txid {
    my $value = $_[0];
    $value =~ /^[0-9a-f]{64}\z/
        or return 0;
    return 1;
}

sub validate_vout {
    my $value = $_[0];
    $value =~ /^(?:0|[1-9][0-9]{0,5})\z/
        or return 0;
    $value <= 65535
        or return 0;
    return 1;
}

# splitstake spec: { tag => amount }, the empty tag stands for the untagged part;
# an empty object cancels the pending split
sub validate_split_spec {
    my $value = $_[0];
    my $spec = ref($value) ? $value : eval { $JSON->decode($value) };
    if (!$spec || ref($spec) ne "HASH") {
        return 0;
    }
    foreach my $tag (keys %$spec) {
        $tag =~ /^(?:[a-zA-Z][a-zA-Z0-9_.-]{0,63})?\z/
            or return 0;
        (!ref($spec->{$tag}) && is_amount($spec->{$tag}))
            or return 0;
    }
    $_[0] = $spec;
    return 1;
}

sub validate_inputs {
    my $value = $_[0];
    my $inputs = ref($value) ? $value : eval { $JSON->decode($value) };
    if (!$inputs || ref($inputs) ne "ARRAY" || !@$inputs) {
        return 0;
    }
    foreach my $in (@$inputs) {
        (defined($in->{txid}) && !ref($in->{txid}) && validate_txid($in->{txid})) or return 0;
        (defined($in->{vout}) && !ref($in->{vout}) && validate_vout($in->{vout})) or return 0;
        keys(%$in) == 2 or return 0;
    }
    $_[0] = $inputs;
    return 1;
}

sub validate_outputs {
    my $value = $_[0];
    my $outputs = ref($value) ? $value : eval { $JSON->decode($value) };
    if (!$outputs || (ref($outputs) ne "ARRAY" && ref($outputs) ne "HASH")) {
        return 0;
    }
    $outputs = [ $outputs ] if ref($outputs) eq "HASH";
    foreach my $out (@$outputs) {
        ref($out) eq "HASH" or return 0;
        my $address_count = 0;
        my $data_count = 0;
        my ($token_id, $token_amount, $token_control);
        foreach my $key (keys %$out) {
            if ($key eq "token_id") {
                $token_id = $out->{$key};
                ($token_id eq "" || $token_id =~ /^[0-9a-f]{64}\z/)
                    or return 0;
            }
            elsif ($key eq "token_amount") {
                (defined($out->{$key}) && !ref($out->{$key}) && $out->{$key} =~ /^(?:0|[1-9][0-9]{0,17})\z/)
                    or return 0;
                $token_amount = 1;
                next;
            }
            elsif ($key eq "token_name" || $key eq "token_symbol") {
                (defined($out->{$key}) && ref($out->{$key}) eq "")
                    or return 0;
                $token_control = 1;
                next;
            }
            elsif ($key eq "token_decimals") {
                (defined($out->{$key}) && ref($out->{$key}) eq "" && $out->{$key} =~ /^(?:[1-9]|1[0-8])\z/)
                    or return 0;
                $token_control = 1;
                next;
            }
            elsif ($key eq "token_permissions") {
                if (ref($out->{$key}) eq "") {
                    $out->{$key} =~ /^0x[0-9a-fA-F][0-9a-fA-F]\z/
                        or return 0;
                }
                elsif (ref($out->{$key}) eq "ARRAY") {
                    foreach my $perm (@{ $out->{$key} }) {
                        ref($perm) eq "" or return 0;
                        $perm =~ /^(?:mint)\z/ or return 0;
                    }
                }
                else {
                    return 0;
                }
                $token_control = 1;
            }
            elsif ($key eq "data") {
                (defined($out->{$key}) && !ref($out->{$key})
                    && $out->{$key} =~ /^(?:[0-9a-fA-F][0-9a-fA-F])*\z/
                    && length($out->{$key}) <= 2 * MAX_TXO_DATA_SIZE)
                    or return 0;
                $data_count++;
                next;
            }
            elsif ($key eq "tag") {
                (defined($out->{$key}) && !ref($out->{$key})
                    && $out->{$key} =~ /^[a-zA-Z][a-zA-Z0-9_.-]{0,63}\z/)
                    or return 0;
                $data_count++;
                next;
            }
            elsif (validate_address($key) || validate_btc_address($key)) {
                (defined($out->{$key}) && !ref($out->{$key}) && is_amount($out->{$key}))
                    or return 0;
                $out->{$key} = int($out->{$key} * DENOMINATOR + 0.5);
                $address_count++;
            }
            else {
                return 0;
            }
        }
        return 0 if defined($token_id) && $address_count != 1;
        return 0 if ($token_amount || $token_control) && !defined($token_id);
        return 0 if $token_amount && $token_control; # must be either amount or control, not both
        return 0 if $token_control && $token_id;     # control allowed only for new tokens
        # data/tag labels a single regular output; token data and the freeze output
        # structure are generated, not user-supplied
        return 0 if $data_count > 1;
        return 0 if $data_count && ($address_count != 1 || defined($token_id));
    }
    $_[0] = $outputs;
    return 1;
}

sub validate_privkey {
    eval { wif_to_pk($_[0]) }
        or return 0;
    return 1;
}

sub validate_privkeys {
    my $value = $_[0];
    my $privkeys = ref($value) ? $value : eval { $JSON->decode($value) };
    if (!$privkeys || ref($privkeys) ne "ARRAY") {
        return 0;
    }
    foreach my $privkey (@$privkeys) {
        ref($privkey) eq "" or return 0;
        eval { wif_to_pk($privkey) }
            or return 0;
    }
    $_[0] = $privkeys;
    return 1;
}

sub validate_password {
    my $value = $_[0];
    return 0 if ref($value);
    return 0 unless defined($value) && length($value) >= 1 && length($value) <= QBitcoin::Password::MAX_LEN();
    return 1;
}

# The base58 form of the pubkey hash used in delegated staking (hash160 for
# pre-quantum keys, hash256 for post-quantum ones); the validated argument is
# replaced with the binary hash
sub validate_pubkeyhash {
    return 0 if !defined($_[0]) || ref($_[0]);
    my $pubkeyhash = eval { pubkeyhash_by_str($_[0]) }
        or return 0;
    $_[0] = $pubkeyhash;
    return 1;
}

sub validate_address_type {
    my $value = $_[0]
        or return 0;
    my $algo = CRYPT_ALGO_BY_NAME->{$value}
        or return 0;
    $_[0] = $algo;
}

sub incorrect_params {
    my $self = shift;
    my ($message, $spec) = @_;
    $self->response_error($message, ERR_INVALID_PARAMS, $self->brief($self->cmd) . "\n" . $self->help($self->cmd));
    return -1;
}

sub brief {
    my $self = shift;
    my ($cmd) = @_;
    my $spec = $self->params($cmd);
    my $params = $cmd;
    my $optional = 0;
    foreach my $arg (split(/\s+/, $spec)) {
        $params .= " ";
        if (substr($arg, -1) eq "?") {
            $params .= "[ ";
            $optional++;
        }
        $arg =~ s@[/:?].*@@;
        $params .= "<$arg>";
    }
    $params .= " " . "]" x $optional if $optional;
    return $params;
}

1;
