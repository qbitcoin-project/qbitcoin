package QBitcoin::Transaction;
use warnings;
use strict;
use feature 'state';

use Tie::IxHash;
use List::Util qw(sum0);
use Scalar::Util qw(refaddr);
use QBitcoin::Const;
use QBitcoin::Log;
use QBitcoin::Config;
use QBitcoin::BlockchainParams;
use QBitcoin::Accessors qw(mk_accessors);
use QBitcoin::ORM qw(find fetch create delete :types);
use QBitcoin::Crypto qw(hash160 hash256);
use QBitcoin::Address qw(address_by_hash);
use QBitcoin::TXO;
use QBitcoin::Coinbase;
use QBitcoin::Slashing;
use QBitcoin::Slashing::Stored;
use QBitcoin::Downgrade;
use QBitcoin::Downgrade::Commitment;
use QBitcoin::Downgrade::Spv;
use QBitcoin::ValueUpgraded qw(level_by_total upgrade_value downgrade_net downgrade_value_at_level);
use QBitcoin::ConnectionList;
use QBitcoin::Notify;
use QBitcoin::ProtocolState qw(skip_scripts blockchain_synced);
use QBitcoin::Generate::Control;
use QBitcoin::Coins;
use Bitcoin::Serialized;
use Bitcoin::Address qw(is_btc_address scriptpubkey_to_btc_address);
use QBitcoin::MyAddress;

use Role::Tiny::With;
with 'QBitcoin::Transaction::Tokens';
with 'QBitcoin::Transaction::Signature';

# Scripthash of the qbt_burn address (QBT_BURN_SCRIPT), precomputed once.
# Outputs to this scripthash whose data field contains a valid Bitcoin address
# string (ASCII) are downgrade requests; the address is shown by decoderawtransaction.
use constant QBT_BURN_SCRIPTHASH => hash160(QBT_BURN_SCRIPT);

# QBT_FREEZE_SCRIPTHASH / QBT_DOWNGRADE_SCRIPTHASH come from QBitcoin::Const.
use constant QBT_RECLAIM_ID_LEN => 32;   # reclaim_id = hash256(pubkey)

use constant FIELDS => {
    id           => NUMERIC, # db primary key for reference links
    hash         => BINARY,
    block_height => NUMERIC,
    block_pos    => NUMERIC,
    fee          => NUMERIC,
    size         => NUMERIC,
    tx_type      => NUMERIC,
    token_id     => NUMERIC,
};

use constant TABLE => 'transaction';

use constant ATTR => qw(
    received_time
    received_from
    in
    out
    up
    input_pending
    input_detached
    blocks
    block_sign_data
    rcvd
    in_raw
    block_time
    drop_immune
    upgrade_level
    token_hash
    slashing
    down
);

mk_accessors(keys %{&FIELDS}, ATTR);

my %TRANSACTION;      # in-memory cache transaction objects by tx_hash
my %TX_SEQ_DEPENDS;   # txo depends min_rel_time or min_rel_block_height
my %PENDING_INPUT_TX; # 2-level hash $pending_hash => $hash; value - transaction object
my %PENDING_TX_INPUT; # hash of pending transaction objects by tx_hash
tie(%PENDING_TX_INPUT, 'Tie::IxHash'); # Ordered by age, to remove oldest

my $MEMPOOL_SIZE = 0;           # total size of txs in mempool
my $MEMPOOL_ZERO_FEE_COUNT = 0; # count of zero-fee txs in mempool
my $MEMPOOL_WORST;

END {
    # Free all references to txo for graceful free %TXO hash
    undef %TRANSACTION;
};

sub get_by_hash { # cached or load from database
    my $class = shift;
    my ($tx_hash) = @_;

    return $class->get($tx_hash) // $class->find(hash => $tx_hash);
}

# return block_height or undef for unknown transaction, -1 for mempool (unconfirmed)
sub check_by_hash {
    my $class = shift;
    my ($tx_hash) = @_;

    my $block_height;
    if (my $tx = $class->get($tx_hash)) {
        $block_height = $tx->block_height // -1;
    }
    elsif (my ($tx_hash) = $class->fetch(hash => $tx_hash)) {
        $block_height = $tx_hash->{block_height};
    }
    else {
        return undef;
    }
    return $block_height || "0e0";
}

sub get { # only cached
    my $class = shift;
    my ($tx_hash) = @_;

    return $TRANSACTION{$tx_hash};
}

sub is_mempool_limited {
    $_[0]->{tx_type} == TX_TYPE_STANDARD || $_[0]->{tx_type} == TX_TYPE_TOKENS;
}

sub _mempool_insert {
    my ($tx) = @_;
    return unless $tx->is_mempool_limited;
    if ($MEMPOOL_SIZE == 0 || (defined($MEMPOOL_WORST) && compare_tx($tx, $MEMPOOL_WORST) > 0)) {
        $MEMPOOL_WORST = $tx;
    }
    $MEMPOOL_SIZE += $tx->size;
    $MEMPOOL_ZERO_FEE_COUNT++ if $tx->fee == 0;
}

sub _mempool_remove {
    my ($tx) = @_;
    return unless $tx->is_mempool_limited;
    $MEMPOOL_SIZE -= $tx->size;
    $MEMPOOL_ZERO_FEE_COUNT-- if $tx->fee == 0;
    if (defined($MEMPOOL_WORST) && $tx->hash eq $MEMPOOL_WORST->hash) {
        undef $MEMPOOL_WORST;
    }
}

sub mempool_worst_tx {
    if (!defined($MEMPOOL_WORST) && $MEMPOOL_SIZE) {
        foreach my $tx (grep { !defined($_->block_height) && $_->is_mempool_limited } values %TRANSACTION) {
            if (!defined($MEMPOOL_WORST) || compare_tx($tx, $MEMPOOL_WORST) > 0) {
                $MEMPOOL_WORST = $tx;
            }
        }
    }
    return $MEMPOOL_WORST;
}

sub add_to_cache {
    my $self = shift;

    if (exists $TRANSACTION{$self->hash}) {
        die "receive already loaded transaction " . $self->hash_str . "\n";
    }
    $TRANSACTION{$self->hash} = $self;

    foreach my $in (@{$self->in}) {
        $in->{txo}->spent_add($self);
    }

    if (!defined($self->block_height)) {
        _mempool_insert($self);
    }
}

sub delete_from_cache {
    my $self = shift;

    foreach my $in (@{$self->in}) {
        my $txo = $in->{txo};
        if (delete $TX_SEQ_DEPENDS{$txo->tx_in}->{$self->hash}) {
            delete $TX_SEQ_DEPENDS{$txo->tx_in} unless %{$TX_SEQ_DEPENDS{$txo->tx_in}};
        }
    }

    _mempool_remove($self);
    delete $TRANSACTION{$self->hash};
}

sub save {
    my $self = shift;

    $self->add_to_cache();

    foreach my $in (@{$self->in}) {
        # Exclude from my utxo spent unconfirmed, do not use them for stake transactions
        $in->{txo}->del_my_utxo() if $self->fee >= 0 && $in->{txo}->is_my;
    }

    # Notify about outputs to tracked addresses (mempool event)
    if (QBitcoin::Notify->enabled()) {
        foreach my $txo (@{$self->out}) {
            QBitcoin::Notify->check_output($txo, $self);
        }
    }

    return 0;
}

sub receive {
    no warnings 'recursion'; # recursion may be deeper than perl default 100 levels
    my $self = shift;

    if ($self->validate_hash() or $self->validate()) {
        foreach my $in (@{$self->in}) {
            $in->{txo}->spent_del($self);
        }
        return -1;
    }

    # A coinbase at or after the locally known stop-utxo spend can never be confirmed;
    # ignore it silently and do not store its record (no punishment for the sender:
    # it may not have seen the spend yet)
    if ($self->is_coinbase && $self->up && $self->up->after_stop) {
        Debugf("Ignore coinbase transaction %s at or after the upgrade stop", $self->hash_str);
        return undef; # ignored, not invalid
    }

    # Admission control: check if mempool has room for this tx
    if (!want_tx($self)) {
        Debugf("Transaction %s rejected by mempool admission control (fee %li, size %u)",
            $self->hash_str, $self->fee, $self->size);
        foreach my $in (@{$self->in}) {
            $in->{txo}->spent_del($self);
        }
        return undef; # ignored, not invalid
    }

    if ($self->save()) {
        foreach my $in (@{$self->in}) {
            $in->{txo}->spent_del($self);
        }
        return -1;
    }

    # Evict lowest-priority transactions if over limits
    if ($MEMPOOL_SIZE > MAX_MEMPOOL_SIZE * 2) {
        evict_mempool();
    }

    if ($self->is_slashing) {
        # A valid slashing tx (built locally or received) bans the equivocated stake:
        # any block built on it is now invalid. Trigger (re)generation to drop such a
        # branch if it is currently best.
        QBitcoin::Slashing->ban_from_tx($self);
        QBitcoin::Generate::Control->generate_new() if blockchain_synced();
    }

    if ($self->up) {
        $self->up->store; # and update $self->up->tx_out here if already stored
    }

    Debugf("Process tx %s fee %li size %u", $self->hash_str, $self->fee, $self->size);
    $self->process_pending();
    return 0;
}

sub received_from_peer {
    my $self = shift;
    return $self->received_from && $self->received_from->can('peer');
}

sub process_pending {
    no warnings 'recursion'; # recursion may be deeper than perl default 100 levels
    my $self = shift;
    if (my $pending = delete $PENDING_INPUT_TX{$self->hash}) {
        foreach my $tx (values %$pending) {
            if (!$tx->add_pending_tx($self)) {
                $tx->drop();
                next;
            }
            if (!$tx->is_pending) {
                Debugf("Process transaction %s pending for %s", $tx->hash_str, $self->hash_str);
                foreach my $in (@{$tx->in}) {
                    $in->{txo}->spent_confirm($tx);
                }
                $tx->calculate_fee();
                $tx->received_from->process_tx($tx);
            }
        }
    }
}

sub add_pending_tx {
    my $self = shift;
    my ($tx) = @_;

    my $tx_in;
    if ($self->{input_pending} && ($tx_in = delete $self->{input_pending}->{$tx->hash})) {
        foreach my $in (grep { defined($_) } values %$tx_in) {
            my $txo = QBitcoin::TXO->get($in);
            if (!$txo) {
                Warningf("Transaction %s has no output %u for tx %s input",
                    $self->hash_str($in->{tx_out}), $in->{num}, $self->hash_str);
                return undef;
            }
            if ($txo->set_redeem_script($in->{redeem_script}) != 0) {
                Warningf("Incorrect redeem_script for input %s on %s", $txo->tx_in_str, $self->hash_str);
                return undef;
            }
            if ($tx->is_pending) {
                $self->{input_detached}->{$tx->hash} //= [];
                push @{$self->{input_detached}->{$tx->hash}}, { txo => $txo, siglist => $in->{siglist} };
            }
            else {
                push @{$self->in}, { txo => $txo, siglist => $in->{siglist} };
            }
            $txo->spent_add($self);
        }
        if (!%{$self->{input_pending}}) {
            delete $self->{input_pending};
        }
    }
    elsif ($self->{input_detached} && ($tx_in = delete $self->{input_detached}->{$tx->hash})) {
        push @{$self->in}, @$tx_in;
        if (!%{$self->{input_detached}}) {
            delete $self->{input_detached};
        }
    }
    if (!($self->{input_pending} && %{$self->{input_pending}}) && !($self->{input_detached} && %{$self->{input_detached}})) {
        $self->in = [ sort { _cmp_inputs($a, $b) } @{$self->in} ];
        delete $PENDING_TX_INPUT{$self->hash};
    }
    $self->received_from->has_pending(1) if $self->received_from;
    return $self;
}

sub add_to_block {
    my $self = shift;
    my ($block) = @_;
    $self->{blocks}->{$block->hash} = 1;
    $self->{received_time} //= $block->time; # for transactions loaded from database, they may be unconfirmed and go to mempool
}

sub del_from_block {
    my $self = shift;
    my ($block) = @_;
    delete $self->{blocks}->{$block->hash};
    if (!%{$self->{blocks}}) {
        if (defined($self->block_height)) {
            # Confirmed, not mempool
            $self->free();
        }
        elsif ($self->is_stake) {
            $self->drop();
        }
    }
}

sub in_blocks {
    my $self = shift;
    return $self->{blocks} ? (keys %{$self->{blocks}}) : ();
}

sub mempool_list {
    my $class = shift;
    return grep { !defined($_->block_height) && $_->fee >= 0 } values %TRANSACTION;
}

# This method calls when the confirmed transaction stored into the database and is not needed in memory anymore
# TXO (input and output) will free from %TXO hash by DESTROY() method, they have weaken reference for this
sub free {
    my $self = shift;
    return if $self->in_blocks;
    if (defined($self->block_height) && !$self->id) {
        die "Attempt to free not stored transaction " . $self->hash_str . " confirmed in block " . $self->block_height . "\n";
    }
    foreach my $in (@{$self->in}) {
        $in->{txo}->spent_del($self);
    }
    $self->delete_from_cache;
}

# Drop the transaction from mempool and all dependent transactions if any
sub drop {
    no warnings 'recursion'; # recursion may be deeper than perl default 100 levels
    my $self = shift;
    if (!$TRANSACTION{$self->hash} && !exists $PENDING_TX_INPUT{$self->hash}) {
        Debugf("Attempt to drop not cached transaction %s (already dropped?)", $self->hash_str);
        return 1;
    }
    if (defined($self->block_height)) {
        Errf("Attempt to drop confirmed transaction %s, block height %u", $self->hash_str, $self->block_height);
        die "Can't drop confirmed transaction " . $self->hash_str . "\n";
    }
    if ($self->in_blocks) {
        Debugf("Transaction %s is in loaded unconfirmed blocks, do not drop", $self->hash_str);
        return;
    }
    if ($self->drop_immune) {
        Debugf("Transaction %s is drop-immune, do not drop", $self->hash_str);
        return;
    }
    foreach my $out (@{$self->out}) {
        foreach my $dep_tx ($out->spent_list, $out->spent_pending) {
            Infof("Drop transaction %s dependent on %s", $dep_tx->hash_str, $self->hash_str);
            $dep_tx->drop()
                or return; # Do not drop the tx if any dependent transaction is in unconfirmed block, at any depth
        }
    }
    foreach my $in (@{$self->in}, map { @$_ } values %{$self->input_detached // {}}) {
        $in->{txo}->spent_del($self);
    }
    if ($self->is_pending) {
        Debugf("Drop pending transaction %s", $self->hash_str);
        delete $PENDING_TX_INPUT{$self->hash};
        foreach my $tx_in (keys(%{$self->input_detached // {}}), keys(%{$self->input_pending // {}})) {
            delete $PENDING_INPUT_TX{$tx_in}->{$self->hash};
            delete $PENDING_INPUT_TX{$tx_in} unless %{$PENDING_INPUT_TX{$tx_in}};
        }
    }
    else {
        Debugf("Drop transaction %s from mempool", $self->hash_str);
        foreach my $out (@{$self->out}) {
            $out->del_my_utxo if $out->is_my;
        }
        foreach my $txo (map { $_->{txo} } @{$self->in}) {
            if ($txo->is_my && $txo->unspent) {
                # add to my_utxo list only if it was confirmed in the best branch
                my $class = ref $self;
                my $tx_in = $class->get($txo->tx_in);
                if (!$tx_in || defined($tx_in->block_height)) {
                    $txo->add_my_utxo;
                }
            }
        }
        $self->delete_from_cache;
    }
    return 1;
}

# Remove unneeded stake transactions and transactions with confirmed spent inputs
sub cleanup_mempool {
    my $class = shift;
    my @tx = grep { !$_->in_blocks && !defined($_->block_height) } values %TRANSACTION;
    foreach my $tx (@tx) {
        if ($tx->is_stake) {
            if ($tx->drop()) {
                Infof("Drop stake tx %s not related to any known blocks", $tx->hash_str);
            }
            next;
        }
        if (UPGRADE_POW && $tx->is_coinbase) {
            my $drop;
            if ($tx->up->tx_out) {
                $drop = "already spent in " . $tx->hash_str($tx->up->tx_out);
            }
            elsif ($tx->up->after_stop) {
                $drop = "at or after the upgrade stop";
            }
            elsif (my $best = QBitcoin::Block->best_block) {
                if (($best->upgraded // 0) >= UPGRADE_MAX_VALUE) {
                    $drop = "upgrade threshold reached";
                }
                elsif ($best->upgrade_stopped) {
                    $drop = "upgrade stopped";
                }
            }
            if ($drop) {
                if ($tx->drop()) {
                    Infof("Drop coinbase tx %s from mempool: %s", $tx->hash_str, $drop);
                }
            }
            next;
        }
        if ($tx->is_upgrade_stop) {
            my $drop;
            if ($tx->up->tx_out) {
                $drop = "already published in " . $tx->hash_str($tx->up->tx_out);
            }
            elsif (my $best = QBitcoin::Block->best_block) {
                if ($best->upgrade_stopped) {
                    $drop = "upgrade already stopped in the best branch";
                }
            }
            if ($drop) {
                if ($tx->drop()) {
                    Infof("Drop upgrade stop tx %s from mempool: %s", $tx->hash_str, $drop);
                }
            }
            next;
        }
        if ($tx->is_slashing) {
            # A slashing tx intentionally double-spends the equivocating stake's UTXO:
            # its input is "already spent" by the block we mean to slash. Keep it so we
            # can land it by unconfirming that block (see QBitcoin::Generate). Drop only
            # once the target is buried deeper than the slashing window can reorg.
            my $db_height = QBitcoin::Block->max_db_height // 0;
            my $buried = 1;
            foreach my $in (@{$tx->in}) {
                my $out = $in->{txo}->tx_out;
                if (!$out) {
                    $buried = 0;
                    last;
                }
                my $spent_tx = $class->get($out)
                    or next; # Spent transaction not in cache, maybe already confirmed and freed
                my $h = $spent_tx->block_height;
                if (!defined($h) || $h > $db_height) {
                    $buried = 0;
                    last;
                }
            }
            if ($buried && $tx->drop()) {
                Infof("Drop slashing tx %s: target buried beyond the slashing window", $tx->hash_str);
            }
            next;
        }
        my $spent_txo;
        foreach my $in (@{$tx->in}) {
            my $txo = $in->{txo};
            if ($txo->tx_out) {
                # Already confirmed spent
                $spent_txo = $txo;
                last;
            }
        }
        if ($spent_txo) {
            if ($tx->drop()) {
                Infof("Drop mempool tx %s b/c input %s:%u was already spent in %s",
                    $tx->hash_str, $spent_txo->tx_in_str, $spent_txo->num, $tx->hash_str($spent_txo->tx_out));
            }
            next;
        }
    }
}

sub store {
    my $self = shift;
    $self->is_cached or die "store not cached transaction " . $self->hash_str;
    # we are in sql transaction
    if ($self->is_tokens && length($self->token_hash // "")) {
        my ($tokens_tx) = QBitcoin::Transaction->fetch(hash => $self->token_hash);
        $self->token_id = $tokens_tx->{id};
    }
    $self->create();
    if ($self->is_slashing && $self->slashing) {
        # Persist the equivocation evidence so the transaction can be rebuilt (and its
        # hash re-verified) on load.
        QBitcoin::Slashing::Stored->create({
            tx_id => $self->id,
            %{$self->slashing->stored_fields},
        });
    }
    elsif ($self->is_downgrade && $self->down) {
        QBitcoin::Downgrade::Commitment->create({
            tx_id        => $self->id,
            freeze_txid  => $self->down->freeze_txid,
            freeze_vout  => $self->down->freeze_vout,
            btc_txid     => $self->down->btc_txid,
            btc_vout     => $self->down->btc_vout,
            btc_value    => $self->down->btc_value,
            scriptpubkey => $self->down->scriptpubkey,
        });
    }
    elsif ($self->is_burn && $self->down) {
        # Link (or insert) the SPV proof row for this burn's source downgrade.
        QBitcoin::Downgrade::Spv->store_burn($self->in->[0]{txo}->tx_in, $self->down, $self->id);
    }
    foreach my $in (@{$self->in}) {
        $in->{txo}->store_spend($self),
    }
    foreach my $txo (@{$self->out}) {
        $txo->store($self);
    }
    if (my $coinbase = $self->up) {
        $coinbase->store_published($self);
    }
}

sub hash_str {
    my $arg = pop;
    my $hash = ref($arg) ? $arg->hash : $arg;
    return unpack("H*", substr($hash, 0, 4));
}

sub serialize {
    my $self = shift;

    my $data = pack("c", $self->tx_type);
    $data .= $self->slashing->serialize if $self->is_slashing && $self->slashing; # equivocation evidence
    $data .= $self->down_payload; # downgrade commitment / burn SPV proof
    $data .= varstr($self->token_hash // "") if $self->is_tokens;
    $data .= varint(scalar @{$self->in});
    # Slashing inputs spend the equivocated UTXOs without a signature, so they carry an
    # empty siglist and empty redeem_script (the evidence authorizes the spend).
    if ($self->is_slashing) {
        $data .= serialize_input_noscript($_) foreach @{$self->in};
    }
    else {
        $data .= serialize_input($_) foreach @{$self->in};
    }
    $data .= varint(scalar @{$self->out});
    $data .= serialize_output($_) foreach @{$self->out};
    if (my $coinbase = $self->is_coinbase) {
        $data .= UPGRADE_POW ? varint($self->upgrade_level) . serialize_coinbase($self->up) : pack("Q<", $self->coins_created);
    }
    elsif ($self->is_upgrade_stop) {
        $data .= serialize_coinbase($self->up); # SPV proof of the stop-utxo spend
    }
    return $data;
}

sub serialize_unsigned {
    my $self = shift;

    my $data = pack("c", $self->tx_type);
    $data .= $self->slashing->serialize if $self->is_slashing && $self->slashing; # equivocation evidence
    $data .= $self->down_payload; # downgrade commitment / burn SPV proof
    $data .= varstr($self->token_hash // "") if $self->is_tokens;
    $data .= varint(scalar @{$self->in});
    if ($self->in_raw) {
        $data .= serialize_input_raw($_) foreach @{$self->in_raw};
    }
    else {
        $data .= serialize_input_unsigned($_) foreach @{$self->in};
    }
    $data .= varint(scalar @{$self->out});
    $data .= serialize_output($_) foreach @{$self->out};
    return $data;
}

sub sign_data {
    my $self = shift;
    my ($input_num, $sighash_type) = @_;

    my $data;
    my $per_input = ($sighash_type & SIGHASH_ANYONECANPAY) || $sighash_type == SIGHASH_SINGLE;
    my $cached_data = $per_input ?  \$self->{sign_data}->[$sighash_type]->[$input_num] : \$self->{sign_data}->[$sighash_type];
    if (!defined($data = $$cached_data)) {
        $data = pack("C", $self->tx_type);
        $data .= $self->down_payload; # downgrade commitment / burn SPV proof
        if ($sighash_type & SIGHASH_ANYONECANPAY) {
            # Only the current input is signed, not all inputs
            $data .= serialize_input_for_sign($self->in->[$input_num]);
        }
        else {
            $data .= varint(scalar @{$self->in});
            $data .= serialize_input_for_sign($_) foreach @{$self->in};
        }
        $sighash_type &= ~SIGHASH_ANYONECANPAY;
        if ($sighash_type == SIGHASH_ALL) {
            $data .= varint(scalar @{$self->out});
            $data .= serialize_output($_) foreach @{$self->out};
        }
        elsif ($sighash_type == SIGHASH_SINGLE) {
            # We do not need to sign coinbase transactions
            $data .= defined($self->out->[$input_num]) ? serialize_output($self->out->[$input_num]) : "";
        }
        elsif ($sighash_type != SIGHASH_NONE) {
            Warningf("Unsupported sighash type %s in transaction %s", $sighash_type, $self->hash_str);
            return undef;
        }
        # We do not need to sign coinbase transactions
        $$cached_data = $data;
    }
    if ($self->is_tokens && length($self->token_hash // "")) {
        $data .= $self->token_hash;
    }
    if ($self->is_stake) {
        # It's stake tx which signs block, add block info
        $data .= $self->block_sign_data;
    }
    return $data;
}

sub type_as_text {
    my $self = shift;
    return TX_TYPES_NAMES->[$self->tx_type];
}

# For JSON RPC output
sub as_hashref {
    my $self = shift;
    my $res = {
        size => $self->size //= length($self->serialize),
        in   => $self->in_raw ? [ map { inputraw_as_hashref($_) } @{$self->in_raw} ] : [ map { input_as_hashref($_) } @{$self->in} ],
        out  => [ map { $self->output_as_hashref($_) } @{$self->out} ],
        type => $self->type_as_text,
    };
    $res->{hash} = $res->{txid} = unpack("H*", $self->hash) if defined $self->hash;
    $res->{fee} = $self->fee / DENOMINATOR if defined $self->fee;
    $res->{up} = $self->up->as_hashref if $self->up;
    $res->{coins_created} = $self->coins_created / DENOMINATOR if !UPGRADE_POW && defined $self->coins_created;
    $res->{time} = $self->received_time if defined $self->received_time;
    $res->{token_id} = unpack("H*", $self->token_hash // "") if $self->is_tokens;
    if ($self->is_downgrade && $self->down) {
        $res->{freeze_txid}   = unpack("H*", $self->down->freeze_txid);
        $res->{freeze_vout}   = $self->down->freeze_vout + 0;
        $res->{btc_txid}      = unpack("H*", scalar reverse $self->down->btc_txid);
        $res->{btc_vout}      = $self->down->btc_vout + 0;
        $res->{btc_value}     = $self->down->btc_value + 0;
        $res->{btc_scriptpubkey} = unpack("H*", $self->down->scriptpubkey);
    }
    elsif ($self->is_burn && $self->down) {
        $res->{btc_block_hash} = unpack("H*", scalar reverse $self->down->btc_block_hash);
        $res->{btc_txid}       = unpack("H*", scalar reverse hash256($self->down->btc_tx_data));
    }
    return $res;
}

sub input_as_hashref {
    my $in = shift;
    $in->{siglist} or die "Undefined siglist during input_as_hashref";
    my $redeem_script = $in->{txo}->redeem_script // ""; # Slashing txo has no redeem_script
    my $alg = 0;
    $alg = unpack("xC", $in->{siglist}->[0]) if @{$in->{siglist}} && length($in->{siglist}->[0]) > 1;
    my $hash = $alg & CRYPT_ALGO_POSTQUANTUM ? hash256($redeem_script) : hash160($redeem_script);
    return {
        txid          => unpack("H*", $in->{txo}->tx_in),
        num           => $in->{txo}->num+0,
        siglist       => [ map { unpack("H*", $_) } @{$in->{siglist}} ],
        redeem_script => unpack("H*", $redeem_script),
        address       => address_by_hash($hash),
    };
}

sub inputraw_as_hashref {
    my $in = shift;
    $in->{siglist} or die "Undefined siglist during input_as_hashref";
    my $redeem_script = $in->{redeem_script} // "";
    my $hash;
    if ($redeem_script) {
        my $alg = 0;
        $alg = unpack("xC", $in->{siglist}->[0]) if @{$in->{siglist}} && length($in->{siglist}->[0]) > 1;
        $hash = $alg & CRYPT_ALGO_POSTQUANTUM ? hash256($redeem_script) : hash160($redeem_script);
    }
    return {
        txid          => unpack("H*", $in->{tx_out}),
        num           => $in->{num}+0,
        siglist       => [ map { unpack("H*", $_) } @{$in->{siglist} // []} ],
        redeem_script => unpack("H*", $redeem_script),
        $redeem_script ? ( address => address_by_hash($hash) ) : (),
    };
}

sub serialize_siglist {
    my $siglist = shift;
    return varint(scalar @$siglist) . join("", map { varstr($_) } @$siglist);
}

sub serialize_input_unsigned {
    my $in = shift;
    return $in->{txo}->tx_in . varint($in->{txo}->num) . serialize_siglist($in->{siglist} // []) . varstr($in->{txo}->redeem_script // "");
}

sub serialize_input_raw {
    my $in = shift;
    return $in->{tx_out} . varint($in->{num}) . serialize_siglist($in->{siglist} // []) . varstr($in->{redeem_script} // "");
}

sub serialize_input {
    my $in = shift;
    my $siglist = $in->{siglist} // die "Undefined siglist during serialize_input";
    my $redeem_script = $in->{txo}->redeem_script // die "Undefined redeem_script during serialize_input";
    return $in->{txo}->tx_in . varint($in->{txo}->num) . serialize_siglist($siglist) . varstr($redeem_script);
}

sub serialize_input_for_sign {
    my $in = shift;
    return $in->{txo}->tx_in . varint($in->{txo}->num);
}

# Slashing input: no signature, no redeem_script (the equivocation evidence authorizes
# the spend). Serialized canonically as empty siglist + empty redeem_script so every
# node produces byte-identical slashing transactions.
sub serialize_input_noscript {
    my $in = shift;
    return $in->{txo}->tx_in . varint($in->{txo}->num) . serialize_siglist([]) . varstr("");
}

sub deserialize_siglist {
    my $data = shift;
    my $num = $data->get_varint() // return undef;
    $num <= MAX_SIGLIST_SIZE or return undef;
    my @siglist = map { $data->get_string() // return undef } 1 .. $num;
    return \@siglist;
}

sub deserialize_input {
    my $data = shift;
    my $tx_out = $data->get(32) // return undef;
    my $num = $data->get_varint() // return undef;
    $num < MAX_OUTPUTS_PER_TX or return undef;
    my $siglist = deserialize_siglist($data) // return undef;
    my $redeem_script = $data->get_string() // return undef;
    return {
        tx_out        => $tx_out,
        num           => $num,
        siglist       => $siglist,
        redeem_script => $redeem_script,
    };
}

sub serialize_output {
    my $out = shift;
    return pack("Q<", $out->value) . varstr($out->scripthash) . varstr($out->data);
}

sub deserialize_output {
    my $data = shift;
    return {
        value      => unpack("Q<", $data->get(8) // return undef),
        scripthash => ( $data->get_string() // return undef ),
        data       => ( $data->get_string() // return undef ),
    };
}

# Display info for a trustless-downgrade output, used by RPC (output_as_hashref)
# and REST (vout_obj). Freeze output: data = [reclaim_id][btc scriptPubKey];
# downgrade-tx output: data = [reclaim_id]. Returns ($btc_address, $downgrade)
# where $downgrade = { reclaim => "pending" | hex reclaim_id, reclaim_address =>
# our QBTC address when the reclaim_id matches a wallet key }, or an empty list
# for other outputs. reclaim_id = hash256(pubkey) is a one-way hash, so the
# reclaim address is resolvable only via the wallet key store.
sub output_downgrade_info {
    my ($out) = @_;
    my $sh = $out->scripthash // "";
    return () unless $sh eq QBT_FREEZE_SCRIPTHASH || $sh eq QBT_DOWNGRADE_SCRIPTHASH;
    my $rlen = QBT_RECLAIM_ID_LEN;
    my $data = $out->data // "";
    return () unless length($data) >= $rlen;
    my $reclaim_id = substr($data, 0, $rlen);
    my $btc_addr = length($data) > $rlen ? scriptpubkey_to_btc_address(substr($data, $rlen)) : undef;
    my $downgrade;
    if ($reclaim_id eq ("\x00" x $rlen)) {
        $downgrade = { reclaim => "pending" };
    }
    else {
        $downgrade = { reclaim => unpack("H*", $reclaim_id) };
        if (my $my_address = QBitcoin::MyAddress->get_by_pubkeyhash($reclaim_id)) {
            $downgrade->{reclaim_address} = $my_address->address;
        }
    }
    return ($btc_addr, $downgrade);
}

sub output_as_hashref {
    my $self = shift;
    my $out = shift;
    my $value = $out->value;
    my $res = {
        value   => $value / DENOMINATOR,
        address => $out->address,
    };
    # Trustless-downgrade outputs: show the Bitcoin destination address (so
    # decoderawtransaction mirrors what createrawtransaction accepted) and the
    # reclaim status.
    if (my ($btc_addr, $downgrade) = output_downgrade_info($out)) {
        $res->{address}   = $btc_addr if defined $btc_addr;
        $res->{downgrade} = $downgrade;
    }
    if ($self->is_tokens) {
        $res = { %$res, %{$self->token_output_as_hashref($out)} };
    }
    elsif (length($out->data)) {
        if (ord($out->data) eq ord(TXO_DATA_TAG) && !$res->{downgrade}) {
            $res->{tag} = substr($out->data, 1);
        }
        else {
            $res->{data} = unpack("H*", $out->data);
        }
    }
    return $res;
}

sub serialize_coinbase {
    my $coinbase = shift;
    return $coinbase->serialize;
}

sub deserialize_coinbase {
    my ($data, $upgrade_level) = @_;
    return QBitcoin::Coinbase->deserialize($data, $upgrade_level);
}

sub deserialize {
    my $class = shift;
    my ($data) = @_;
    my $start_index = $data->index;
    my $tx_type = unpack("c", $data->get(1));
    my $slashing;
    my $down;
    if ($tx_type == TX_TYPE_SLASHING) {
        $slashing = QBitcoin::Slashing->deserialize($data) // return undef;
    }
    elsif ($tx_type == TX_TYPE_DOWNGRADE) {
        $down = QBitcoin::Downgrade->deserialize_commitment($data) // return undef;
    }
    elsif ($tx_type == TX_TYPE_BURN) {
        $down = QBitcoin::Downgrade->deserialize_proof($data) // return undef;
    }
    my $token_hash;
    $token_hash = $data->get_string() if $tx_type == TX_TYPE_TOKENS;
    my $inputs = $data->get_varint // return undef;
    $inputs <= MAX_INPUTS_PER_TX or return undef;
    my @input  = map { deserialize_input($data)  // return undef } 1 .. $inputs;
    my $outputs = $data->get_varint // return undef;
    $outputs <= MAX_OUTPUTS_PER_TX or return undef;
    my @output = map { deserialize_output($data) // return undef } 1 .. $outputs;
    my $up;
    my $upgrade_level;
    if ($tx_type == TX_TYPE_COINBASE) {
        if (UPGRADE_POW) {
            $upgrade_level = $data->get_varint;
            $upgrade_level < level_by_total(MAX_VALUE)
                or return undef;
            $up = deserialize_coinbase($data, $upgrade_level) // return undef;
        }
        else {
            $up = unpack("Q<", $data->get(8) // return undef);
        }
    }
    elsif ($tx_type == TX_TYPE_UPGRADE_STOP) {
        $up = QBitcoin::Coinbase->deserialize_stop($data) // return undef;
        $upgrade_level = 0;
    }
    my $end_index = $data->index;
    $data->index = $start_index;
    my $tx_raw_data = $data->get($end_index - $start_index);
    my $hash = tx_data_hash($tx_raw_data);

    if ($end_index - $start_index > MAX_TX_SIZE) {
        Warningf("Transaction size %u exceeds maximum %u", $end_index - $start_index, MAX_TX_SIZE);
        return undef;
    }

    my $self = $class->new(
        in_raw        => \@input,
        out           => create_outputs(\@output, $hash, $tx_type == TX_TYPE_TOKENS ? $token_hash || $hash : undef),
        $up ? UPGRADE_POW ? ( up => $up, upgrade_level => $upgrade_level ) : ( coins_created => $up ) : (),
        tx_type       => $tx_type,
        $tx_type == TX_TYPE_TOKENS ? ( token_hash => $token_hash ) : (),
        $slashing ? ( slashing => $slashing ) : (),
        $down ? ( down => $down ) : (),
        hash          => $hash,
        size          => $end_index - $start_index,
        received_time => time(),
    );
    return $self;
}

sub is_pending {
    my $self = shift;
    return exists $PENDING_TX_INPUT{$self->hash};
}

sub has_pending {
    my $class = shift;
    my ($hash) = @_;
    return exists $PENDING_TX_INPUT{$hash};
}

sub load_txo {
    my $self = shift;

    # Slashing inputs are spent without a signature (no redeem_script): load them in
    # "unsigned" mode so the empty redeem_script is accepted; validate_slashing checks
    # the equivocation evidence instead of the input scripts.
    $self->load_inputs($self->is_slashing)
        or return undef; # transaction has no such output or incorrect redeem script
    $_->save foreach @{$self->out};

    if ($self->input_pending || $self->input_detached) {
        # put the transaction into separate "waiting" pull (limited size) and reprocess it by each received transaction
        foreach my $tx_in (keys %{$self->input_pending // {}}) {
            Debugf("Save transaction %s as pending for %s", $self->hash_str, $self->hash_str($tx_in));
            # request pending inputs
            if (!$PENDING_INPUT_TX{$tx_in}) {
                $self->received_from->request_tx($tx_in) if $self->received_from && $self->received_from->can('request_tx');
            }
            $PENDING_INPUT_TX{$tx_in}->{$self->hash} = $self;
        }
        foreach my $tx_in (keys %{$self->input_detached // {}}) {
            Debugf("Save transaction %s dependent on pending %s", $self->hash_str, $self->hash_str($tx_in));
            $PENDING_INPUT_TX{$tx_in}->{$self->hash} = $self;
        }
        $PENDING_TX_INPUT{$self->hash} = $self;
        foreach my $in (map { @$_ } values %{$self->input_detached // {}}) {
            $in->{txo}->spent_add($self);
        }
        $self->drop_immune = 1;
        if (keys %PENDING_TX_INPUT > MAX_PENDING_TX) {
            foreach my $pending_tx_hash (keys %PENDING_TX_INPUT) {
                my $pending_tx = $PENDING_TX_INPUT{$pending_tx_hash}
                    or next; # already dropped as dependent in this loop
                if ($pending_tx->drop()) {
                    Debugf("Drop old pending transaction %s", $pending_tx->hash_str);
                    last if keys %PENDING_TX_INPUT <= MAX_PENDING_TX;
                }
            }
        }
        delete $self->{drop_immune};
    }
    else {
        $self->calculate_fee();
    }

    foreach my $in (@{$self->in}) {
        $in->{txo}->spent_add($self);
    }

    return $self;
}

sub calculate_fee {
    my $self = shift;

    # "+0" is needed to avoid rounding float values in case of NV value exists in addition to IV
    $self->fee = $self->is_burn ? 0 :
        sum0(map { $_->{txo}->value+0 } @{$self->in}) + $self->coins_created - sum0(map { $_->value+0 } @{$self->out});
}

sub coins_created {
    my $self = shift;

    if (UPGRADE_POW) {
        return $self->up ? $self->up_value : 0;
    }
    else {
        return $self->{coins_created} // 0;
    }
}

sub create_outputs {
    my ($outputs, $hash, $token_hash) = @_;
    my @txo;
    my $num = 0;
    foreach my $out (@$outputs) {
        my $txo = QBitcoin::TXO->new_txo({
            value      => $out->{value},
            scripthash => $out->{scripthash},
            data       => $out->{data},
            tx_in      => $hash,
            num        => $num++,
            length($token_hash // "") ? ( token_hash => $token_hash ) : (),
        });
        push @txo, $txo;
    }
    return \@txo;
}

# get inputs as hashes from $self->in_raw
# and save them to $self->in and $self->input_pending
# request input_pending from remote ($self->received_from)
sub load_inputs {
    my $self = shift;
    my ($unsigned) = @_;

    my @loaded_inputs;
    my @need_load_txo;
    my %unknown_inputs;
    my %pending_inputs;
    my $inputs = delete $self->{in_raw};
    foreach my $in (@$inputs) {
        if (my $txo = QBitcoin::TXO->get($in)) {
            unless (($unsigned && $in->{redeem_script} eq "") || $txo->set_redeem_script($in->{redeem_script}) == 0) {
                Warningf("Incorrect redeem_script for input %s on %s", $txo->tx_in_str, $self->hash_str);
                return undef;
            }
            if (exists $PENDING_TX_INPUT{$in->{tx_out}}) {
                Infof("input %s:%u is pending in transaction %s",
                    $self->hash_str($in->{tx_out}), $in->{num}, $self->hash_str);
                $pending_inputs{$in->{tx_out}} //= [];
                push @{$pending_inputs{$in->{tx_out}}}, {
                    txo     => $txo,
                    siglist => $in->{siglist},
                };
            }
            else {
                push @loaded_inputs, {
                    txo     => $txo,
                    siglist => $in->{siglist},
                };
            }
        }
        else {
            push @need_load_txo, $in;
        }
    }

    if (@need_load_txo) {
        # var @txo here needed to prevent free txo objects as unused just after load
        my @txo = QBitcoin::TXO->load(@need_load_txo);
        my $class = ref $self;
        foreach my $in (@need_load_txo) {
            if (my $txo = QBitcoin::TXO->get($in)) {
                unless (($unsigned && $in->{redeem_script} eq "") || $txo->set_redeem_script($in->{redeem_script}) == 0) {
                    Warningf("Incorrect redeem_script for input %s on %s", $txo->tx_in_str, $self->hash_str);
                    return undef;
                }
                if (exists $PENDING_TX_INPUT{$in->{tx_out}}) {
                    Infof("input %s:%u is pending in transaction %s",
                        $self->hash_str($in->{tx_out}), $in->{num}, $self->hash_str);
                    $pending_inputs{$in->{tx_out}} //= [];
                    push @{$pending_inputs{$in->{tx_out}}}, {
                        txo     => $txo,
                        siglist => $in->{siglist},
                    };
                }
                else {
                    push @loaded_inputs, {
                        txo     => $txo,
                        siglist => $in->{siglist},
                    };
                }
            }
            else {
                if ($class->check_by_hash($in->{tx_out})) {
                    Warningf("Transaction %s has no output %u for tx %s input",
                        $self->hash_str($in->{tx_out}), $in->{num}, $self->hash_str);
                    return undef;
                }
                else {
                    Infof("input %s:%u not found in transaction %s",
                        $self->hash_str($in->{tx_out}), $in->{num}, $self->hash_str);
                    $unknown_inputs{$in->{tx_out}}->{$in->{num}} = $in;
                }
            }
        }
    }
    $self->in = [ sort { _cmp_inputs($a, $b) } @loaded_inputs ];
    $self->input_pending  = \%unknown_inputs if %unknown_inputs;
    $self->input_detached = \%pending_inputs if %pending_inputs;
    return $self;
}

sub _cmp_inputs {
    my ($in1, $in2) = @_;
    return $in1->{txo}->tx_in cmp $in2->{txo}->tx_in || $in1->{txo}->num <=> $in2->{txo}->num;
}

sub tx_data_hash {
    my ($tx_raw_data) = @_;
    return hash256($tx_raw_data);
}

sub calculate_hash {
    my $self = shift;
    my $tx_raw_data = $self->serialize;
    $self->size = length($tx_raw_data);
    $self->hash = tx_data_hash($tx_raw_data);
}

sub is_standard { $_[0]->{tx_type} == TX_TYPE_STANDARD }
sub is_stake    { $_[0]->{tx_type} == TX_TYPE_STAKE    }
sub is_coinbase { $_[0]->{tx_type} == TX_TYPE_COINBASE }
sub is_tokens   { $_[0]->{tx_type} == TX_TYPE_TOKENS   }
sub is_slashing { $_[0]->{tx_type} == TX_TYPE_SLASHING }
sub is_burn     { $_[0]->{tx_type} == TX_TYPE_BURN     }
sub is_downgrade { $_[0]->{tx_type} == TX_TYPE_DOWNGRADE }
sub is_upgrade_stop { $_[0]->{tx_type} == TX_TYPE_UPGRADE_STOP }

# Serialized downgrade payload placed right after tx_type: the commitment for a
# DOWNGRADE transaction, the BTC SPV proof for a BURN transaction.
sub down_payload {
    my $self = shift;
    return $self->down->serialize_commitment if $self->is_downgrade && $self->down;
    return $self->down->serialize_proof      if $self->is_burn      && $self->down;
    return "";
}

sub validate_coinbase {
    my $self = shift;
    # Each upgrade should correspond fixed and deterministic tx hash for qbitcoin
    if (@{$self->in}) {
        Warningf("Mixed input and coinbase in the transaction %s", $self->hash_str);
        return -1;
    }
    if (@{$self->out} != 1) {
        Warningf("Incorrect coinbase transaction %s: %u outputs, must be 1", $self->hash_str, scalar @{$self->out});
        return -1;
    }
    if ($self->out->[0]->data ne '') {
        Warningf("Incorrect transaction %s, coinbase can't contain data", $self->hash_str);
        return -1;
    }
    if (UPGRADE_POW) {
        if (!$self->up) {
            Warningf("Incorrect transaction %s, no coinbase information nor inputs", $self->hash_str);
            return -1;
        }
        $self->up->validate() == 0
            or return -1;
        if ($self->up->scripthash ne $self->out->[0]->scripthash) {
            Warningf("Mismatch scripthash for coinbase transaction %s", $self->hash_str);
            return -1 unless $config->{fake_coinbase};
            $self->up->scripthash = $self->out->[0]->scripthash;
        }
        if ($self->out->[0]->value != coinbase_value($self->up_value)) {
            Warningf("Mismatch value for coinbase transaction %s", $self->hash_str);
            return -1;
        }
    }
    else {
        Warningf("Coinbase denied, invalid transaction %s", $self->hash_str);
        return -1 unless $config->{fake_coinbase};
    }
    return 0;
}

# TX_TYPE_UPGRADE_STOP: SPV proof that one of UPGRADE_STOP_UTXO outputs was spent in the
# btc blockchain; once confirmed it permanently stops the btc->qbt conversion in its branch.
# The transaction is fully deterministic given the proof: no inputs, no outputs, no fee.
# The spent prevout is matched against UPGRADE_STOP_UTXO in Coinbase::deserialize_stop.
sub validate_upgrade_stop {
    my $self = shift;
    if (!UPGRADE_POW) {
        Warningf("Upgrade stop denied, invalid transaction %s", $self->hash_str);
        return -1;
    }
    if (@{$self->in} || @{$self->out}) {
        Warningf("Upgrade stop transaction %s must have no inputs and no outputs", $self->hash_str);
        return -1;
    }
    my $stop = $self->up;
    if (!$stop || !$stop->upgrade_stop) {
        Warningf("Upgrade stop transaction %s has no stop-utxo spend proof", $self->hash_str);
        return -1;
    }
    $stop->validate() == 0
        or return -1;
    return 0;
}

sub validate_hash {
    my $self = shift;

    # Do not use calculate_hash() here: it would overwrite $self->hash. That hash is the
    # identity key under which the transaction's outputs are registered in the global TXO
    # cache and its inputs are marked as spent (see load_txo). If we changed it here and the
    # transaction is then rejected, the cleanup (spent_del / weakened %TXO entries) would use
    # the new hash and fail to release the old registrations, leaking the transaction and its
    # txo (observed as "Attempt to override already loaded txo" on the next attempt).
    my $tx_raw_data = $self->serialize;
    my $hash = tx_data_hash($tx_raw_data);
    if ($hash ne $self->hash) {
        # The most common cause is non-canonical input order: inputs must be serialized
        # sorted by (tx_in, num), the same order the node uses to compute the hash.
        Warningf("Incorrect serialized transaction has different hash: %s != %s (inputs must be sorted by tx_in, num)",
            $self->hash_str($hash), $self->hash_str);
        return -1;
    }
    $self->size = length($tx_raw_data);
    return 0;
}

# GENESIS_REWARD coins are service coins: they give the chain a non-zero validation
# weight from the very first block, but they are not backed by upgraded BTC, so they
# must never enter circulation (otherwise the downgrade of all circulating coins back
# to BTC could not be guaranteed). Consensus rule: no transaction may decrease the
# balance of a genesis-reward scripthash - for each such scripthash the sum of the
# transaction's inputs must not exceed the sum of its outputs back to the same
# scripthash. Staking these coins is possible (the stake returns the full value),
# spending, downgrading or slashing them is not.
sub check_genesis_balance {
    my $self = shift;
    my $genesis = QBitcoin::Coins->genesis_scripthashes;
    return 0 unless $genesis && %$genesis;
    my %balance;
    foreach my $in (@{$self->in}) {
        my $txo = $in->{txo};
        my $scripthash = $txo->scripthash // "";
        $balance{$scripthash} += $txo->value if $genesis->{$scripthash};
    }
    %balance or return 0;
    foreach my $out (@{$self->out}) {
        my $scripthash = $out->scripthash // "";
        $balance{$scripthash} -= $out->value if exists $balance{$scripthash};
    }
    foreach my $scripthash (sort keys %balance) {
        if ($balance{$scripthash} > 0) {
            Warningf("Transaction %s decreases genesis-reward balance of scripthash %s by %lu",
                $self->hash_str, unpack("H*", $scripthash), $balance{$scripthash});
            return -1;
        }
    }
    return 0;
}

sub validate {
    my $self = shift;

    if ($self->is_coinbase) {
        return 0 if skip_scripts();
        if (UPGRADE_FINISHED) {
            Warningf("Coinbase transaction %s rejected: upgrade finished", $self->hash_str);
            return -1;
        }
        return $self->validate_coinbase;
    }
    if ($self->is_upgrade_stop) {
        return 0 if skip_scripts();
        if (UPGRADE_FINISHED) {
            Warningf("Upgrade stop transaction %s rejected: upgrade finished", $self->hash_str);
            return -1;
        }
        return $self->validate_upgrade_stop;
    }
    # The genesis-reward balance rule applies to every transaction type that spends inputs
    if (!skip_scripts()) {
        $self->check_genesis_balance == 0
            or return -1;
    }
    if ($self->is_slashing) {
        return 0 if skip_scripts();
        return $self->validate_slashing;
    }
    if ($self->is_downgrade) {
        return 0 if skip_scripts();
        return $self->validate_downgrade;
    }
    if ($self->is_burn) {
        return 0 if skip_scripts();
        return $self->validate_burn;
    }
    # Transaction must contains at least one output (can't spend all inputs as fee)
    if (!@{$self->out}) {
        Warningf("No outputs in transaction %s", $self->hash_str);
        return -1;
    }
    # Transaction must contains at least one input
    if (!@{$self->in} && !$self->is_stake) {
        Warningf("No inputs in transaction %s", $self->hash_str);
        return -1;
    }
    foreach my $out (@{$self->out}) {
        if ($out->value < 0 || $out->value > MAX_VALUE) {
            Warningf("Incorrect output value %ld in transaction %s", $out->value, $self->hash_str);
            return -1;
        }
        if (length($out->data) > MAX_TXO_DATA_SIZE) {
            Warningf("Too large output data size %u in transaction %s", length($out->data), $self->hash_str);
            return -1;
        };
        if (length($out->scripthash) > 32) {
            Warningf("Incorrect scripthash size %u in transaction %s", length($out->scripthash), $self->hash_str);
            return -1;
        }
    }
    my $class = ref $self;
    my %inputs;
    foreach my $in (@{$self->in}) {
        my $txo = $in->{txo};
        if ($inputs{$txo->key}++) {
            Warningf("Input %s:%u included in transaction %s twice",
                $txo->tx_in_str, $txo->num, $self->hash_str);
            return -1;
        }
        if (length($txo->redeem_script) > MAX_REDEEM_SCRIPT_SIZE) {
            Warningf("Too large redeem script size %u in transaction %s input %s:%u",
                length($txo->redeem_script), $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
        if (length(serialize_siglist($in->{siglist} // [])) > MAX_SIGLIST_SIZE) {
            Warningf("Too large siglist size in transaction %s input %s:%u",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
        if (($txo->scripthash // "") eq QBT_BURN_SCRIPTHASH) {
            Warningf("Non-burn transaction %s attempts to spend qbt_burn output %s:%u",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
    }
    if ($self->is_stake) {
        if ($self->fee >= 0) {
            Warningf("Fee for stake transaction %s is %li, not negative",
                $self->hash_str, $self->fee);
            return -1;
        }
        if (!skip_scripts()) {
            # A slashing refund can never be staked (see txo_stakeable)
            foreach my $in (@{$self->in}) {
                if (!$class->txo_stakeable($in->{txo})) {
                    Warningf("Stake transaction %s spends slashing refund %s:%u",
                        $self->hash_str, $in->{txo}->tx_in_str, $in->{txo}->num);
                    return -1;
                }
            }
        }
    }
    elsif ($self->is_standard || $self->is_tokens) {
        if ($self->fee < 0) {
            Warningf("Fee for standard transaction %s is %li, can't be negative",
                $self->hash_str, $self->fee);
            return -1;
        }
        if ($self->fee == 0 && $self->size > MAX_EMPTY_TX_SIZE) {
            Warningf("Transaction %s with zero fee has too large size %u, fee must be at least 1 satoshi if size > %u bytes",
                $self->hash_str, $self->size, MAX_EMPTY_TX_SIZE);
            return -1;
        }
        # Signcheck for stake transaction depends on block it relates to,
        # so skip this check while block_sign_data is not known, check from valid_for_block()
        if (!skip_scripts()) {
            $self->check_input_script == 0
                or return -1;
        }
        # Is this a token transaction?
        if ($self->is_tokens) {
            $self->validate_tokens_tx() == 0
                or return -1;
        }
    }
    else {
        Warningf("Unknown type %d for transaction %s", $self->tx_type, $self->hash_str);
        return -1;
    }
    return 0;
}

# TX_TYPE_SLASHING: trustless penalty for equivocation. Carries evidence that one
# validator signed two conflicting blocks (same stake UTXO, same timeslot); spends the
# shared UTXOs without a signature and refunds each owner its value minus SLASHING_FINE
# (the fine is the fee). The whole transaction is deterministic given the evidence, so
# we re-derive its inputs/outputs and require an exact match - any deviation is invalid.
sub validate_slashing {
    my $self = shift;

    my $evidence = $self->slashing or do {
        Warningf("Slashing transaction %s has no evidence", $self->hash_str);
        return -1;
    };
    if (!@{$self->in}) {
        Warningf("Slashing transaction %s has no inputs", $self->hash_str);
        return -1;
    }
    if (!@{$self->out}) {
        Warningf("Slashing transaction %s has no outputs", $self->hash_str);
        return -1;
    }
    my $info = $evidence->verify;
    if (!$info) {
        Warningf("Slashing transaction %s carries invalid equivocation evidence", $self->hash_str);
        return -1;
    }
    my $shared = $info->{shared};
    my %spent;
    foreach my $in (@{$self->in}) {
        my $txo = $in->{txo};
        if ($spent{$txo->key}++) {
            Warningf("Slashing transaction %s spends input %s:%u twice",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
        my $s = $shared->{$txo->key};
        if (!$s) {
            Warningf("Slashing transaction %s spends non-equivocated input %s:%u",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
        # A slashed UTXO must be a stakeable one, so a slashing refund can never be
        # slashed again. The evidence only proves double-signing, not that the signed
        # stakes could enter a valid block - so without this rule a malicious delegate
        # (whose covenant branch only allows spending into a stake) could fabricate
        # conflicting stake signatures on the refund and grind the owner's coins down
        # SLASHING_FINE at a time. Together with the stake-input rule above this makes
        # an owner's standard spend the only way out for a refund.
        if (!(ref $self)->txo_stakeable($txo)) {
            Warningf("Slashing transaction %s spends slashing refund %s:%u",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
        # The slashed UTXO must really belong to the equivocating signer: the evidence's
        # redeem_script must hash to the real UTXO's scripthash.
        if (!QBitcoin::Slashing->redeem_matches_scripthash($s->{redeem_script}, $txo->scripthash)) {
            Warningf("Slashing transaction %s input %s:%u scripthash does not match the evidence",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
        if (@{$in->{siglist} // []}) {
            Warningf("Slashing transaction %s input %s:%u must carry no signature",
                $self->hash_str, $txo->tx_in_str, $txo->num);
            return -1;
        }
    }
    # Every equivocated UTXO must be spent, except genesis-reward ones: their balance
    # may never decrease (check_genesis_balance), so the fine cannot be taken from them
    # and new_tx leaves them out. Any other omission would let an equivocator escape
    # part of the fine.
    my $genesis = QBitcoin::Coins->genesis_scripthashes // {};
    foreach my $key (sort keys %$shared) {
        next if $spent{$key};
        my $s = $shared->{$key};
        unless (grep { QBitcoin::Slashing->redeem_matches_scripthash($s->{redeem_script}, $_) } keys %$genesis) {
            Warningf("Slashing transaction %s does not spend non-genesis equivocated UTXO %s:%u",
                $self->hash_str, unpack("H*", substr($key, 0, 32)), unpack("v", substr($key, 32)));
            return -1;
        }
    }
    # Outputs must be the canonical, deterministic refund.
    my @want = QBitcoin::Slashing->canonical_outputs($self->in);
    if (@want != @{$self->out}) {
        Warningf("Slashing transaction %s has %u outputs, expected %u canonical refunds",
            $self->hash_str, scalar @{$self->out}, scalar @want);
        return -1;
    }
    for my $i (0 .. $#want) {
        if (serialize_output($self->out->[$i]) ne serialize_output($want[$i])) {
            Warningf("Slashing transaction %s output %u is not the canonical refund", $self->hash_str, $i);
            return -1;
        }
    }
    if ($self->fee <= 0) {
        Warningf("Slashing transaction %s fine (fee) %li must be positive", $self->hash_str, $self->fee);
        return -1;
    }
    return 0;
}

# TX_TYPE_DOWNGRADE: spends one freeze output (system IF branch), pins the qbtc
# branch and commits the BTC destination/amount. Output is the downgrade-tx
# reclaim script carrying the same reclaim_id, so the user can reclaim if the
# burn never appears.
sub validate_downgrade {
    my $self = shift;

    if (@{$self->in} != 1) {
        Warningf("Downgrade transaction %s must have exactly 1 input", $self->hash_str);
        return -1;
    }
    if (@{$self->out} != 1) {
        Warningf("Downgrade transaction %s must have exactly 1 output", $self->hash_str);
        return -1;
    }
    if ($self->fee != 0) {
        Warningf("Downgrade transaction %s must have zero fee (has %li)", $self->hash_str, $self->fee);
        return -1;
    }
    my $down = $self->down or do {
        Warningf("Downgrade transaction %s has no commitment", $self->hash_str);
        return -1;
    };
    my $txo = $self->in->[0]{txo};
    my $sh  = $txo->scripthash // "";
    my ($reclaim_len, $out_sh) = (QBT_RECLAIM_ID_LEN, QBT_DOWNGRADE_SCRIPTHASH);
    unless ($sh eq QBT_FREEZE_SCRIPTHASH) {
        Warningf("Downgrade transaction %s input is not a freeze output", $self->hash_str);
        return -1;
    }
    if (($down->freeze_txid // "") ne $txo->tx_in || $down->freeze_vout != $txo->num) {
        Warningf("Downgrade transaction %s source freeze commitment does not match the spent output",
            $self->hash_str);
        return -1;
    }
    my $data = $txo->data // "";
    if (length($data) < $reclaim_len) {
        Warningf("Freeze input data too short in downgrade transaction %s", $self->hash_str);
        return -1;
    }
    my $reclaim_id    = substr($data, 0, $reclaim_len);
    my $committed_spk = substr($data, $reclaim_len);
    # The committed BTC destination must be exactly the one the user put in freeze.
    if ($down->scriptpubkey ne $committed_spk) {
        Warningf("Downgrade transaction %s scriptpubkey does not match the freeze destination", $self->hash_str);
        return -1;
    }
    # Value floor: at least the level-0 (1:1) rate minus the downgrade fee.
    my $qbtc_value = $txo->value;
    my $min_btc_value = downgrade_net(downgrade_value_at_level($qbtc_value, 0));
    if ($down->btc_value < $min_btc_value) {
        Warningf("Downgrade transaction %s btc_value %lu below floor %lu",
            $self->hash_str, $down->btc_value, $min_btc_value);
        return -1;
    }
    # Output must be the downgrade-output reclaim script carrying the same
    # reclaim_id, with the full value (it is burned later, fee stays zero here).
    my $out = $self->out->[0];
    if (($out->scripthash // "") ne $out_sh) {
        Warningf("Downgrade transaction %s output is not a downgrade-output script", $self->hash_str);
        return -1;
    }
    if (($out->data // "") ne $reclaim_id) {
        Warningf("Downgrade transaction %s output data is not the reclaim_id", $self->hash_str);
        return -1;
    }
    if ($out->value != $qbtc_value) {
        Warningf("Downgrade transaction %s output value %lu != input %lu",
            $self->hash_str, $out->value, $qbtc_value);
        return -1;
    }
    # Freeze IF branch: system signature + OP_TX_TYPE == TX_TYPE_DOWNGRADE.
    $self->check_input_script == 0
        or return -1;
    return 0;
}

# TX_TYPE_BURN: spends one downgrade output (permissionless IF branch) and proves,
# via a BTC SPV proof, that the committed payment happened; finalizes the burn.
sub validate_burn {
    my $self = shift;

    if (@{$self->out}) {
        Warningf("Burn transaction %s must not contain outputs", $self->hash_str);
        return -1;
    }
    if (@{$self->in} != 1) {
        Warningf("Burn transaction %s must have exactly 1 input", $self->hash_str);
        return -1;
    }
    my $proof = $self->down or do {
        Warningf("Burn transaction %s has no SPV proof", $self->hash_str);
        return -1;
    };
    my $txo = $self->in->[0]{txo};
    my $sh  = $txo->scripthash // "";
    unless ($sh eq QBT_DOWNGRADE_SCRIPTHASH) {
        Warningf("Burn transaction %s input is not a downgrade output", $self->hash_str);
        return -1;
    }
    my $src = (ref $self)->get($txo->tx_in) // (ref $self)->find(hash => $txo->tx_in);
    unless ($src && $src->is_downgrade && $src->down) {
        Warningf("Burn transaction %s input source is not a downgrade transaction", $self->hash_str);
        return -1;
    }
    $proof->validate_spv($src->down) == 0
        or return -1;
    # Downgrade-output IF branch: permissionless, only constrains OP_TX_TYPE == BURN.
    $self->check_input_script == 0
        or return -1;
    return 0;
}

sub valid_for_block {
    my $self = shift;
    my ($block) = @_;
    if ($self->is_stake) {
        $self->block_sign_data = $block->sign_data;
        if (!skip_scripts()) {
            $self->check_input_script == 0
                or return -1;
        }
    }
    if (!skip_scripts()) {
        ( $self->min_tx_time // "Inf" ) <= timeslot($block->time)
            or return -1;
        ( $self->min_tx_block_height // "Inf" ) <= $block->height
            or return -1;
    }
    return 0;
}

sub check_input_script {
    my $self = shift;
    $self->{min_tx_time} = -1;
    $self->{min_tx_block_height} = -1;
    # Slashing inputs are spent without a signature, so there is no input script to
    # evaluate (the txo may not even have its redeem_script revealed); the equivocation
    # evidence is checked by validate_slashing instead. Reached lazily via
    # min_tx_time()/min_tx_block_height() for a mempool slashing tx.
    return 0 if $self->is_slashing;
    foreach my $num (0 .. $#{$self->in}) {
        my $in = $self->in->[$num];
        if ($in->{txo}->check_script($in->{siglist}, $self, $num) != 0) {
            Warningf("Unmatched check script for input %s:%u in transaction %s",
                $in->{txo}->tx_in_str, $in->{txo}->num, $self->hash_str);
            return -1;
        }
        # Set txo min_rel_time to STAKE_MATURITY if previous tx is stake or slashing:
        if (($self->is_standard || $self->is_tokens || $self->is_burn) && ($in->{min_rel_time} // -1) < STAKE_MATURITY) {
            my $tx_in_type = (ref $self)->type_by_hash($in->{txo}->tx_in);
            if (!defined($tx_in_type)) {
                Errf("No input transaction %s for txo", $in->{txo}->tx_in_str);
                return -1;
            }
            if ($tx_in_type == TX_TYPE_STAKE || $tx_in_type == TX_TYPE_SLASHING) {
                $in->{min_rel_time} = STAKE_MATURITY;
            }
        }
    }
    return 0;
}

sub type_by_hash {
    my $class = shift;
    my ($hash) = @_;

    if (my $tx = $class->get($hash)) {
        return $tx->tx_type;
    }
    elsif (my ($tx_hashref) = $class->fetch(hash => $hash)) {
        return $tx_hashref->{tx_type};
    }
    else {
        return undef;
    }
}

# Can this output be an input of a stake transaction? A slashing refund cannot:
# equivocation means the staking setup is broken (or a delegate is dishonest), so the
# punished coins are banned from staking and from repeated slashing (both enforced in
# validate()) until the owner moves them with a standard spend. An unknown creating tx
# passes: a real txo implies its transaction is known, only synthetic txos in tests
# resolve to undef. Memoized on the txo: the answer never changes for a given output.
sub txo_stakeable {
    my $class = shift;
    my ($txo) = @_;
    return $txo->{stakeable} //=
        ($class->type_by_hash($txo->tx_in) // 0) == TX_TYPE_SLASHING ? 0 : 1;
}

sub announce {
    my $self = shift;
    my $recv_peer = $self->received_from_peer ? $self->received_from->peer : undef;
    foreach my $connection (QBitcoin::ConnectionList->connected(PROTOCOL_QBITCOIN)) {
        next if $recv_peer && $connection->peer->id eq $recv_peer->id;
        next unless $connection->protocol->can("announce_tx");
        next unless $connection->protocol->greeted;
        $connection->protocol->announce_tx($self);
    }
}

sub pre_load {
    my $class = shift;
    my ($attr) = @_;
    if (!$TRANSACTION{$attr->{hash}}) {
        # Load TXO for inputs and outputs
        my @outputs = QBitcoin::TXO->load_stored_outputs($attr->{id}, $attr->{hash});
        $attr->{out} = \@outputs;
        if ($attr->{tx_type} == TX_TYPE_COINBASE) {
            if (UPGRADE_POW) {
                my $upgrade = QBitcoin::Coinbase->load_stored_coinbase($attr->{id}, $attr->{hash});
                if ($upgrade) {
                    $attr->{up} = $upgrade;
                    $attr->{upgrade_level} = $upgrade->upgrade_level;
                    $attr->{up_value} = $upgrade->value;
                }
                else {
                    Errf("No coinbase for transaction %s", lc unpack("H*", substr($attr->{hash}, 0, 4)));
                }
            }
            else {
                $attr->{coins_created} = sum0(map { $_->value } @outputs) + $attr->{fee};
            }
            $attr->{in} = [];
        }
        elsif ($attr->{tx_type} == TX_TYPE_UPGRADE_STOP) {
            my $upgrade = QBitcoin::Coinbase->load_stored_coinbase($attr->{id}, $attr->{hash});
            if ($upgrade) {
                $attr->{up} = $upgrade;
                $attr->{upgrade_level} = $upgrade->upgrade_level // 0;
                $attr->{up_value} = 0;
            }
            else {
                Errf("No stop-utxo spend record for transaction %s", lc unpack("H*", substr($attr->{hash}, 0, 4)));
            }
            $attr->{in} = [];
        }
        else {
            my @in_txo = QBitcoin::TXO->load_stored_inputs($attr->{id}, $attr->{hash});
            my @inputs;
            foreach my $txo (@in_txo) {
                push @inputs, {
                    txo     => $txo,
                    siglist => $txo->siglist,
                };
            }
            $attr->{in} = \@inputs;
        }
        if ($attr->{tx_type} == TX_TYPE_DOWNGRADE) {
            my ($c) = QBitcoin::Downgrade::Commitment->find(tx_id => $attr->{id});
            if (!$c) {
                Errf("No downgrade commitment for transaction %s", unpack("H*", $attr->{hash}));
                die "No downgrade commitment for transaction " . unpack("H*", $attr->{hash}) . "\n";
            }
            $attr->{down} = QBitcoin::Downgrade->new({
                freeze_txid  => $c->freeze_txid,
                freeze_vout  => $c->freeze_vout,
                btc_txid     => $c->btc_txid,
                btc_vout     => $c->btc_vout,
                btc_value    => $c->btc_value,
                scriptpubkey => $c->scriptpubkey,
            });
        }
        elsif ($attr->{tx_type} == TX_TYPE_BURN) {
            my ($sv) = QBitcoin::Downgrade::Spv->find(burn_tx_id => $attr->{id});
            if (!$sv) {
                Errf("No SPV proof for burn transaction %s", unpack("H*", $attr->{hash}));
                die "No SPV proof for burn transaction " . unpack("H*", $attr->{hash}) . "\n";
            }
            $attr->{down} = QBitcoin::Downgrade->new({
                btc_block_hash => $sv->btc_block_hash,
                btc_tx_num     => $sv->btc_tx_num,
                merkle_path    => $sv->merkle_path,
                btc_tx_data    => $sv->btc_tx_data,
            });
        }
        elsif ($attr->{tx_type} == TX_TYPE_TOKENS) {
            my $token_hash;
            if ($attr->{token_id} ) {
                my ($token_tx) = QBitcoin::Transaction->fetch(id => $attr->{token_id});
                if (!$token_tx) {
                    Errf("No token transaction id %u for transaction %s", $attr->{token_id}, unpack("H*", $attr->{hash}));
                    die "No token transaction id $attr->{token_id} for transaction " . unpack("H*", $attr->{hash}) . "\n";
                }
                $token_hash = $token_tx->{hash};
                $attr->{token_hash} = $token_tx->{hash};
            }
            else {
                $token_hash = $attr->{hash};
            }
            foreach my $out (@{$attr->{out}}) {
                $out->token_hash = $token_hash;
            }
        }
        elsif ($attr->{tx_type} == TX_TYPE_SLASHING) {
            my ($s) = QBitcoin::Slashing::Stored->find(tx_id => $attr->{id});
            if (!$s) {
                Errf("No evidence for slashing transaction %s", unpack("H*", $attr->{hash}));
                die "No evidence for slashing transaction " . unpack("H*", $attr->{hash}) . "\n";
            }
            $attr->{slashing} = QBitcoin::Slashing->from_row($s);
        }
    }
    return $attr;
}

sub new {
    my $class = shift;
    my $attr = @_ == 1 ? $_[0] : { @_ };
    # tx inputs are not sorted in the database, so sort them here for get deterministic transaction hash
    $attr->{in} = [ sort { _cmp_inputs($a, $b) } @{$attr->{in}} ];
    my $self = bless $attr, $class;
    return $self;
}

sub on_load {
    my $self = shift;

    my $hash = $self->hash;
    if ($TRANSACTION{$hash}) {
        $self = $TRANSACTION{$hash};
    }
    else {
        # Verify the integrity of the data loaded from the database. Compute the hash into a
        # local variable instead of via calculate_hash(): the latter would overwrite
        # $self->hash and $self->size, which are exactly the stored values we are validating
        # against (and which serve as identity keys elsewhere, see the note in validate_hash).
        my $tx_raw_data = $self->serialize;
        my $calc_hash = tx_data_hash($tx_raw_data);
        if ($calc_hash ne $hash) {
            Errf("Incorrect hash for loaded transaction %s != %s", $self->hash_str($calc_hash), $self->hash_str($hash));
            Errf("Serialized transaction in hex: %s", unpack("H*", $tx_raw_data));
            die "Incorrect hash for loaded transaction " . $self->hash_str($calc_hash) . " != " . $self->hash_str($hash) . "\n";
        }
        if (($self->size // -1) != length($tx_raw_data)) {
            Errf("Incorrect size for loaded transaction %s: %s != %u",
                $self->hash_str, $self->size // "undef", length($tx_raw_data));
            die "Incorrect size for loaded transaction " . $self->hash_str . ": " . ($self->size // "undef") . " != " . length($tx_raw_data) . "\n";
        }
    }

    return $self;
}

sub confirm {
    my $self = shift;
    my ($block, $pos) = @_;

    $self->block_height = $block->height;
    $self->block_pos = $pos;
    $self->block_time = $block->time;
    # The genesis stake transaction defines the genesis-reward scripthashes; make
    # them known before its outputs get their wallet-utxo roles assigned below
    QBitcoin::Coins->set_genesis_tx($self) if $block->height == 0 && $pos == 0;
    if (my $coinbase = $self->up) {
        $coinbase->tx_out = $self->hash;
        $coinbase->upgrade_level = $self->upgrade_level;
        $coinbase->value = $self->up_value;
        QBitcoin::Coins->add_coinbase($self->up_value);
    }
    elsif ($self->is_stake) {
        # block reward is the negated fee of the stake transaction; the static part of it
        # is freshly emitted coins (the dynamic part recirculates the reward fund)
        if (-$self->fee) {
            QBitcoin::Coins->add_static(QBitcoin::Block->static_reward($block->prev_block, $block->time));
        }
    }
    foreach my $in (@{$self->in}) {
        my $txo = $in->{txo};
        $txo->tx_out = $self->hash;
        $txo->siglist = $in->{siglist};
        $txo->del_my_utxo if $txo->is_my; # for stake transaction
    }
    foreach my $txo (@{$self->out}) {
        $txo->add_my_utxo if $txo->is_my && $txo->unspent;
    }
    # min proper block_height and time should be recalculated for depended transactions
    if (exists $TX_SEQ_DEPENDS{$self->hash}) {
        foreach my $tx (values %{$TX_SEQ_DEPENDS{$self->hash}}) {
            delete $tx->{min_tx_rel_time};
            delete $tx->{min_tx_rel_block_height};
        }
    }
    _mempool_remove($self);
}

sub unconfirm {
    my $self = shift;
    my ($block) = @_;
    Debugf("unconfirm transaction %s (confirmed in block height %u)", $self->hash_str, $self->block_height);
    $self->is_cached or die "unconfirm not cached transaction " . $self->hash_str;
    $self->block_height = undef;
    $self->block_time = undef;
    $self->block_pos = undef;
    foreach my $in (@{$self->in}) {
        my $txo = $in->{txo};
        $txo->tx_out = undef;
        $txo->siglist = undef;
        # TXO will be returned to list of my utxo later, on drop the branch and free the block
    }
    foreach my $txo (@{$self->out}) {
        $txo->del_my_utxo() if $txo->is_my;
    }
    if ($self->id) {
        # Transaction will be deleted by "foreign key (block_height) references block (height) on delete cascade" on replace block
        # $self->delete;
        $self->id = undef;
    }
    if (my $coinbase = $self->up) {
        $coinbase->tx_out = undef;
        QBitcoin::Coins->del_coinbase($self->up_value);
    }
    elsif ($self->is_stake && $block) {
        if (-$self->fee) {
            QBitcoin::Coins->del_static(QBitcoin::Block->static_reward($block->prev_block, $block->time));
        }
    }
    # dependent transactions with seq limits should not be confirmed
    if (exists $TX_SEQ_DEPENDS{$self->hash}) {
        foreach my $tx (values %{$TX_SEQ_DEPENDS{$self->hash}}) {
            $tx->{min_tx_rel_time} = undef if exists $tx->{min_tx_rel_time};
            $tx->{min_tx_rel_block_height} = undef if exists $tx->{min_tx_rel_block_height};
        }
    }
    _mempool_insert($self);
}

sub is_cached {
    my $self = shift;

    return exists($TRANSACTION{$self->hash}) && refaddr($TRANSACTION{$self->hash}) == refaddr($self);
}

sub txo_height {
    my $class = shift;
    my ($txo) = @_;
    my $block_time;
    my $block_height;
    if (my $tx_in = $class->get($txo->tx_in)) {
        # block_time may be differ from the time of the best block with block_height if we're checking alternative branch
        $block_time = $tx_in->block_time;
        if (!defined($block_height = $tx_in->block_height)) {
            return undef;
        }
    }
    elsif (my ($tx_hashref) = $class->fetch(hash => $txo->tx_in)) {
        $block_height = $tx_hashref->{block_height};
    }
    else {
        # tx generated this txo should be loaded during tx validation
        Errf("No input transaction %s for txo", $txo->tx_in_str);
        return undef;
    }
    return wantarray ? ($block_height, $block_time) : $block_height;
}

sub txo_time {
    my $class = shift;
    my ($txo) = @_;

    my ($block_height, $block_time) = $class->txo_height($txo);
    if (!$block_time) {
        defined($block_height) or return undef;
        # The transaction was loaded from the database, so we can get block time from the blockchain
        my $block = QBitcoin::Block->best_block($block_height) // QBitcoin::Block->find(height => $block_height)
            or die "Can't find block height $block_height\n";
        $block_time = $block->time;
    }
    return $block_time;
}

sub stake_weight {
    my $self = shift;
    my ($block) = @_;
    my $weight = 0;
    if ($self->is_stake) {
        my $class = ref $self;
        foreach my $in (map { $_->{txo} } @{$self->in}) {
            my $in_block_time = $class->txo_time($in);
            if (!defined($in_block_time)) {
                Warningf("Can't get stake_weight for %s with unconfirmed input %s:%u",
                    $self->hash_str, $in->tx_in_str, $in->num);
                return undef;
            }
            my $value = $in->value; # prevent convertion to float
            $weight += $value * ((timeslot($block->time) - timeslot($in_block_time)) / BLOCK_INTERVAL);
        }
        # Prevent int64 overflow, too large weight will not give more advantage, so just set it to maximum value
        $weight = MAX_INT64 if $weight > MAX_INT64;
    }
    return int($weight / 0x10000); # prevent int64 overflow for total blockchain weight
}

# Weight a slashing transaction contributes to its block: the slashed stake UTXOs are
# aged exactly like stake inputs (value * slot-age), so a slashing tx carries enough
# weight to outweigh the equivocating stake it replaces. Returns undef if an input is
# not yet confirmed (block self_weight is then recomputed later).
sub slashing_weight {
    my $self = shift;
    my ($block_time) = @_;
    my $weight = 0;
    my $class = ref $self;
    foreach my $in (map { $_->{txo} } @{$self->in}) {
        my $in_block_time = $class->txo_time($in);
        if (!defined($in_block_time)) {
            Warningf("Can't get slashing_weight for %s with unconfirmed input %s:%u",
                $self->hash_str, $in->tx_in_str, $in->num);
            return undef;
        }
        $weight += $in->value * ((timeslot($block_time) - timeslot($in_block_time)) / BLOCK_INTERVAL);
    }
    $weight = MAX_INT64 if $weight > MAX_INT64;
    return int($weight / 0x10000); # prevent int64 overflow for total blockchain weight
}

sub coinbase_weight {
    my $self = shift;
    my ($block_time) = @_;
    my $weight = 0;
    if (my $coinbase = $self->up) {
        # Early confirmation should have more weight than later
        my $confirm_time = $coinbase->btc_confirm_time // return 0;
        my $base_time = timeslot($confirm_time);
        my $virtual_time = timeslot($confirm_time - COINBASE_WEIGHT_TIME); # MB negative, it's ok
        $weight = $self->up_value * ($base_time - $virtual_time) / BLOCK_INTERVAL;
        $weight *= ($base_time - $virtual_time) / (timeslot($block_time) - $virtual_time);
        $weight = MAX_INT64 if $weight > MAX_INT64;
    }
    return int($weight / 0x10000); # prevent int64 overflow for total blockchain weight
}

sub up_value {
    my $self = shift;
    return $self->{up_value} //= $self->up->get_value($self->upgrade_level);
}

sub coinbase_value {
    my ($value) = @_;
    state $permil = 1000 - int(UPGRADE_FEE * 1000);
    use integer;
    return $value * $permil / 1000;
}

# Create a transaction with already exising coinbase output
sub new_coinbase {
    my $class = shift;
    my ($coinbase, $upgrade_level) = @_;

    my $value = $coinbase->get_value($upgrade_level);
    my $txo = QBitcoin::TXO->new_txo({
        value      => coinbase_value($value),
        scripthash => $coinbase->scripthash,
    });
    my $self = $class->new(
        in            => [],
        out           => [ $txo ],
        up            => $coinbase,
        tx_type       => TX_TYPE_COINBASE,
        fee           => $value - $txo->value,
        received_time => time(),
        upgrade_level => $upgrade_level,
    );
    $self->calculate_hash;
    if (my $cached = $class->get($self->hash)) {
        Debugf("Coinbase transaction %s for btc %s:%u already in %s",
            $self->hash_str, $class->hash_str($coinbase->btc_tx_hash), $coinbase->btc_out_num,
            $cached->block_height ? "blockchain" : "mempool");
        $self = $cached;
    }
    else {
        QBitcoin::TXO->save_all($self->hash, $self->out);
        $self->save(); # Add coinbase tx to mempool
        Infof("Generated new coinbase transaction %s for btc output %s:%u value %lu fee %lu",
            $self->hash_str, $class->hash_str($coinbase->btc_tx_hash), $coinbase->btc_out_num,
            $txo->value, $self->fee);
        $self->announce();
    }
    return $self;
}

# The upgrade stop transaction weighs as much as a coinbase upgrading the whole
# remaining conversion quota (UPGRADE_MAX_VALUE - upgraded before the marker).
# This makes suppressing the marker unprofitable: a branch which omits it and keeps
# including post-spend coinbases can never extract more upgrade weight than the
# honest branch gets from the marker alone.
sub upgrade_stop_weight {
    my $self = shift;
    my ($block_time, $upgraded) = @_;
    my $stop = $self->up
        or return 0;
    my $remaining_btc = UPGRADE_MAX_VALUE > $upgraded ? UPGRADE_MAX_VALUE - $upgraded : 0;
    my $virtual_value = upgrade_value($remaining_btc, level_by_total($upgraded));
    my $confirm_time = $stop->btc_confirm_time // return 0;
    my $base_time = timeslot($confirm_time);
    my $virtual_time = timeslot($confirm_time - COINBASE_WEIGHT_TIME); # MB negative, it's ok
    # Scale down by 0x10000 before applying the time factors: the whole remaining quota
    # at once would hit the MAX_INT64 clamp of the coinbase_weight() formula, and then
    # many small coinbases would outweigh the single marker
    my $weight = $virtual_value / 0x10000 * ($base_time - $virtual_time) / BLOCK_INTERVAL;
    $weight *= ($base_time - $virtual_time) / (timeslot($block_time) - $virtual_time);
    $weight = MAX_INT64 if $weight > MAX_INT64;
    return int($weight);
}

# Create a transaction for a stored stop-utxo spend record
sub new_upgrade_stop {
    my $class = shift;
    my ($stop) = @_;

    my $self = $class->new(
        in            => [],
        out           => [],
        up            => $stop,
        tx_type       => TX_TYPE_UPGRADE_STOP,
        fee           => 0,
        received_time => time(),
        upgrade_level => 0,
    );
    $self->calculate_hash;
    if (my $cached = $class->get($self->hash)) {
        Debugf("Upgrade stop transaction %s for btc tx %s already in %s",
            $self->hash_str, $class->hash_str($stop->btc_tx_hash),
            $cached->block_height ? "blockchain" : "mempool");
        $self = $cached;
    }
    else {
        $self->save(); # Add upgrade stop tx to mempool
        Warningf("Generated upgrade stop transaction %s for btc tx %s: btc->qbt conversion stops",
            $self->hash_str, $class->hash_str($stop->btc_tx_hash));
        $self->announce();
    }
    return $self;
}

sub burn_weight {
    my $self = shift;
    my ($block_time) = @_;
    my $weight = 0;
    my $class = ref $self;
    foreach my $in (map { $_->{txo} } @{$self->in}) {
        my $in_block_time = $class->txo_time($in);
        if (!defined($in_block_time)) {
            Errf("Can't get burn_weight for %s with unconfirmed input %s:%u",
                $self->hash_str, $in->tx_in_str, $in->num);
            die "Burn transaction " . $self->hash_str . " has unconfirmed input " . $in->tx_in_str . ":" . $in->num . "\n";
        }
        # Weight of burn transaction does not depend on the age
        # to avoid reorg if the burn transaction is included in the block with old block time or with later block
        $weight += $in->value * (QBT_BURN_VIRT_AGE / BLOCK_INTERVAL);
    }
    return int($weight / 0x10000); # prevent int64 overflow for total blockchain weight
}

# A downgrade transaction pins the qbtc branch before the BTC payment confirms, so
# it must carry the same heavy, age-independent weight as a burn: this is what makes
# rolling back a freeze (after the BTC was paid) economically infeasible.
sub downgrade_weight {
    my $self = shift;
    my $class = ref $self;
    my $weight = 0;
    foreach my $in (map { $_->{txo} } @{$self->in}) {
        my $in_block_time = $class->txo_time($in);
        if (!defined($in_block_time)) {
            Errf("Can't get downgrade_weight for %s with unconfirmed input %s:%u",
                $self->hash_str, $in->tx_in_str, $in->num);
            die "Downgrade transaction " . $self->hash_str . " has unconfirmed input " . $in->tx_in_str . ":" . $in->num . "\n";
        }
        $weight += $in->value * (QBT_BURN_VIRT_AGE / BLOCK_INTERVAL);
    }
    return int($weight / 0x10000); # prevent int64 overflow for total blockchain weight
}

# $self->{min_tx_time}, $self->{min_tx_block_height}: minimal time and block_height for transaction set by checklocktimeverify opcode
# Set to -1 if unlimited (default), undef if unknown (loaded from database, need to check)
# $self->{min_tx_rel_time}, $self->{min_tx_rel_block_height}: minimal time and block_height for transaction
# These set by both checklocktimeverify and checksequenceverify opcode to minimum block height and time of all inputs
# Methods min_tx_time and min_tx_block_height cache calculated values to $self->{min_tx_rel_time} and $self->{min_tx_rel_block_height} keys
# Set these values to undef means that dependent transaction is not confirmed, so the transaction should not be confirmed too
# Delete these values means these values are unknown and should be recalculated in input scripts

# For standard transaction this can be set by check_input_script() if it execute "checklocktimeverify" opcode
sub set_min_tx_time {
    my $self = shift;
    my ($val) = @_;

    if (defined($self->{min_tx_time}) && $self->{min_tx_time} < $val) {
        $self->{min_tx_time} = $val;
    }
}

sub min_tx_time {
    my $self = shift;

    if ($self->up) {
        return $self->up->min_tx_time;
    }
    if (exists $self->{min_tx_rel_time}) {
        return $self->{min_tx_rel_time};
    }
    if (!exists $self->{min_tx_time}) {
        # min_tx_time may be unknown if the transaction was loaded from database
        # and then unconfirmed (moved to mempool)
        $self->check_input_script;
    }
    my $min_tx_time = $self->{min_tx_time};
    foreach my $in (@{$self->in}) {
        my $min_rel_time = $in->{min_rel_time}
            or next;
        my $txo = $in->{txo};
        # reset $self->{min_tx_rel_time} if previous tx confirmed or unconfirmed
        $TX_SEQ_DEPENDS{$txo->tx_in}->{$self->hash} = $self if $self->is_cached;
        my $txo_time = QBitcoin::Transaction->txo_time($txo);
        if (defined($txo_time)) {
            $min_tx_time = $min_rel_time + $txo_time if defined($min_tx_time) && $min_tx_time < $min_rel_time + $txo_time;
        }
        else {
            $min_tx_time = undef;
        }
    }
    return $self->{min_tx_rel_time} = $min_tx_time;
}

sub set_min_tx_block_height {
    my $self = shift;
    my ($val) = @_;

    if (defined($self->{min_tx_block_height}) && $self->{min_tx_block_height} < $val) {
        $self->{min_tx_block_height} = $val;
    }
}

sub min_tx_block_height {
    my $self = shift;

    if ($self->up) {
        return -1;
    }
    if (exists $self->{min_tx_rel_block_height}) {
        return $self->{min_tx_rel_block_height};
    }
    if (!exists $self->{min_tx_block_height}) {
        # min_tx_block_height may be unknown if the transaction was loaded from database
        # and then unconfirmed (moved to mempool)
        $self->check_input_script;
    }
    my $min_tx_height = $self->{min_tx_block_height};
    foreach my $in (@{$self->in}) {
        my $min_rel_height = $in->{min_rel_block_height}
            or next;
        my $txo = $in->{txo};
        $TX_SEQ_DEPENDS{$txo->tx_in}->{$self->hash} = $self; # reset $self->{min_tx_rel_block_height} if previous tx confirmed or unconfirmed
        my $txo_height = QBitcoin::Transaction->txo_height($txo);
        if (defined($txo_height)) {
            $min_tx_height = $min_rel_height + $txo_height if defined($min_tx_height) && $min_tx_height < $min_rel_height + $txo_height;
        }
        else {
            $min_tx_height = undef;
        }
    }
    return $self->{min_tx_rel_block_height} = $min_tx_height;
}

sub drop_all_pending {
    my $class = shift;
    my ($connection) = @_;

    foreach my $tx_hash (keys %PENDING_TX_INPUT) {
        my $tx = $PENDING_TX_INPUT{$tx_hash}
            or next;
        if ($tx->received_from_peer && $tx->received_from->peer->id eq $connection->peer->id) {
            $tx->drop();
        }
    }
}

sub compare_tx {
    # coinbase first
    my ($a, $b) = @_;
    return
        $b->fee * $a->size <=> $a->fee * $b->size ||
        ($a->received_time // 0) <=> ($b->received_time // 0) ||
        $a->hash cmp $b->hash;
}

use constant DESC_PACKAGE_TX => 64; # bound for the descendant walk, as MAX_PACKAGE_TX in QBitcoin::Mempool

# Cumulative fee and size of the transaction with its unconfirmed descendants;
# the walk is bounded, a partial aggregate is enough for eviction ordering
sub _desc_aggregate {
    my ($tx) = @_;
    my ($fee, $size, $count) = (0, 0, 0);
    my %seen = ($tx->hash => 1);
    my @stack = ($tx);
    while (defined(my $t = pop @stack)) {
        $fee  += $t->fee;
        $size += $t->size;
        last if ++$count > DESC_PACKAGE_TX;
        foreach my $out (@{$t->out}) {
            foreach my $sp ($out->spent_list) {
                next if defined $sp->block_height;
                next if $seen{$sp->hash}++;
                push @stack, $sp;
            }
        }
    }
    return ($fee, $size);
}

# worst first: lowest feerate, then latest received
sub _cmp_evict {
    my ($sa, $ta, $sb, $tb) = @_;
    return
        $sa->[0] * $sb->[1] <=> $sb->[0] * $sa->[1] ||
        ($tb->received_time // 0) <=> ($ta->received_time // 0) ||
        $ta->hash cmp $tb->hash;
}

sub want_tx {
    my ($tx) = @_;

    # Coinbase, burn, and stake transactions are never limited
    return 1 unless $tx->is_mempool_limited;

    # Reject zero-fee tx if limit reached
    if ($tx->fee == 0 && $MEMPOOL_ZERO_FEE_COUNT >= MAX_MEMPOOL_ZERO_FEE_TX) {
        return 0;
    }

    # Reject if mempool over size limit and this tx is not better than worst evictable.
    # Admission compares plain feerates (an incoming tx has no descendants yet, and the
    # worst tx is tracked cheaply); eviction ranks by descendant score, see evict_mempool
    if ($MEMPOOL_SIZE + $tx->size > MAX_MEMPOOL_SIZE) {
        return 0 if $tx->fee == 0;
        my $worst = mempool_worst_tx();
        if (!$worst) {
            Errf("Mempool over size limit but no evictable transaction found, rejecting transaction %s fee %li size %u",
                $tx->hash_str, $tx->fee, $tx->size);
            return 0;
        }
        # Accept only if strictly better than worst
        return compare_tx($tx, $worst) < 0 ? 1 : 0;
    }

    return 1;
}

sub evict_mempool {
    my @mempool =
        grep { !defined($_->block_height)
               && $_->is_mempool_limited
               && !$_->in_blocks
               && !$_->drop_immune
               && !($_->received_from_peer && $_->received_from->syncing)
        } values %TRANSACTION;
    # Order by descendant score: the rate of the transaction alone or with all its
    # unconfirmed descendants, whichever is better. A cheap parent paid for by its
    # descendants (CPFP) is evicted after single transactions with a lower cumulative
    # rate; drop() cascades to the descendants, so the whole chain goes together.
    my %score;
    foreach my $tx (@mempool) {
        my ($desc_fee, $desc_size) = _desc_aggregate($tx);
        $score{$tx->hash} = $tx->fee * $desc_size >= $desc_fee * $tx->size
            ? [ $tx->fee, $tx->size ]
            : [ $desc_fee, $desc_size ];
    }
    foreach my $tx (sort { _cmp_evict($score{$a->hash}, $a, $score{$b->hash}, $b) } @mempool) {
        last if $MEMPOOL_SIZE <= MAX_MEMPOOL_SIZE && $MEMPOOL_ZERO_FEE_COUNT <= MAX_MEMPOOL_ZERO_FEE_TX;
        next unless $MEMPOOL_SIZE > MAX_MEMPOOL_SIZE || ($MEMPOOL_ZERO_FEE_COUNT > MAX_MEMPOOL_ZERO_FEE_TX && $tx->fee == 0);
        next unless $TRANSACTION{$tx->hash}; # already dropped with an evicted ancestor
        Infof("Evict tx %s fee %li size %u from mempool", $tx->hash_str, $tx->fee, $tx->size);
        $tx->drop();
    }
}

1;
