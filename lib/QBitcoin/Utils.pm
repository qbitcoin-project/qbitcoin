package QBitcoin::Utils;
use warnings;
use strict;
use feature 'state';

# Utility functions for QBitcoin REST and RPC interfaces

use Exporter qw(import);
our @EXPORT_OK = qw(
    get_address_txs
    get_address_utxo
    address_received
    address_balance
    address_stats
    tokens_received
    tokens_balance
    all_tokens_balance
    get_tokens_txs
    get_tokens_info
    create_txo
    estimate_fees
    check_tx_tokens_balance
);

use List::Util qw(sum0);
use QBitcoin::Log;
use QBitcoin::Const;
use QBitcoin::BlockchainParams;
use QBitcoin::ORM qw(dbh DEBUG_ORM);
use QBitcoin::Address qw(scripthash_by_address);
use QBitcoin::Crypto qw(hash160);
use QBitcoin::RedeemScript;
use QBitcoin::TXO;
use QBitcoin::Transaction;
use QBitcoin::Block;
use QBitcoin::MinFee qw(MIN_FEE);
use QBitcoin::ProtocolState qw(blockchain_synced mempool_synced);
use Bitcoin::Address qw(is_btc_address btc_address_to_scriptpubkey);

use constant MAX_TXO_PER_ADDRESS => 10_000;

my $SQL_IS_TOKEN_TRANSFER = "LENGTH(data) = 9 AND SUBSTR(data, 1, 1) = UNHEX('" . unpack("H2", TOKEN_TXO_TYPE_TRANSFER) . "')";

# returns list of arrays [ txid, value, block_height ] for blockchain and [ txid, value, received_time ] for mempool
sub get_address_txs {
    my ($address, $last_seen, $chain_limit, $mempool_limit) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return ();
    $chain_limit //= MAX_TXO_PER_ADDRESS;
    $mempool_limit //= MAX_TXO_PER_ADDRESS;
    my @txs_chain;

    my ($skip_before, $skip_before_id);
    my $script = QBitcoin::RedeemScript->find(hash => $scripthash);
    if ($last_seen) {
        if ($skip_before = QBitcoin::Transaction->get($last_seen)) {
            $skip_before_id = $skip_before->id; # undef if not in DB
        }
        elsif (($skip_before) = QBitcoin::Transaction->fetch(hash => $last_seen)) {
            $skip_before_id = $skip_before->{id};
        }
    }

    my @txs_inmem;
    if (!$skip_before_id) {
        my $in_skip = $skip_before ? 1 : 0;
        for (my $height = QBitcoin::Block->blockchain_height // -1; $height > QBitcoin::Block->max_db_height; $height--) {
            my $block = QBitcoin::Block->best_block($height)
                or next;
            foreach my $tx (reverse @{$block->transactions}) {
                if ($in_skip) {
                    if ($tx->hash eq $last_seen) {
                        $in_skip = 0;
                    }
                    next;
                }
                my $tx_data;
                for (my $num = 0; $num < @{$tx->out}; $num++) {
                    my $out = $tx->out->[$num];
                    next if $out->scripthash ne $scripthash;
                    if ($tx_data) {
                        $tx_data->[1] += $out->value;
                    }
                    else {
                        $tx_data = [ $tx->hash, $out->value, $height, $tx->block_pos ];
                    }
                }
                foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
                    if ($tx_data) {
                        $tx_data->[1] -= $in->{txo}->value;
                    }
                    else {
                        $tx_data = [ $tx->hash, -$in->{txo}->value, $height, $tx->block_pos ];
                    }
                }
                push @txs_inmem, $tx_data if $tx_data;
            }
        }
    }

    if (@txs_inmem < $chain_limit && $script) {
        my @txs_in = dbh->selectall_array("SELECT hash, amount, block_height, block_pos FROM `" . QBitcoin::Transaction->TABLE . "` tx JOIN (SELECT tx_in, SUM(value) AS amount FROM `" . QBitcoin::TXO->TABLE . "` txo WHERE scripthash = ? AND (? IS NULL OR tx_in < ?) GROUP BY tx_in ORDER BY tx_in DESC LIMIT ?) AS t ON (tx_in = id)", undef, $script->id, $skip_before_id, $skip_before_id, $chain_limit - @txs_inmem);
        my @txs_out = dbh->selectall_array("SELECT hash, amount, block_height, block_pos FROM `" . QBitcoin::Transaction->TABLE . "` tx JOIN (SELECT tx_out, -SUM(value) AS amount FROM `" . QBitcoin::TXO->TABLE . "` txo WHERE scripthash = ? AND tx_out IS NOT NULL AND (? IS NULL OR tx_out < ?) GROUP BY tx_out ORDER BY tx_out DESC LIMIT ?) AS t ON (tx_out = id)", undef, $script->id, $skip_before_id, $skip_before_id, $chain_limit - @txs_inmem);
        my %txs_in = map { $_->[0] => $_ } @txs_in;
        foreach my $tx (@txs_out) {
            if (exists $txs_in{$tx->[0]}) {
                $txs_in{$tx->[0]}->[1] += $tx->[1];
            }
            else {
                push @txs_in, $tx;
            }
        }
        undef @txs_out;
        undef %txs_in;
        # $_->[1]+0: SUM() amounts are fetched as strings, numify for JSON encoding
        @txs_chain = map [ $_->[0], $_->[1]+0, $_->[2] ],
            sort { $b->[2] <=> $a->[2] || $b->[3] <=> $a->[3] }
                @txs_inmem, @txs_in;
    }
    else {
        @txs_chain = map [ $_->[0], $_->[1], $_->[2] ], @txs_inmem;
    }
    splice @txs_chain, $chain_limit if @txs_chain > $chain_limit;
    undef @txs_inmem;

    my @txs_mempool;
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        my $tx_data;
        for (my $num = 0; $num < @{$tx->out}; $num++) {
            my $out = $tx->out->[$num];
            next if $out->scripthash ne $scripthash;
            if ($tx_data) {
                $tx_data->[1] += $out->value;
            }
            else {
                $tx_data = [ $tx->hash, $out->value, $tx->received_time ];
            }
        }
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
            if ($tx_data) {
                $tx_data->[1] -= $in->{txo}->value;
            }
            else {
                $tx_data = [ $tx->hash, -$in->{txo}->value, $tx->received_time ];
            }
        }
        push @txs_mempool, $tx_data if $tx_data;
        last if @txs_mempool == $mempool_limit;
    }

    return (\@txs_chain, \@txs_mempool);
}

sub address_stats {
    my ($address) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return undef;
    my ($funded_sum, $funded_cnt, $spent_sum, $spent_cnt, $tx_cnt) = (0,0,0,0,0);
    my $max_db_height = QBitcoin::Block->max_db_height;
    my $blockchain_height = QBitcoin::Block->blockchain_height // -1;
    if (my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) {
        my $sql = "SELECT IFNULL(SUM(value), 0), COUNT(*), IFNULL(SUM(CASE WHEN tx_out IS NULL THEN 0 ELSE value END), 0), COUNT(tx_out), COUNT(DISTINCT tx_in)+COUNT(DISTINCT tx_out) FROM `" . QBitcoin::TXO->TABLE . "` WHERE scripthash = ?";
        my ($result) = dbh->selectall_array($sql, undef, $script->id);
        # SUM() results come from the driver as strings (DECIMAL type); numify them
        # so JSON encoders render the stats as numbers
        ($funded_sum, $funded_cnt, $spent_sum, $spent_cnt, $tx_cnt) = map { $_ + 0 } @$result;
        # Calculate transactions with both inputs and outputs to this address
        my ($res2) = dbh->selectall_array("SELECT COUNT(DISTINCT t1.tx_in) FROM `" . QBitcoin::TXO->TABLE . "` t1 JOIN `" . QBitcoin::TXO->TABLE . "` t2 ON (t1.tx_in = t2.tx_out AND t1.scripthash = t2.scripthash) WHERE t1.scripthash = ?", undef, $script->id);
        $tx_cnt -= $res2->[0];
    }
    for (my $height = $max_db_height + 1; $height <= $blockchain_height; $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (@{$block->transactions}) {
            my $tx_involved = 0;
            foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
                $spent_sum += $in->{txo}->value;
                $spent_cnt++;
                $tx_involved = 1;
            }
            foreach my $out (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
                $funded_sum += $out->value;
                $funded_cnt++;
                $tx_involved = 1;
            }
            $tx_cnt++ if $tx_involved;
        }
    }
    my ($mempool_funded_sum, $mempool_funded_cnt, $mempool_spent_sum, $mempool_spent_cnt, $mempool_tx_cnt) = (0,0,0,0,0);
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        my $tx_involved = 0;
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
            $mempool_spent_sum += $in->{txo}->value;
            $mempool_spent_cnt++;
            $tx_involved = 1;
        }
        foreach my $out (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
            $mempool_funded_sum += $out->value;
            $mempool_funded_cnt++;
            $tx_involved = 1;
        }
        $mempool_tx_cnt++ if $tx_involved;
    }

    return {
        chain_stats   => {
            funded_txo_sum   => $funded_sum,
            funded_txo_count => $funded_cnt,
            spent_txo_sum    => $spent_sum,
            spent_txo_count  => $spent_cnt,
            tx_count         => $tx_cnt,
        },
        mempool_stats => {
            funded_txo_sum   => $mempool_funded_sum,
            funded_txo_count => $mempool_funded_cnt,
            spent_txo_sum    => $mempool_spent_sum,
            spent_txo_count  => $mempool_spent_cnt,
            tx_count         => $mempool_tx_cnt,
        },
    };
}

sub address_received {
    my ($address, $minconf) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return undef;
    my $value = 0;
    my $max_db_height = QBitcoin::Block->max_db_height;
    my $blockchain_height = QBitcoin::Block->blockchain_height // -1;
    if (my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) {
        my $result;
        if ($minconf && $blockchain_height - $minconf + 1 < $max_db_height) {
            my $sql = "SELECT SUM(value) FROM `" . QBitcoin::TXO->TABLE . "` JOIN `" . QBitcoin::Transaction->TABLE . "` tx_in ON (tx_in = tx_in.id) WHERE scripthash = ? AND tx_in.block_height <= ?";
            ($result) = dbh->selectall_array($sql, undef, $script->id, $blockchain_height - $minconf + 1);
        }
        else {
            my $sql = "SELECT SUM(value) FROM `" . QBitcoin::TXO->TABLE . "` WHERE scripthash = ?";
            ($result) = dbh->selectall_array($sql, undef, $script->id);
        }
        $value = $result->[0] // 0;
        $value += 0; # SUM() is fetched as a string, numify for JSON encoding
    }
    if ($minconf && $blockchain_height - $minconf + 1 <= $max_db_height) {
        return $value;
    }

    for (my $height = $max_db_height + 1; $height <= $blockchain_height - $minconf + 1; $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (@{$block->transactions}) {
            $value += sum0 map { $_->value } grep { $_->scripthash eq $scripthash } @{$tx->out};
        }
    }
    if (!$minconf) {
        foreach my $tx (QBitcoin::Transaction->mempool_list()) {
            $value += sum0 map { $_->value } grep { $_->scripthash eq $scripthash } @{$tx->out};
        }
    }

    return $value;
}

sub address_balance {
    my ($address, $minconf) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return undef;
    my $value = 0;
    my $max_db_height = QBitcoin::Block->max_db_height;
    my $blockchain_height = QBitcoin::Block->blockchain_height // -1;
    my %fresh_inputs;
    if (my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) {
        my $result;
        if ($minconf && $blockchain_height - $minconf + 1 < $max_db_height) {
            my ($last_tx) = QBitcoin::Transaction->fetch(block_height => { '<=', $blockchain_height - $minconf + 1 }, -sortby => 'id DESC', -limit => 1);
            if (defined $last_tx) {
                my $sql = "SELECT SUM(value) FROM `" . QBitcoin::TXO->TABLE . "` WHERE tx_out IS NULL AND scripthash = ? AND tx_in <= ?";
                ($result) = dbh->selectall_array($sql, undef, $script->id, $last_tx->{id});
                # Store inputs that have not enough confirmations to exclude them later
                my $fresh_inputs_sql = "SELECT hash FROM `" . QBitcoin::TXO->TABLE . "` JOIN `" . QBitcoin::Transaction->TABLE . "` ON (id = tx_in) WHERE scripthash = ? AND tx_out IS NULL AND tx_in > ?";
                %fresh_inputs = map { $_->[0] => 1 } dbh->selectall_array($fresh_inputs_sql, undef, $script->id, $last_tx->{id});
            }
            else {
                $result = [0];
            }
        }
        else {
            my $sql = "SELECT SUM(value) FROM `" . QBitcoin::TXO->TABLE . "` WHERE tx_out IS NULL and scripthash = ?";
            ($result) = dbh->selectall_array($sql, undef, $script->id);
        }
        $value = $result->[0] // 0;
        $value += 0; # SUM() is fetched as a string, numify for JSON encoding
    }

    for (my $height = $max_db_height + 1; $height <= $blockchain_height; $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (@{$block->transactions}) {
            foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
                next if exists $fresh_inputs{$in->{txo}->tx_in};
                my $first_spent = $in->{txo}->tx_out;
                ($first_spent) = sort { $a cmp $b } map { $_->hash } $in->{txo}->spent_list unless $first_spent;
                next if $first_spent && $first_spent ne $tx->hash;
                $value -= $in->{txo}->value;
            }
            if (!$minconf || $height <= $blockchain_height - $minconf + 1) {
                foreach my $out (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
                    $value += $out->value;
                }
            }
            elsif (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
                $fresh_inputs{$tx->hash} = 1;
            }
        }
    }

    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        if (!$minconf) {
            foreach my $out (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
                $value += $out->value;
            }
        }
        elsif (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
            $fresh_inputs{$tx->hash} = 1;
        }
    }
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
            next if exists $fresh_inputs{$in->{txo}->tx_in};
            my $first_spent = $in->{txo}->tx_out;
            ($first_spent) = sort { $a cmp $b } map { $_->hash } $in->{txo}->spent_list unless $first_spent;
            next if $first_spent && $first_spent ne $tx->hash;
            $value -= $in->{txo}->value;
        }
    }

    return $value;
}

sub _add_token_data {
    my ($utxo, $data) = @_;
    return if !defined($data);
    if (length($data) == 9 && substr($data, 0, 1) eq TOKEN_TXO_TYPE_TRANSFER) {
        $utxo->{token_amount} = unpack("Q<", substr($data, 1, 8));
    }
    elsif (length($data) == 2 && substr($data, 0, 1) eq TOKEN_TXO_TYPE_PERMISSIONS) {
        my $permissions = unpack("C", substr($data, 1, 1));
        my @perms;
        push (@perms, 'mint') if $permissions & TOKEN_PERMISSION_MINT;
        $utxo->{token_permissions} = \@perms;
    }
}

sub get_address_utxo {
    my ($address, $limit) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return ();
    my %txo_chain;
    my $txo_cnt = 0;
    if (my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) {
        my %token_hash;
        foreach my $txo (dbh->selectall_array("SELECT tx_in.hash, num, value, tx_in.block_height, tx_in.block_pos, tx_in.tx_type, tx_in.id, tx_in.token_id, data FROM `" . QBitcoin::TXO->TABLE . "` JOIN `" . QBitcoin::Transaction->TABLE . "` tx_in ON (tx_in = tx_in.id) WHERE tx_out IS NULL AND scripthash = ? ORDER BY tx_in.block_height DESC, tx_in.block_pos DESC LIMIT ?", undef, $script->id, $limit // MAX_TXO_PER_ADDRESS)) {
            my $utxo = {
                value        => $txo->[2],
                block_height => $txo->[3],
                block_pos    => $txo->[4],
                tx_type      => TX_TYPES_NAMES->[$txo->[5]],
            };
            if ($txo->[5] == TX_TYPE_TOKENS) {
                if ($txo->[7]) {
                    if (!exists $token_hash{$txo->[7]}) {
                        my ($token_tx) = QBitcoin::Transaction->fetch(id => $txo->[7]);
                        $token_hash{$txo->[7]} = $token_tx ? $token_tx->{hash} : undef;
                    }
                    $utxo->{token_id} = $token_hash{$txo->[7]};
                }
                else {
                    $utxo->{token_id} = $token_hash{$txo->[6]} = $txo->[0];
                }
                _add_token_data($utxo, $txo->[8]);
            }
            elsif (defined($txo->[8]) && length($txo->[8]) > 0) {
                $utxo->{data} = $txo->[8];
            }
            $txo_chain{$txo->[0]}->[$txo->[1]] = $utxo;
            $txo_cnt++;
        }
        if (!defined($limit) && $txo_cnt >= MAX_TXO_PER_ADDRESS) {
            Infof("Too many UTXO for address %s", $address);
        }
    }
    for (my $height = QBitcoin::Block->max_db_height + 1; $height <= (QBitcoin::Block->blockchain_height // -1); $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (@{$block->transactions}) {
            for (my $num = 0; $num < @{$tx->out}; $num++) {
                my $out = $tx->out->[$num];
                next if $out->scripthash ne $scripthash;
                next unless $out->unspent;
                my $utxo = {
                    value        => $out->value,
                    block_height => $height,
                    block_pos    => $tx->block_pos,
                    tx_type      => $tx->type_as_text,
                };
                $utxo->{data} = $out->data if defined $out->data;
                if ($tx->is_tokens) {
                    $utxo->{token_id} = $tx->token_hash || $tx->hash;
                    _add_token_data($utxo, $out->data);
                }
                $txo_chain{$tx->hash}->[$num] = $utxo;
            }
            foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
                my $txid = $in->{txo}->tx_in;
                if (exists $txo_chain{$txid}) {
                    $txo_chain{$txid}->[$in->{txo}->num] = undef;
                    delete $txo_chain{$txid} unless grep { defined } @{ $txo_chain{$txid} };
                }
            }
        }
    }
    my %txo_mempool;
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        for (my $num = 0; $num < @{$tx->out}; $num++) {
            my $out = $tx->out->[$num];
            next if $out->scripthash ne $scripthash;
            next unless $out->unspent;
            my $utxo = { value => $out->value, tx_type => $tx->type_as_text };
            $utxo->{data} = $out->data if defined $out->data;
            if ($tx->is_tokens) {
                $utxo->{token_id} = $tx->token_hash || $tx->hash;
                _add_token_data($utxo, $out->data);
            }
            $txo_mempool{$tx->hash}->[$num] = $utxo;
        }
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
            my $txid = $in->{txo}->tx_in;
            if ($txo_chain{$txid}) {
                $txo_chain{$txid}->[$in->{txo}->num] = undef;
                delete $txo_chain{$txid} unless grep { defined } @{ $txo_chain{$txid} };
            }
            elsif ($txo_mempool{$txid}) {
                $txo_mempool{$txid}->[$in->{txo}->num] = undef;
                delete $txo_mempool{$txid} unless grep { defined } @{ $txo_mempool{$txid} };
            }
        }
    }

    return wantarray ? (\%txo_chain, \%txo_mempool) : \%txo_chain;
}

sub _unpack_data_value {
    if (dbh->get_info(17) eq "SQLite") {
        state $created =
            dbh->sqlite_create_function('data2int64', 1, sub {
                 return defined $_[0] ? unpack("Q<", $_[0]) : undef;
            });
        return "data2int64(SUBSTR(data, 2, 8))";
    }
    elsif (dbh->get_info(17) eq "PostgreSQL") {
        return "GET_BYTE(data, 1)::BIGINT + (GET_BYTE(data, 2)::BIGINT << 8) + (GET_BYTE(data, 3)::BIGINT << 16) + (GET_BYTE(data, 4)::BIGINT << 24) + (GET_BYTE(data, 5)::BIGINT << 32) + (GET_BYTE(data, 6)::BIGINT << 40) + (GET_BYTE(data, 7)::BIGINT << 48) + (GET_BYTE(data, 8)::BIGINT << 56)";
    }
    else { # MySQL
        return "CAST(CONV(HEX(REVERSE(SUBSTR(data, 2, 8))), 16, 10) AS UNSIGNED)";
    }
}

sub tokens_received {
    my ($address, $token_hash, $minconf) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return undef;
    my $value = 0;
    my $max_db_height = QBitcoin::Block->max_db_height;
    my $blockchain_height = QBitcoin::Block->blockchain_height // -1;
    if ((my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) && (my ($token_tx) = QBitcoin::Transaction->fetch(hash => $token_hash))) {
        my $result;
        state $unpack_value = _unpack_data_value();
        my $sql = "SELECT IFNULL(SUM($unpack_value), 0) AS value FROM `" . QBitcoin::TXO->TABLE . "` JOIN `" . QBitcoin::Transaction->TABLE . "` tx_in ON (tx_in = tx_in.id) WHERE scripthash = ? AND tx_in.tx_type = ? AND IFNULL(tx_in.token_id, tx_in.id) = ?+0 AND $SQL_IS_TOKEN_TRANSFER";
        if ($minconf && $blockchain_height - $minconf + 1 < $max_db_height) {
            $sql .= " AND tx_in.block_height <= ?";
            ($result) = dbh->selectall_array($sql, undef, $script->id, TX_TYPE_TOKENS, $token_tx->{id}, $blockchain_height - $minconf + 1);
        }
        else {
            ($result) = dbh->selectall_array($sql, undef, $script->id, TX_TYPE_TOKENS, $token_tx->{id});
        }
        $value = $result->[0] // 0;
        $value += 0;
    }
    if ($minconf && $blockchain_height - $minconf + 1 <= $max_db_height) {
        return $value;
    }

    for (my $height = $max_db_height + 1; $height <= $blockchain_height - $minconf + 1; $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (grep { $_->is_tokens && ($_->token_hash || $_->hash) eq $token_hash } @{$block->transactions}) {
            $value += sum0 map { unpack("Q<", substr($_->data, 1, 8)) }
                grep { $_->scripthash eq $scripthash && $_->is_token_transfer }
                    @{$tx->out};
        }
    }
    if (!$minconf) {
        foreach my $tx (grep { $_->is_tokens && ($_->token_hash || $_->hash) eq $token_hash } QBitcoin::Transaction->mempool_list()) {
            $value += sum0 map { unpack("Q<", substr($_->data, 1, 8)) }
                grep { $_->scripthash eq $scripthash && $_->is_token_transfer }
                    @{$tx->out};
        }
    }

    return $value;
}

sub tokens_balance {
    my ($address, $token_hash, $minconf) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return undef;
    my $value = 0;
    my $max_db_height = QBitcoin::Block->max_db_height;
    my $blockchain_height = QBitcoin::Block->blockchain_height // -1;
    my %fresh_inputs;
    my $token_id;
    if ((my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) && (my ($token_tx) = QBitcoin::Transaction->fetch(hash => $token_hash))) {
        $token_id = $token_tx->{id};
        my $result;
        my $last_tx;
        state $unpack_value = _unpack_data_value();
        if ($minconf && $blockchain_height - $minconf + 1 < $max_db_height) {
            ($last_tx) = QBitcoin::Transaction->fetch(block_height => { '<=', $blockchain_height - $minconf + 1 }, -sortby => 'id DESC', -limit => 1);
        }
        my $sql = "SELECT SUM($unpack_value) FROM `" . QBitcoin::TXO->TABLE . "` AS txo JOIN `" . QBitcoin::Transaction->TABLE . "` AS tx_in ON (tx_in.id = txo.tx_in) WHERE tx_out IS NULL AND scripthash = ? AND tx_in.tx_type = ? AND IFNULL(tx_in.token_id, tx_in.id) = ?+0 AND $SQL_IS_TOKEN_TRANSFER";
        if (defined $last_tx) {
            $sql .= " AND tx_in <= ?";
            ($result) = dbh->selectall_array($sql, undef, $script->id, TX_TYPE_TOKENS, $token_id, $last_tx->{id});
            # Store inputs that have not enough confirmations to exclude them later
            my $fresh_inputs_sql = "SELECT hash FROM `" . QBitcoin::TXO->TABLE . "` JOIN `" . QBitcoin::Transaction->TABLE . "` ON (id = tx_in) WHERE scripthash = ? AND tx_out IS NULL AND tx_in > ?";
            %fresh_inputs = map { $_->[0] => 1 } dbh->selectall_array($fresh_inputs_sql, undef, $script->id, $last_tx->{id});
        }
        else {
            ($result) = dbh->selectall_array($sql, undef, $script->id, TX_TYPE_TOKENS, $token_id);
        }
        DEBUG_ORM && Debugf("sql: [%s], values: [%s]", $sql, join(',', $script->id, TX_TYPE_TOKENS, $token_id, $last_tx ? $last_tx->{id} : ()));
        DEBUG_ORM && Debugf("result: [%u]", $result->[0] // 0);
        $value = $result->[0] // 0;
        $value += 0;
    }

    for (my $height = $max_db_height + 1; $height <= $blockchain_height; $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (@{$block->transactions}) {
            foreach my $in (grep { $_->{txo}->scripthash eq $scripthash && $_->{txo}->is_token_transfer } @{$tx->in}) {
                next if exists $fresh_inputs{$in->{txo}->tx_in};
                my $first_spent = $in->{txo}->tx_out;
                ($first_spent) = sort { $a cmp $b } map { $_->hash } $in->{txo}->spent_list unless $first_spent;
                next if $first_spent && $first_spent ne $tx->hash;
                # Decrease value if it was correct tokens output, i.e if $_->{txo}->tx_in is tokens transaction with correct token id
                if (my $tx_in = QBitcoin::Transaction->get($in->{txo}->tx_in)) {
                    next if !$tx_in->is_tokens || ( ($tx_in->token_hash || $tx_in->hash) ne $token_hash );
                }
                else {
                    my ($tx_in) = QBitcoin::Transaction->fetch(hash => $in->{txo}->tx_in);
                    next if $tx_in->{tx_type} != TX_TYPE_TOKENS || ($tx_in->{token_id} || $tx_in->{id}) != $token_id;
                }
                $value -= unpack("Q<", substr($in->{txo}->data, 1, 8));
            }
            if ($tx->is_tokens && ($tx->token_hash || $tx->hash) eq $token_hash) {
                if (!$minconf || $height <= $blockchain_height - $minconf + 1) {
                    foreach my $out (grep { $_->scripthash eq $scripthash && $_->is_token_transfer } @{$tx->out}) {
                        $value += unpack("Q<", substr($out->data, 1, 8));
                    }
                }
                elsif (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
                    $fresh_inputs{$tx->hash} = 1;
                }
            }
        }
    }
    foreach my $tx (grep { $_->is_tokens && ($_->token_hash || $_->hash) eq $token_hash } QBitcoin::Transaction->mempool_list()) {
        if (!$minconf) {
            foreach my $out (grep { $_->scripthash eq $scripthash && $_->is_token_transfer } @{$tx->out}) {
                $value += unpack("Q<", substr($out->data, 1, 8));
            }
        }
        elsif (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
            $fresh_inputs{$tx->hash} = 1;
        }
    }
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash && $_->{txo}->is_token_transfer } @{$tx->in}) {
            next if exists $fresh_inputs{$in->{txo}->tx_in};
            my $first_spent = $in->{txo}->tx_out;
            ($first_spent) = sort { $a cmp $b } map { $_->hash } $in->{txo}->spent_list unless $first_spent;
            next if $first_spent && $first_spent ne $tx->hash;
            # Decrease value if it was correct tokens output, i.e if $_->{txo}->tx_in is tokens transaction with correct token id
            if (my $tx_in = QBitcoin::Transaction->get($in->{txo}->tx_in)) {
                next if !$tx_in->is_tokens || ( ($tx_in->token_hash || $tx_in->hash) ne $token_hash );
            }
            else {
                my ($tx_in) = QBitcoin::Transaction->fetch(hash => $in->{txo}->tx_in);
                next if $tx_in->{tx_type} != TX_TYPE_TOKENS || ($tx_in->{token_id} || $tx_in->{id}) != $token_id;
            }
            $value -= unpack("Q<", substr($in->{txo}->data, 1, 8));
        }
    }

    return $value;
}

sub all_tokens_balance {
    my ($address, $minconf) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return undef;
    my %value;
    my %token_hash_by_id;
    my $max_db_height = QBitcoin::Block->max_db_height;
    my $blockchain_height = QBitcoin::Block->blockchain_height // -1;
    my %fresh_inputs;
    if (my $script = QBitcoin::RedeemScript->find(hash => $scripthash)) {
        my @result;
        my $last_tx;
        my %value_by_id;
        state $unpack_value = _unpack_data_value();
        if ($minconf && $blockchain_height - $minconf + 1 < $max_db_height) {
            ($last_tx) = QBitcoin::Transaction->fetch(block_height => { '<=', $blockchain_height - $minconf + 1 }, -sortby => 'id DESC', -limit => 1);
        }
        my $sql = "SELECT IFNULL(tx_in.token_id, tx_in.id) AS token_id, SUM($unpack_value) AS value FROM `" . QBitcoin::TXO->TABLE . "` AS txo JOIN `" . QBitcoin::Transaction->TABLE . "` AS tx_in ON (tx_in.id = txo.tx_in) WHERE tx_out IS NULL AND scripthash = ? AND tx_in.tx_type = ? AND $SQL_IS_TOKEN_TRANSFER";
        if (defined $last_tx) {
            $sql .= " AND tx_in <= ?";
            $sql .= " GROUP BY token_id";
            @result = dbh->selectall_array($sql, undef, $script->id, TX_TYPE_TOKENS, $last_tx->{id});
            # Store inputs that have not enough confirmations to exclude them later
            my $fresh_inputs_sql = "SELECT hash FROM `" . QBitcoin::TXO->TABLE . "` JOIN `" . QBitcoin::Transaction->TABLE . "` ON (id = tx_in) WHERE scripthash = ? AND tx_out IS NULL AND tx_in > ?";
            %fresh_inputs = map { $_->[0] => 1 } dbh->selectall_array($fresh_inputs_sql, undef, $script->id, $last_tx->{id});
        }
        else {
            $sql .= " GROUP BY token_id";
            @result = dbh->selectall_array($sql, undef, $script->id, TX_TYPE_TOKENS);
        }
        foreach my $result (@result) {
            $value_by_id{$result->[0]} = $result->[1]+0;
        }
        if (%value_by_id) {
            my @token_txs = QBitcoin::Transaction->fetch(
                tx_type => TX_TYPE_TOKENS,
                id      => [ keys %value_by_id ],
            );
            foreach my $token_tx (@token_txs) {
                $token_hash_by_id{$token_tx->{id}} = $token_tx->{hash};
                $value{$token_tx->{hash}} = $value_by_id{$token_tx->{id}};
            }
        }
    }

    for (my $height = $max_db_height + 1; $height <= $blockchain_height; $height++) {
        my $block = QBitcoin::Block->best_block($height)
            or next;
        foreach my $tx (@{$block->transactions}) {
            foreach my $in (grep { $_->{txo}->scripthash eq $scripthash && $_->{txo}->is_token_transfer } @{$tx->in}) {
                next if exists $fresh_inputs{$in->{txo}->tx_in};
                my $first_spent = $in->{txo}->tx_out;
                ($first_spent) = sort { $a cmp $b } map { $_->hash } $in->{txo}->spent_list unless $first_spent;
                next if $first_spent && $first_spent ne $tx->hash;
                # Decrease value if it was correct tokens output, i.e if $_->{txo}->tx_in is tokens transaction with correct token id
                my $tokens;
                if (my $tx_in = QBitcoin::Transaction->get($in->{txo}->tx_in)) {
                    next unless $tx_in->is_tokens;
                    $tokens = $tx_in->token_hash || $tx_in->hash;
                }
                else {
                    my ($tx_in) = QBitcoin::Transaction->fetch(hash => $in->{txo}->tx_in);
                    next if $tx_in->{tx_type} != TX_TYPE_TOKENS;
                    if ($tx_in->{token_id}) {
                        $tokens = $token_hash_by_id{$tx_in->{token_id}}
                            or next;
                    }
                    else {
                        $tokens = $tx_in->{hash};
                    }
                }
                $value{$tokens} -= unpack("Q<", substr($in->{txo}->data, 1, 8));
            }
            if ($tx->is_tokens) {
                if (!$minconf || $height <= $blockchain_height - $minconf + 1) {
                    foreach my $out (grep { $_->scripthash eq $scripthash && $_->is_token_transfer } @{$tx->out}) {
                        $value{$tx->token_hash || $tx->hash} += unpack("Q<", substr($out->data, 1, 8));
                    }
                }
                elsif (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
                    $fresh_inputs{$tx->hash} = 1;
                }
            }
        }
    }
    foreach my $tx (grep { $_->is_tokens } QBitcoin::Transaction->mempool_list()) {
        if (!$minconf) {
            foreach my $out (grep { $_->scripthash eq $scripthash && $_->is_token_transfer } @{$tx->out}) {
                $value{$tx->token_hash || $tx->hash} += unpack("Q<", substr($out->data, 1, 8));
            }
        }
        elsif (grep { $_->scripthash eq $scripthash } @{$tx->out}) {
            $fresh_inputs{$tx->hash} = 1;
        }
    }
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash && $_->{txo}->is_token_transfer } @{$tx->in}) {
            next if exists $fresh_inputs{$in->{txo}->tx_in};
            my $first_spent = $in->{txo}->tx_out;
            ($first_spent) = sort { $a cmp $b } map { $_->hash } $in->{txo}->spent_list unless $first_spent;
            next if $first_spent && $first_spent ne $tx->hash;
            # Decrease value if it was correct tokens output, i.e if $_->{txo}->tx_in is tokens transaction with correct token id
            my $tokens;
            if (my $tx_in = QBitcoin::Transaction->get($in->{txo}->tx_in)) {
                next unless $tx_in->is_tokens;
                $tokens = $tx_in->token_hash || $tx_in->hash;
            }
            else {
                my ($tx_in) = QBitcoin::Transaction->fetch(hash => $in->{txo}->tx_in);
                next if $tx_in->{tx_type} != TX_TYPE_TOKENS;
                if ($tx_in->{token_id}) {
                    $tokens = $token_hash_by_id{$tx_in->{token_id}}
                        or next;
                }
                else {
                    $tokens = $tx_in->{hash};
                }
            }
            $value{$tokens} -= unpack("Q<", substr($in->{txo}->data, 1, 8));
        }
    }

    return \%value;
}

sub _matched_token {
    my ($txid, $token_hash, $token_id) = @_;
    if (my $tx = QBitcoin::Transaction->get($txid)) {
        return $tx->is_tokens && ($tx->token_hash || $tx->hash) eq $token_hash;
    }
    elsif ($token_id && (($tx) = QBitcoin::Transaction->fetch(hash => $txid))) {
        return $tx->{tx_type} == TX_TYPE_TOKENS && ($tx->{token_id} || $tx->{id}) == $token_id;
    }
    else {
        return undef;
    }
}

# returns list of arrays [ txid, value, block_height ] for blockchain and [ txid, value, received_time ] for mempool
sub get_tokens_txs {
    my ($address, $token_hash, $last_seen, $chain_limit, $mempool_limit) = @_;
    my $scripthash = eval { scripthash_by_address($address) }
        or return ();
    $chain_limit //= MAX_TXO_PER_ADDRESS;
    $mempool_limit //= MAX_TXO_PER_ADDRESS;
    my @txs_chain;

    my ($skip_before, $skip_before_id);
    if ($last_seen) {
        if ($skip_before = QBitcoin::Transaction->get($last_seen)) {
            $skip_before_id = $skip_before->id; # undef if not in DB
        }
        elsif (($skip_before) = QBitcoin::Transaction->fetch(hash => $last_seen)) {
            $skip_before_id = $skip_before->{id};
        }
    }
    my $token_id;
    my $script = QBitcoin::RedeemScript->find(hash => $scripthash);
    if ($script) {
        if (my ($token_tx) = QBitcoin::Transaction->fetch(hash => $token_hash)) {
            $token_id = $token_tx->{id};
        }
    }

    my @txs_inmem;
    if (!$skip_before_id) {
        my $in_skip = $skip_before ? 1 : 0;
        for (my $height = QBitcoin::Block->blockchain_height // -1; $height > QBitcoin::Block->max_db_height; $height--) {
            my $block = QBitcoin::Block->best_block($height)
                or next;
            foreach my $tx (reverse @{$block->transactions}) {
                if ($in_skip) {
                    if ($tx->hash eq $last_seen) {
                        $in_skip = 0;
                    }
                    next;
                }
                my $tx_data;
                if ($tx->is_tokens && ($tx->token_hash || $tx->hash) eq $token_hash) {
                    for (my $num = 0; $num < @{$tx->out}; $num++) {
                        my $out = $tx->out->[$num];
                        next if $out->scripthash ne $scripthash;
                        next if !$out->is_token_transfer;
                        my $value = unpack("Q<", substr($out->data, 1, 8));
                        if ($tx_data) {
                            $tx_data->[1] += $value;
                        }
                        else {
                            $tx_data = [ $tx->hash, $value+0, $height, $tx->block_pos ];
                        }
                    }
                }
                foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
                    _matched_token($in->{txo}->tx_in, $token_hash, $token_id)
                        or next;
                    next if !$in->{txo}->is_token_transfer;
                    my $value = unpack("Q<", substr($in->{txo}->data, 1, 8));
                    if ($tx_data) {
                        $tx_data->[1] -= $value;
                    }
                    else {
                        $tx_data = [ $tx->hash, -$value, $height, $tx->block_pos ];
                    }
                }
                push @txs_inmem, $tx_data if $tx_data;
            }
        }
    }

    if (@txs_inmem < $chain_limit && $script && $token_id) {
        state $unpack_value = _unpack_data_value();
        my @txs_in = dbh->selectall_array("SELECT hash, SUM($unpack_value) AS amount, block_height, block_pos FROM `" . QBitcoin::Transaction->TABLE . "` tx JOIN `" . QBitcoin::TXO->TABLE . "` txo ON (tx_in = id) WHERE scripthash = ? AND (? IS NULL OR tx_in < ?) AND tx_type = ? AND IFNULL(token_id, id) = ?+0 AND $SQL_IS_TOKEN_TRANSFER GROUP BY tx_in ORDER BY tx_in DESC LIMIT ?", undef, $script->id, $skip_before_id, $skip_before_id, TX_TYPE_TOKENS, $token_id, $chain_limit - @txs_inmem);
        my @txs_out = dbh->selectall_array("SELECT hash, amount, block_height, block_pos FROM `" . QBitcoin::Transaction->TABLE . "` tx JOIN (SELECT tx_out, -SUM($unpack_value) AS amount FROM `" . QBitcoin::TXO->TABLE . "` txo JOIN `" . QBitcoin::Transaction->TABLE . "` tx_in ON (tx_in.id = tx_in) WHERE scripthash = ? AND tx_out IS NOT NULL AND (? IS NULL OR tx_out < ?) AND tx_type = ? AND IFNULL(token_id, id) = ?+0 AND $SQL_IS_TOKEN_TRANSFER GROUP BY tx_out ORDER BY tx_out DESC LIMIT ?) AS t ON (tx_out = id)", undef, $script->id, $skip_before_id, $skip_before_id, TX_TYPE_TOKENS, $token_id, $chain_limit - @txs_inmem);
        my %txs_in = map { $_->[0] => $_ } @txs_in;
        foreach my $tx (@txs_out) {
            if (exists $txs_in{$tx->[0]}) {
                $txs_in{$tx->[0]}->[1] += $tx->[1];
            }
            else {
                push @txs_in, $tx;
            }
        }
        undef @txs_out;
        undef %txs_in;
        @txs_chain = map [ $_->[0], $_->[1]+0, $_->[2] ],
            sort { $b->[2] <=> $a->[2] || $b->[3] <=> $a->[3] }
                @txs_inmem, @txs_in;
    }
    else {
        @txs_chain = map [ $_->[0], $_->[1], $_->[2] ], @txs_inmem;
    }
    splice @txs_chain, $chain_limit if @txs_chain > $chain_limit;
    undef @txs_inmem;

    my @txs_mempool;
    foreach my $tx (QBitcoin::Transaction->mempool_list()) {
        my $tx_data;
        if ($tx->is_tokens && ($tx->token_hash || $tx->hash) eq $token_hash) {
            for (my $num = 0; $num < @{$tx->out}; $num++) {
                my $out = $tx->out->[$num];
                next if $out->scripthash ne $scripthash;
                next if !$out->is_token_transfer;
                my $value = unpack("Q<", substr($out->data, 1, 8));
                if ($tx_data) {
                    $tx_data->[1] += $value;
                }
                else {
                    $tx_data = [ $tx->hash, $value+0, $tx->received_time ];
                }
            }
        }
        foreach my $in (grep { $_->{txo}->scripthash eq $scripthash } @{$tx->in}) {
            _matched_token($in->{txo}->tx_in, $token_hash, $token_id)
                or next;
            next if !$in->{txo}->is_token_transfer;
            my $value = unpack("Q<", substr($in->{txo}->data, 1, 8));
            if ($tx_data) {
                $tx_data->[1] -= $value;
            }
            else {
                $tx_data = [ $tx->hash, -$value, $tx->received_time ];
            }
        }
        push @txs_mempool, $tx_data if $tx_data;
        last if @txs_mempool == $mempool_limit;
    }

    return (\@txs_chain, \@txs_mempool);
}

sub get_tokens_info {
    my ($token_hash) = @_;
    my ($token_tx) = QBitcoin::Transaction->get_by_hash($token_hash)
        or return undef;
    $token_tx->is_tokens
        or return undef;
    my $data = $token_tx->token_info;
    my $res = {
        token_id => unpack("H*", $token_tx->hash),
        decimals => $data->{decimals} // TOKEN_DEFAULT_DECIMALS,
        issuer   => $token_tx->in->[0]->{txo}->address,
    };
    $res->{name} = $data->{name} if defined $data->{name};
    $res->{symbol} = $data->{symbol} if defined $data->{symbol};
    if ($token_tx->block_height) {
        if (my $block = QBitcoin::Block->best_block($token_tx->block_height) // QBitcoin::Block->find(height => $token_tx->block_height)) {
            $res->{create_time} = $block->time;
        }
    }
    # TODO: add total_supply and mint_allowed attributes
    return $res;
}

sub create_txo {
    my $out = shift;
    my @txo;
    my $token_id;
    my $token_data;
    my @token_attr;
    my $data;
    foreach my $key (keys %$out) {
        if ($key eq "token_id") {
            $token_id = pack("H*", $out->{$key});
        }
        elsif ($key eq "token_amount") {
            $token_data = TOKEN_TXO_TYPE_TRANSFER . pack("Q<", $out->{$key} );
        }
        elsif ($key eq "token_permissions") {
            my $permissions = 0;
            if (!ref($out->{$key})) {
                $permissions = hex($1) if $out->{$key} =~ /^0x([0-9a-fA-F]{2})$/;
            }
            else {
                foreach my $attr (@{$out->{$key}}) {
                    if ($attr eq "mint") {
                        $permissions |= TOKEN_PERMISSION_MINT;
                    }
                }
            }
            $token_attr[unpack("C", TOKEN_TXO_TYPE_PERMISSIONS)] = pack("C", $permissions);
        }
        elsif ($key eq "token_decimals") {
            $token_attr[unpack("C", TOKEN_TXO_TYPE_DECIMALS)] = pack("C", $out->{$key});
        }
        elsif ($key eq "token_name") {
            $token_attr[unpack("C", TOKEN_TXO_TYPE_NAME)] = pack("C", length($out->{$key})) . $out->{$key};
        }
        elsif ($key eq "token_symbol") {
            $token_attr[unpack("C", TOKEN_TXO_TYPE_SYMBOL)] = pack("C", length($out->{$key})) . $out->{$key};
        }
        elsif ($key eq "data") {
            # Raw data payload for a regular output, hex-encoded
            return undef if defined($data);
            $out->{$key} =~ /^(?:[0-9a-fA-F][0-9a-fA-F])*\z/
                or return undef;
            $data = pack("H*", $out->{$key});
        }
        elsif ($key eq "tag") {
            # Tag string label for a regular output (see TXO_DATA_TAG); used to
            # partition the genesis-reward UTXOs between validator nodes
            return undef if defined($data);
            length($out->{$key})
                or return undef;
            $data = TXO_DATA_TAG . $out->{$key};
        }
        elsif (is_btc_address($key)) {
            # Bitcoin address as destination: create a trustless-downgrade freeze
            # output. data = [reclaim_id (32)][btc scriptPubKey]. The scriptPubKey is
            # what the BTC payment must pay (any address format; consensus byte-compares
            # it, never interprets it). reclaim_id is left as a 32-byte zero sentinel and
            # filled with the first signing key's pubkey hash256 at signrawtransactionwithkey.
            my $spk = btc_address_to_scriptpubkey($key)
                or return undef;
            my $value = $out->{$key};
            $value =~ /^[0-9]+$/ && $value <= MAX_VALUE
                or return undef;
            push @txo, {
                scripthash => hash160(QBT_FREEZE_SCRIPT),
                value      => $value,
                data       => ("\x00" x 32) . $spk,
            };
        }
        elsif (my $scripthash = eval { scripthash_by_address($key) }) {
            $out->{$key} =~ /^[0-9]+$/ && $out->{$key} <= MAX_VALUE
                or return undef;
            # +0: the client may send the amount as a decimal string (and the regex
            # match above marks even a JSON number as string), numify for JSON encoding
            push @txo, { scripthash => $scripthash, value => $out->{$key}+0 };
        }
        else {
            return undef;
        }
    }
    if (defined($data)) {
        # data/tag labels exactly one regular address output
        return undef if defined($token_id);
        @txo == 1 && !defined($txo[0]->{data})
            or return undef;
        length($data) <= MAX_TXO_DATA_SIZE
            or return undef;
        $txo[0]->{data} = $data;
    }
    if (defined($token_id)) {
        @txo == 1 or return undef;
        if (@token_attr) {
            return undef if defined($token_data);
            $token_data = "";
            foreach my $attr (0 .. $#token_attr) {
                next unless defined $token_attr[$attr];
                $token_data .= pack("C", $attr) . $token_attr[$attr];
            }
        }
        $txo[0]->{data} = $token_data;
    }
    return ([ map { QBitcoin::TXO->new_txo($_) } @txo ], $token_id);
}

# Returns ($error, $warning). $error is set for transactions the consensus would
# reject (mint without permission, overflow, malformed data); burning tokens is
# consensus-legal, so it is reported as $warning and the transaction is allowed.
sub check_tx_tokens_balance {
    my ($tx) = @_;

    my $in_value = 0;
    my $permissions = 0;
    my @warnings;
    foreach my $txo (map { $_->{txo} } grep { defined($_->{txo}->token_hash) && length($_->{txo}->data // "") > 0 } @{$tx->in}) {
        if (!$txo->token_hash || !$tx->is_tokens || $txo->token_hash ne $tx->token_hash) {
            # sprintf %u: concatenating the num accessor return would mark the live
            # txo's num field as string for JSON encoding
            push @warnings, sprintf("Input %s:%u burns tokens", $txo->tx_in_str, $txo->num);
            next;
        }
        my $txo_type = substr($txo->data, 0, 1);
        if ($txo_type eq TOKEN_TXO_TYPE_TRANSFER) {
            length($txo->data) == 9 or next;
            my $transfer_value = unpack("Q<", substr($txo->data, 1, 8));
            if ($transfer_value > MAX_UINT64 - $in_value) {
                return (sprintf("Input %s:%u causes integer overflow", $txo->tx_in_str, $txo->num));
            }
            $in_value += $transfer_value;
        }
        elsif ($txo_type eq TOKEN_TXO_TYPE_PERMISSIONS) {
            if (length($txo->data) >= 2) {
                my $in_permissions = unpack("C", substr($txo->data, 1, 1));
                $permissions |= $in_permissions;
            }
        }
    }
    if ($tx->is_tokens && $tx->token_hash) {
        my $out_value = 0;
        foreach my $out (grep { length($_->data // "") > 0 } @{$tx->out}) {
            my $txo_type = substr($out->data, 0, 1);
            if ($txo_type eq TOKEN_TXO_TYPE_TRANSFER) {
                length($out->data) == 9 or return ("Incorrect data length in token transfer output");
                my $transfer_value = unpack("Q<", substr($out->data, 1, 8));
                if ($transfer_value > MAX_UINT64 - $out_value) {
                    return ("Output causes integer overflow");
                }
                $out_value += $transfer_value;
            }
            else {
                my $data = $tx->unpack_token_info($out->data)
                    or return ("Incorrect data in token output");
                if ($data->{permissions} && ($data->{permissions} & ~$permissions)) {
                    return ("Attempt to gain token permission");
                }
                if ($data->{decimals} || $data->{symbol} || $data->{name}) {
                    return ("Attempt to change token attributes");
                }
            }
        }
        if ($in_value < $out_value && !($permissions & TOKEN_PERMISSION_MINT)) {
            return ("Attempt to mint tokens without permission");
        }
        if ($in_value > $out_value) {
            push @warnings, "Transaction burns " . ($in_value - $out_value) . " token units";
        }
    }
    return (undef, @warnings ? join("; ", @warnings) : undef);
}

# estimate_fees(@targets) - estimate fee rates for given confirmation targets
# Returns ($result, $error) where $result is { target => feerate_sat_per_byte } or undef on error
sub estimate_fees {
    my (@targets) = @_;
    blockchain_synced() && mempool_synced()
        or return (undef, "Blockchain is not synced");
    my $height = QBitcoin::Block->blockchain_height;
    defined $height
        or return (undef, "Blockchain is not initialized");
    my $best_block = QBitcoin::Block->best_block($height)
        or return (undef, "Best block not found");
    my @mempool = QBitcoin::Transaction->mempool_list();
    my $min_fee_rate = ( $best_block->min_fee > MIN_FEE ? $best_block->min_fee : MIN_FEE ) / 1024;
    my %result;
    my @sorted = sort { $b->fee / $b->size <=> $a->fee / $a->size }
        grep { ($_->is_standard || $_->is_tokens) && $_->fee / $_->size > $min_fee_rate } @mempool;
    my $total_size = sum0 map { $_->size } @sorted;
    my $block_size = $best_block->size;
    my $acc_size = 0;
    my $tx_idx = 0;
    foreach my $target (sort { $a <=> $b } @targets) {
        if (@mempool < $target) {
            $result{$target} = MIN_FEE / 1024;
            next;
        }
        if ($total_size < $target * $block_size) {
            $result{$target} = $min_fee_rate;
            next;
        }
        my $target_size = $target * $block_size;
        while ($tx_idx < @sorted && $acc_size < $target_size) {
            $acc_size += $sorted[$tx_idx]->size;
            $tx_idx++;
        }
        if ($tx_idx > 0) {
            $result{$target} = $sorted[$tx_idx - 1]->fee / $sorted[$tx_idx - 1]->size;
            $result{$target} = $min_fee_rate if $result{$target} < $min_fee_rate;
        }
        else {
            $result{$target} = $min_fee_rate;
        }
    }
    return (\%result, undef);
}

1;
