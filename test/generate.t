#! /usr/bin/env perl
use warnings;
use strict;

use FindBin '$Bin';
use lib ("$Bin/../lib", "$Bin/lib");

use Test::More;
use Test::MockModule;
use QBitcoin::Test::ORM;
use QBitcoin::Test::BlockSerialize qw(block_hash);
use QBitcoin::Test::Send qw(send_tx send_block $last_tx);
use QBitcoin::Const;
use QBitcoin::Config;
use QBitcoin::BlockchainParams;
use QBitcoin::Transaction;
use QBitcoin::Block;
use QBitcoin::Generate;
use QBitcoin::Crypto qw(generate_keypair);
use QBitcoin::Address qw(wallet_import_format addresses_by_pubkey);
use QBitcoin::MyAddress;
use QBitcoin::Coins;

#$config->{debug} = 1;
$config->{regtest} = 1;
$config->{genesis} = 1;
$config->{genesis_reward} = GENESIS_REWARD;

my $transaction_module = Test::MockModule->new('QBitcoin::Transaction');
$transaction_module->mock('validate_coinbase', sub { 0 });

my $static_reward = 200000000;
my $block_module = Test::MockModule->new('QBitcoin::Block');
$block_module->mock('static_reward', sub { $static_reward });

my $pk = generate_keypair(CRYPT_ALGO_ECDSA);
my $pubkey = $pk->pubkey_by_privkey;
my ($address) = addresses_by_pubkey($pubkey, CRYPT_ALGO_ECDSA);
my $myaddr = QBitcoin::MyAddress->create({
    private_key => wallet_import_format($pk->pk_serialize),
    address     => $address,
    staked      => 1,
});

QBitcoin::Coins->init();
is(QBitcoin::Coins->total(), 0, "No coins before genesis");

my $time = GENESIS_TIME;
block_hash("a0");
my $block0 = QBitcoin::Generate->generate($time);
ok($block0, "Genesis block generated");

my $stake_tx = send_tx(-$static_reward);
undef $last_tx;
my $tx = send_tx(0);
my $tx2 = send_tx(0, $tx, $myaddr->redeem_script);
send_block(1, "a1", "a0", 5, $stake_tx, $tx, $tx2);
is(QBitcoin::Block->blockchain_height, 1, "Block a1 received");

is(QBitcoin::Coins->total(), 2*$static_reward, "Total coins is " . (2*$static_reward));

block_hash("b1");
my $block1 = eval { QBitcoin::Generate->generate($time + BLOCK_INTERVAL) };
ok($block1, "Alternative block b1 generated");
is(scalar(@{$block1->transactions}), 2, "Generated block contains 2 transactions");
is(QBitcoin::Block->best_block->hash, "b1", "Block 1 altered");
$block1->{self_weight} = $block1->{self_weight} - 1;
$block1->{weight} = $block1->{weight} - 1;

my $tx3 = send_tx(10, undef);
block_hash("c1");
my $block2 = eval { QBitcoin::Generate->generate($time + BLOCK_INTERVAL) };
ok($block2, "Alternative block c1 generated");
is(scalar(@{$block2->transactions}), 4, "Generated block contains 4 transactions");
is(QBitcoin::Block->best_block->hash, "c1", "Block 1 altered");

is(QBitcoin::Coins->total(), 2*$static_reward, "Total coins is " . (2*$static_reward));

done_testing();
